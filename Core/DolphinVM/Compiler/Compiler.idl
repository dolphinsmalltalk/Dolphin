// Compiler.idl : IDL source for DolphinCR???.dll
//

// This file will be processed by the MIDL tool to
// produce the type library (Compiler.tlb) and marshalling code.

import "oaidl.idl";
import "ocidl.idl";

cpp_quote("#if !defined(__cplusplus)")
typedef unsigned char char8_t;
cpp_quote("#endif")

[
	uuid(446CBAE8-D3AD-4D40-9A50-20D725195AF7),
	version(8.0),
	helpstring("Dolphin Smalltalk Compiler 8.0")
]
library CompilerLib
{
	importlib("stdole32.tlb");
	importlib("stdole2.tlb");

	cpp_quote("#ifndef POTE_DEFINED")
	typedef void* POTE;
	cpp_quote("#define POTE_DEFINED")
	cpp_quote("#endif")
	
	typedef UINT_PTR Oop;

	typedef enum ReservedCodeRanges {
		LErrMin = 1,
		LErrMax = 32,
		CErrMin,
		CErrMax = 999,
		CWarnMin,
		CWarnMax = 1999,
		CInfoMin,
		CInfoMax = 2999
	} ReservedCodeRanges;

	typedef enum LexErrors { 
		LErrBadChar = LErrMin,
		LErrCommentNotClosed,
		LErrStringNotClosed,
		LErrExpectConst,
		LErrExpectExtendedLiteral,
		LErrExpectMetaVariable,
		LErrExpectChar,
		LErrBadNumber=10,
		LErrExpectCodePoint,
		LErrBadCodePoint
	} LexErrors;

	typedef enum ParseErrors {
		CErrTempListNotClosed = CErrMin,
		CErrInvalExprStart,
		CErrNonsenseAtMethodEnd,
		CErrNonsenseAtExprEnd,
		CErrBraceNotClosed,
		CErrParenNotClosed,
		CErrExpectMessage,
		CErrUnterminatedStatement,
		CErrExpectVariable,
		CErrBlockArgListNotClosed,
		CErrBlockNotClosed,
		CErrExpectPrimIdx,
		CErrBadPrimIdx,
		CErrExpectCloseTag,
		CErrExpectCallConv,
		CErrUnsupportedCallConv=48,
		CErrExpectFnName,
		CErrExpectExtType,
		CErrArgTypeCannotBeVoid,
		CErrNotIndirectable,
		CErrInvalidStructArg = 54,
		CErrBadExtTypeQualifier,
		CErrInsufficientArgTypes,
		CErrTooManyArgTypes,
		CErrExpectVfn,
		CErrBadVfn,
		CErrBadTokenInArray,
		CErrArrayNotClosed,
		CErrBadValueInByteArray,
		CErrByteArrayNotClosed,
		CErrErrorInStaticExpression,
		CErrTooManyIfNotNilBlockArgs,
		CErrInternal,
		CErrAssignmentToArgument,
		CErrTooManyArgs,
		CErrTooManyTemps,
		CErrRedefiningPseudoVar,
		CErrUndeclared,
		CErrAssignConstant,
		CErrStaticExprNotClosed,
		CErrBadSelector,
		CErrBadContext=76,
		CErrIncorrectBlockArgCount,
		CErrExpectLiteralBlock,
		CErrMethodTooLarge,
		CErrTooManyLiterals,
		CErrBlockNestingTooDeep,
		CErrBadPools=83,
		CErrDuplicateTempName=85,
		CErrDuplicateArgName,
		CErrRedefiningArg,
		CErrEmptyConditionBlock,
		CErrQualifiedRefNotClosed,
		CErrBadQualifiedRefModifier,
		CErrExpectAnnotationArg,
		CErrExpectNamespace
	} ParseErrors;

	typedef enum ParseWarnings {
		CWarnRedefiningArg = CWarnMin,
		CWarnRedefiningTemp,
		CWarnRedefiningInstVar,
		CWarnRedefiningStatic,
		CWarnMsgUnimplemented,
		CWarnUndefinedSelector,
		CWarnRestrictedSelector,
		CWarnUnreferencedTemp,
		CWarnReadNotWritten,
		CWarnReadBeforeWritten,
		CWarnWrittenNotRead,
		CWarnExpectNiladicBlockArg,
		CWarnExpectMonadicBlockArg,
		CWarnExpectMonadicOrNiladicBlockArg,
		CWarnExpectNiladicBlockReceiver,
		CWarnUnreachable,
		CWarnUnimplementedAbstractMethod,
		CWarnUsesBooleanClass,
		CWarnMutableIgnored,
		CWarnNonCharCodePoint,
	} ParseWarnings;

	typedef enum ParseInformation {
		CInfoUnreferencedArg = CInfoMin,
		CInfoSupersendsOther,
		CInfoHardBreakpoint
	} ParseInformation;

	typedef enum FirstAndLastCodes {
		LErrFirst = LErrBadChar,
		LErrLast = LErrBadNumber,
		CErrFirst = CErrTempListNotClosed,
		CErrLast = CErrRedefiningArg,
		CWarnFirst = CWarnRedefiningArg,
		CWarnLast = CWarnUsesBooleanClass,
		CInfoFirst = CInfoUnreferencedArg,
		CInfoLast = CInfoHardBreakpoint
	} FirstAndLastCodes;

	cpp_quote("#define class_CompilerFlags class CompilerFlags")
	typedef enum class_CompilerFlags
	{Default=0x00, SyntaxCheckOnly=0x02, NoOptimize=0x08, TextMap=0x10, TempsMap=0x20, DebugMethod=0x40, Boot=0x80, ScanOnly=0x100, Interactive=0x200, SendYourself=0x400, NoRefOnlyLiterals=0x800 } FLAGS;
	cpp_quote("#undef class_CompilerFlags")

	[
		object,
		uuid(c389a32d-bf21-4c47-865a-34b45b3ea03a),
		pointer_default(unique)
	]
	interface ICompiler: IUnknown
	{
		POTE CompileForClass([in]IUnknown* piVM, [in]Oop compiler, [in]const char8_t* compiletext, [in]int length, [in]POTE aClass, [in]POTE aNamespace, [in]FLAGS flags, [in]Oop notifier);
		POTE CompileForEval([in]IUnknown* piVM, [in]Oop compiler, [in]const char8_t* compiletext, [in]int length, [in]POTE aClass, [in]POTE aNamespace, [in]POTE workspacePools, [in]FLAGS flags, [in]Oop notifier);
	};

	[
		//uuid(CF88CAFC-2993-11D3-8366-6E485B000000)	// 3.05
		//uuid(C47CCC7F-A6B6-401E-8853-F1EC1D20114D)	// 4.x
		//uuid(77A96CF7-0709-4EE5-ABCD-EF11735CEA0D)	// 5.0
		//uuid(52DE08AC-F761-4D50-9FC5-964156F8A0D5)	// 5.04
		//uuid(B1401906-004A-4D84-8A8D-7D7D5F7FEC92)	// 6.0
		//uuid(F27D15BD-B8EB-488C-9932-C7FA0C598640)	// 7.0
		uuid(84b13fd5-cd7c-48a6-b8da-bcb5dcfa05ea)		// 8.0
	]
	coclass DolphinCompiler
	{
		[default] interface ICompiler;
	};
};
