| package |
package := Package name: 'Dolphin STON Tests'.
package paxVersion: 1;
	preDeclareClassesOnLoad: false;
	basicComment: 'Dolphin STON SUnit tests
Copyright (c) Object Arts Ltd, 2019

Tests of Dolphin specific aspects of the STON implementation.'.

package methodNames
	add: #Array -> #asDictionary;
	add: #ByteArrayTest -> #testHex;
	add: #Collection -> #atRandom;
	add: #Integer -> #atRandom;
	add: #SequenceableCollection -> #asFloatArray;
	add: #SequenceableCollection -> #asIntegerArray;
	add: #SequenceableCollection -> #atRandom;
	add: #SequenceableCollection -> #doWithIndex:;
	add: #STONReaderTest -> #testColorBackwardsCompatibility;
	add: #STONReaderTest -> #testColorInvalid;
	add: #STONReaderTest -> #testFloatsNonFinite;
	add: #STONReaderTest -> #testReferenceCycle2;
	add: #STONReaderTest -> #testSymbolMultiByte;
	add: #STONWriteReadTest -> #testColorReferences;
	add: #STONWriteReadTest -> #testCompiledMethodsAndBlocks;
	add: #STONWriteReadTest -> #testDynamicLinkLibraries;
	add: #STONWriteReadTest -> #testExtendedCharacters;
	add: #STONWriteReadTest -> #testFloats2;
	add: #STONWriteReadTest -> #testFloatsNonFinite;
	add: #STONWriteReadTest -> #testFramingCalculation;
	add: #STONWriteReadTest -> #testNamedColors;
	add: #STONWriteReadTest -> #testPluggableColors;
	add: #STONWriteReadTest -> #testProcessor;
	add: #STONWriteReadTest -> #testSearchPolicy;
	add: #STONWriteReadTest -> #testSortedCollections;
	add: #STONWriteReadTest -> #testSymbols2;
	add: #STONWriteReadTest -> #testSystemDictionary;
	add: #STONWriterTest -> #testColorReferences;
	add: #STONWriterTest -> #testFloatsNonFinite;
	add: #STONWriterTest -> #testFramingCalculation;
	add: #STONWriterTest -> #testIsSimpleSymbol2;
	add: #STONWriterTest -> #testLegacyDate;
	add: #STONWriterTest -> #testProcessor;
	add: #STONWriterTest -> #testSearchPolicy;
	add: #STONWriterTest -> #testSymbolMultiByte;
	add: 'STONTestDomainObject class' -> #icon;
	add: 'STONTestKnownObject class' -> #icon;
	add: 'STONTestUser class' -> #icon;
	add: 'String class' -> #crlf;
	add: 'Symbol class' -> #hasInterned:ifTrue:;
	add: 'Time class' -> #hour:minute:second:;
	add: 'Time class' -> #hour:minute:second:nanoSecond:;
	yourself.

package binaryGlobalNames: (Set new
	yourself).

package globalAliases: (Set new
	yourself).

package setPrerequisites: #(
	'..\..\Base\Dolphin'
	'..\..\Base\Tests\Dolphin Base Tests'
	'..\..\Base\Dolphin Legacy Date & Time'
	'..\..\MVP\Base\Dolphin MVP Base'
	'..\Random\Dolphin Random Stream'
	'..\..\..\..\Contributions\svenc\STON\STON-Core'
	'..\..\..\..\Contributions\svenc\STON\STON-Tests').

package!

"Loose Methods"!

!Array methodsFor!

asDictionary
	"Private - Questionable conversion message from Pharo that works only it the target collection contains associations (or at least elements that understand #key and #value). 
	It is here only for the STON tests."

	^Dictionary withAll: self! !

!Array categoriesForMethods!
asDictionary!private! !
!

!ByteArrayTest methodsFor!

testHex
	self assert: #[122 43 213 7] hex equals: '7a2bd507'.
	self
		assert: #[151 193 242 221 249 32 153 72 179 41 49 154 48 193 99 134] hex
		equals: '97c1f2ddf9209948b329319a30c16386'.
	self
		assert: (ByteArray readHexFrom: '7A2BD507')
		equals: #[122 43 213 7].
	self
		assert: (ByteArray readHexFrom: '7a2bd507')
		equals: #[122 43 213 7]! !

!ByteArrayTest categoriesForMethods!
testHex!public! !
!

!Collection methodsFor!

atRandom
	| r i |
	r := self size atRandom.
	i := 1.
	self do: [:each | i == r ifTrue: [^each]].
	^self errorEmptyCollection! !

!Collection categoriesForMethods!
atRandom!public! !
!

!Integer methodsFor!

atRandom
	"Answer a random integer from 1 to self."

	^(Random default next * self) truncated + 1! !

!Integer categoriesForMethods!
atRandom!public! !
!

!SequenceableCollection methodsFor!

asFloatArray
	^FLOATArray withAll: self!

asIntegerArray
	^SDWORDArray withAll: self!

atRandom
	^self at: self size atRandom!

doWithIndex: aDyadicValuable 
	"For Pharo compatibility; #keysAndValuesDo: is preferred (and faster)"

	self keysAndValuesDo: [:index :value | aDyadicValuable value: value value: index]! !

!SequenceableCollection categoriesForMethods!
asFloatArray!public! !
asIntegerArray!public! !
atRandom!public! !
doWithIndex:!public! !
!

!STONReaderTest methodsFor!

testColorBackwardsCompatibility
	"ensure the older Color representation still works"

	self assert: (self materialize: 'Color{#rgb:1072693248}') equals: Color red.
	self assert: (self materialize: 'Color{#rgb:1072693248}') equals: (self materialize: 'Color[#red]')
!

testColorInvalid
	"This is a handy test of DolphinSTONReader>>parseListSingleton"
	self should: [self materialize: 'Color[#red,#blue]'] raise: STONReaderError matching: [:ex | ex description = 'At character 11: ] expected'].
	self should: [self materialize: 'Color[]'] raise: STONReaderError matching: [:ex | ex description = 'At character 7: invalid input']
!

testFloatsNonFinite
	self assert: (self materialize: 'Float[#nan]') isNaN.
	self assert: (self materialize: 'Float[#infinity]') equals: Float infinity.
	self assert: (self materialize: 'Float[#negativeInfinity]') equals: Float negativeInfinity!

testReferenceCycle2
	| object |
	object := self materialize: 'Link{#nextLink:@1}'.
	self assert: object class equals: Link.
	self assert: object nextLink identicalTo: object
!

testSymbolMultiByte
	"https://github.com/svenvc/ston/issues/25"

	self assert: (self materialize: '#''£''') equals: #'£'.
	self assert: (self materialize: '#''€''') equals: Character euro asSymbol.
	self assert: (self materialize: '#''你好''') equals: #'你好'.
	self assert: (self materialize: '#''🐬''') equals: #'🐬'! !

!STONReaderTest categoriesForMethods!
testColorBackwardsCompatibility!public!tests! !
testColorInvalid!public!tests! !
testFloatsNonFinite!public!tests! !
testReferenceCycle2!public!tests! !
testSymbolMultiByte!public!tests! !
!

!STONTestDomainObject class methodsFor!

icon
	"Answers an Icon that can be used to represent this class"

	^STONTest icon! !

!STONTestDomainObject class categoriesForMethods!
icon!constants!public! !
!

!STONTestKnownObject class methodsFor!

icon
	"Answers an Icon that can be used to represent this class"

	^STONTest icon! !

!STONTestKnownObject class categoriesForMethods!
icon!constants!public! !
!

!STONTestUser class methodsFor!

icon
	"Answers an Icon that can be used to represent this class"

	^STONTest icon! !

!STONTestUser class categoriesForMethods!
icon!constants!public! !
!

!STONWriteReadTest methodsFor!

testColorReferences
	| light lighter |
	light := Color red lighter lighter.
	lighter := light lighter.
	self serializeAndMaterialize: {Color red. light. lighter. light. Color red. lighter}!

testCompiledMethodsAndBlocks
	self serializeAndMaterialize: self class methodDictionary!

testDynamicLinkLibraries
	| libs |
	libs := STON listClass withAll: {KernelLibrary default.
						{KernelLibrary default}.
						CompilerLibrary default.
						VMLibrary default.
						CRTLibrary default.
						CRTLibrary nonblocking}.
	self serializeAndMaterialize: libs!

testExtendedCharacters
	| characters |
	characters := {$£. $\x6587. $\x1F37A. $£. $\x394}.
	self serializeAndMaterialize: characters!

testFloats2
	{0.022999999999999854.
		4.999999999999996.
		-4.999999999999996.
		0.333333333333.
		Float zero.
		Float negativeZero.
		Float fmin.
		Float fmax.
		Float pi.
		Float e.
		0.00001} do: [:each | self serializeAndMaterialize: {each. each}]!

testFloatsNonFinite
	self serializeAndMaterialize: {Float infinity. Float negativeInfinity}.
	self assert: (self materialize: (self serialize: Float nan)) isNaN!

testFramingCalculation
	| objects |
	objects := {FramingCalculation fixedParentLeft. FramingCalculation relativeParentHeight}.
	self serializeAndMaterialize: objects!

testNamedColors
	| objects |
	"A mix of SystemColors, ThemeColors, RGBs, etc."
	objects := {Color face3d.  Color named: #smalltalkSystem. Color named: #darkGreen. Color default. Color none}.
	self serializeAndMaterialize: objects.
	"IndexedColors will materialize as the equivalent RGB. As IndexedColors are effectively deprecated, this is OK"
	self assert: (self materialize: (self serialize: (Color named: #maroon))) equals: Color maroon!

testPluggableColors
	| objects |
	objects := {PluggableColor with: [Color window lighter]}.
	self serializeAndMaterialize: objects!

testProcessor
	self serializeAndMaterialize: {'Foo'. Processor. 'Bar'}!

testSearchPolicy
	| objects |
	objects := { SearchPolicy always. SearchPolicy association. SearchPolicy method}.
	self serializeAndMaterialize: objects!

testSortedCollections
	| collections materialized |
	collections := STON listClass withAll: {SortedCollection new.
						SortedCollection new: 0.
						#(5 3 7 2 1 4 10 9 8 6) asSortedCollection.
						#(5 3 7 2 1 4 10 9 8 6) asSortedCollection: [:a :b | a < b].
						#(5 3 7 2 1 4 10 9 8 6) asSortedCollection: [:a :b | a > b].
						#('****' '*' '*****' '**' '***') asSortedCollection: [:a :b | a size < b size].
						#('****' '*' '*****' '**' '***') asSortedCollection: [:a :b | a size > b size].
						(#(nil 5 3 7 2 1 4 10 nil 9 8 6)
							asSortedCollection: [:a :b | a isNil or: [b notNil and: [a < b]]])
							removeFirst;
							yourself.
						(#(5 3 7 2 1 4 10 nil 9 8 nil 6)
							asSortedCollection: [:a :b | a isNil or: [b notNil and: [a > b]]])
							removeLast;
							yourself}.
	materialized := self serializeAndMaterialize: collections.
	"Verify the sort algo was restored correctly."
	materialized do: 
			[:each |
			| before sorted |
			before := each asArray.
			sorted := each reSort asArray.
			self assert: before equals: sorted]!

testSymbols2
	| symbols |
	"simple symbols"
	symbols := #( #foo123 #'123foo' #'punctuation-_./' #'_Foo' #'/root' #'---' #'.st' ).
	self serializeAndMaterialize: symbols.
	"non-simple symbols"
	symbols := #( #'les-élèves-français' #'euro-€' #'ångström' ).
	self serializeAndMaterialize: symbols.!

testSystemDictionary
	self serializeAndMaterialize: {'Foo'. Smalltalk. 'Bar'}! !

!STONWriteReadTest categoriesForMethods!
testColorReferences!public!tests! !
testCompiledMethodsAndBlocks!public!tests! !
testDynamicLinkLibraries!public!tests! !
testExtendedCharacters!public!tests! !
testFloats2!public!tests! !
testFloatsNonFinite!public!tests! !
testFramingCalculation!public!tests! !
testNamedColors!public!tests! !
testPluggableColors!public!tests! !
testProcessor!public!tests! !
testSearchPolicy!public!tests! !
testSortedCollections!public!tests! !
testSymbols2!public!tests! !
testSystemDictionary!public!tests! !
!

!STONWriterTest methodsFor!

testColorReferences
	| serialized light |
	light := Color red lighter lighter.
	serialized := self serialize: {Color red. light. light. light. Color red. light}.
	self assert: serialized
		equals: '[Color[#red],Color{#red:1.0,#green:0.063,#blue:0.063,#alpha:1.0},@3,@3,@2,@3]'!

testFloatsNonFinite
	self assert: (self serialize: Float nan) equals: 'Float[#nan]'.
	self assert: (self serialize: Float infinity) equals: 'Float[#infinity]'.
	self assert: (self serialize: Float negativeInfinity) equals: 'Float[#negativeInfinity]'!

testFramingCalculation
	self assert: (self serialize: FramingCalculation fixedParentLeft) equals: 'FramingCalculation[#fixedParentLeft]'!

testIsSimpleSymbol2
	self deny: (STON writer isSimpleSymbol: #'£').
	self deny: (STON writer isSimpleSymbol: Character euro asSymbol).
	self deny: (STON writer isSimpleSymbol: Character dolphin asSymbol).
!

testLegacyDate
	| date tz offset |
	offset := Locale timeZoneInformation offset.
	tz := offset = 0 seconds ifTrue: ['Z'] ifFalse: [offset printStringFormat: '+hh:mm'].
	date := Date
				year: 2012
				month: 1
				day: 1.
	self assert: (self serialize: date) equals: ('Date[''2012-01-01<1s>'']' expandMacrosWith: tz)!

testProcessor
	self assert: (self serialize: Processor) equals: 'ProcessorScheduler'!

testSearchPolicy
	self assert: (self serialize: SearchPolicy caseInsensitive) equals: 'SearchPolicy[#caseInsensitive]'!

testSymbolMultiByte
	"https://github.com/svenvc/ston/issues/25"

	self assert: (self serialize: #'£') equals: '#''£'''.
	self assert: (self serialize: Character euro asSymbol) equals: '#''€'''.
	self assert: (self serialize: #'你好') equals: '#''你好'''.
	self assert: (self serialize: #'🐬') equals: '#''🐬'''! !

!STONWriterTest categoriesForMethods!
testColorReferences!public!tests! !
testFloatsNonFinite!public!tests! !
testFramingCalculation!public!tests! !
testIsSimpleSymbol2!public!tests! !
testLegacyDate!public!tests! !
testProcessor!public!tests! !
testSearchPolicy!public!tests! !
testSymbolMultiByte!public!tests! !
!

!String class methodsFor!

crlf
	^##(AnsiString with: $\r with: $\n)! !

!String class categoriesForMethods!
crlf!public! !
!

!Symbol class methodsFor!

hasInterned: aString ifTrue: aMonadicValuable 
	^(self findInterned: aString) ifNil: [false] ifNotNil: [:symbol | aMonadicValuable value: symbol. true]! !

!Symbol class categoriesForMethods!
hasInterned:ifTrue:!public! !
!

!Time class methodsFor!

hour: anInteger minute: anInteger2 second: anInteger3 
	^self hours: anInteger minutes: anInteger2 seconds: anInteger3!

hour: hoursInteger minute: minutesInteger second: secondsInteger nanoSecond: nanosInteger
	^self fromSeconds: (hoursInteger * 60 + minutesInteger) * 60 + secondsInteger + (nanosInteger / 1e9)! !

!Time class categoriesForMethods!
hour:minute:second:!public! !
hour:minute:second:nanoSecond:!public! !
!

"End of package definition"!

