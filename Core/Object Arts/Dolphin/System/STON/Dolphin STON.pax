| package |
package := Package name: 'Dolphin STON'.
package paxVersion: 1;
	preDeclareClassesOnLoad: false;
	basicComment: 'Dolphin STON support
Copyright (c) Object Arts Ltd, 2019'.

package classNames
	add: #DolphinSTONCStyleCommentsSkipStream;
	add: #DolphinSTONReader;
	add: #DolphinSTONReaderError;
	add: #DolphinSTONReference;
	add: #DolphinSTONWriter;
	yourself.

package methodNames
	add: #BlockClosure -> #fromSton:;
	add: #BlockClosure -> #stonOn:;
	add: #ByteArray -> #hex;
	add: #Color -> #stonOn:;
	add: #CompiledCode -> #fromSton:;
	add: #CompiledCode -> #stonOn:;
	add: #Date -> #stonOn:;
	add: #FramingCalculation -> #stonOn:;
	add: #GUID -> #stonOn:;
	add: #IconImageManager -> #stonOn:;
	add: #MessageSequence -> #stonOn:;
	add: #ProcessorScheduler -> #stonOn:;
	add: #ReferenceView -> #stonExtras;
	add: #SingletonSearchPolicy -> #stonOn:;
	add: #SmalltalkSystem -> #stonOn:;
	add: #STONCStyleCommentsSkipStream -> #skipSeparators;
	add: #SystemDictionary -> #stonOn:;
	add: #View -> #stonExtras;
	add: #View -> #stonOn:;
	add: 'ByteArray class' -> #readHexFrom:;
	add: 'CardLayout class' -> #stonAllInstVarNames;
	add: 'Color class' -> #fromSton:;
	add: 'Color class' -> #stonName;
	add: 'ControlView class' -> #stonAllInstVarNames;
	add: 'Date class' -> #fromSton:;
	add: 'FramingCalculation class' -> #fromSton:;
	add: 'GraphicsTool class' -> #stonAllInstVarNames;
	add: 'GUID class' -> #fromSton:;
	add: 'IconImageManager class' -> #fromSton:;
	add: 'MessageSequence class' -> #fromSton:;
	add: 'Model class' -> #stonAllInstVarNames;
	add: 'Object class' -> #fromSton:;
	add: 'ProcessorScheduler class' -> #fromSton:;
	add: 'ReferenceView class' -> #stonAllInstVarNames;
	add: 'ScintillaView class' -> #stonAllInstVarNames;
	add: 'SearchPolicy class' -> #fromSton:;
	add: 'SingletonSearchPolicy class' -> #stonName;
	add: 'STON class' -> #icon;
	add: 'STONCStyleCommentsSkipStream class' -> #icon;
	add: 'STONCStyleCommentsSkipStream class' -> #new;
	add: 'STONJSON class' -> #icon;
	add: 'STONReader class' -> #icon;
	add: 'STONReader class' -> #new;
	add: 'STONReference class' -> #icon;
	add: 'STONStreamWriter class' -> #icon;
	add: 'STONWriter class' -> #icon;
	add: 'STONWriter class' -> #new;
	add: 'SystemDictionary class' -> #fromSton:;
	add: 'Toolbar class' -> #stonAllInstVarNames;
	add: 'View class' -> #stonAllInstVarNames;
	yourself.

package binaryGlobalNames: (Set new
	yourself).

package globalAliases: (Set new
	yourself).

package setPrerequisites: #(
	'..\..\IDE\Base\Development System'
	'..\..\Base\Dolphin'
	'..\..\MVP\Views\Cards\Dolphin Card Containers'
	'..\..\MVP\Views\Control Bars\Dolphin Control Bars'
	'..\..\Base\Dolphin Legacy Date & Time'
	'..\..\MVP\Base\Dolphin MVP Base'
	'..\..\MVP\Views\Scintilla\Dolphin Scintilla View'
	'..\Compiler\Smalltalk Parser'
	'..\..\..\..\Contributions\svenc\STON\STON-Core').

package!

"Class Definitions"!

STONReaderError subclass: #DolphinSTONReaderError
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!

STONCStyleCommentsSkipStream subclass: #DolphinSTONCStyleCommentsSkipStream
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!

STONReader subclass: #DolphinSTONReader
	instanceVariableNames: 'classInstVarMaps currentCharacter objectProperties renamedClasses'
	classVariableNames: 'EscapeChars HexDigitValues'
	poolDictionaries: ''
	classInstanceVariableNames: ''!

STONReference subclass: #DolphinSTONReference
	instanceVariableNames: 'refCount'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!

STONWriter subclass: #DolphinSTONWriter
	instanceVariableNames: 'classInstVarMaps stringQuote'
	classVariableNames: 'ExtendedCharacters'
	poolDictionaries: ''
	classInstanceVariableNames: ''!

"Loose Methods"!

!BlockClosure methodsFor!

fromSton: stonReader
	| instVars |
	instVars := stonReader instVarMapFor: BlockClosure.
	stonReader parseMapDo: 
			[:name :value |
			name == #copiedValues
				ifTrue: 
					[| count |
					count := value size.
					self resize: count.
					value
						replaceElementsOf: self
						from: 1
						to: count
						startingAt: 1]
				ifFalse: [self instVarAt: (instVars at: name) put: value]]!

stonOn: stonWriter
	stonWriter writeObject: self
		streamMap: 
			[:dictionary |
			"BlockClosures have a fixed set of inst vars that cannot be changed"
			BlockClosure instVarNames
				keysAndValuesDo: [:i :each | (self instVarAt: i) ifNotNil: [:value | dictionary at: each asSymbol put: value]].
			dictionary at: #copiedValues put: self copiedValues]! !

!BlockClosure categoriesForMethods!
fromSton:!public! !
stonOn:!public!ston! !
!

!ByteArray methodsFor!

hex
	"Answer a hexadecimal representation of the receiver"

	| hexStream string |
	string := AnsiString new: self size * 2.
	hexStream := string writeStream.
	self printHexOn: hexStream.
	^string! !

!ByteArray categoriesForMethods!
hex!public! !
!

!ByteArray class methodsFor!

readHexFrom: aString
	^self fromHexString: aString! !

!ByteArray class categoriesForMethods!
readHexFrom:!public!ston! !
!

!CardLayout class methodsFor!

stonAllInstVarNames
	"Override to exclude the number of instance variables we never want to serialize."

	^superclass stonAllInstVarNames
		, ##(self instVarNames reject: [:each | each = 'events' or: [each beginsWith: '_unused']])! !

!CardLayout class categoriesForMethods!
stonAllInstVarNames!public! !
!

!Color methodsFor!

stonOn: stonWriter
	"We have to replace the STON-Core implementation because Dolphin's Color representation is different to Pharo's."

	self name
		ifNil: 
			[stonWriter writeObject: self
				streamMap: 
					[:map |
					map
						at: #red put: (self normalizedRed roundTo: 0.001);
						at: #green put: (self normalizedGreen roundTo: 0.001);
						at: #blue put: (self normalizedBlue roundTo: 0.001);
						at: #alpha put: (self normalizedAlpha roundTo: 0.001)]]
		ifNotNil: [:name | stonWriter writeObject: self listSingleton: name]! !

!Color categoriesForMethods!
stonOn:!public!ston! !
!

!Color class methodsFor!

fromSton: stonReader
	"We have to replace the STON-Core implementation because Dolphin's Color representation is different to Pharo's."

	^stonReader isList
		ifTrue: [self named: stonReader parseListSingleton]
		ifFalse: 
			[| values |
			values := stonReader parseMap.
			self
				alpha: (values at: #alpha) * 255
				red: (values at: #red) * 255
				green: (values at: #green) * 255
				blue: (values at: #blue) * 255]!

stonName
	^#Color! !

!Color class categoriesForMethods!
fromSton:!public!ston! !
stonName!public!ston! !
!

!CompiledCode methodsFor!

fromSton: stonReader
	| instVars |
	instVars := stonReader instVarMapFor: CompiledCode.
	stonReader parseMapDo: 
			[:name :value |
			name == #literals
				ifTrue: 
					[| count |
					count := value size.
					self resize: count.
					value
						replaceElementsOf: self
						from: 1
						to: count
						startingAt: 1]
				ifFalse: [self instVarAt: (instVars at: name) put: value]]!

stonOn: stonWriter
	stonWriter writeObject: self
		streamMap: 
			[:dictionary |
			"CompiledMethods have a fixed set of inst vars that cannot be changed"
			CompiledCode instVarNames
				keysAndValuesDo: [:i :each | (self instVarAt: i) ifNotNil: [:value | dictionary at: each asSymbol put: value]].
			"Store the indexable fields as a literals list"
			dictionary at: #literals put: self literals]! !

!CompiledCode categoriesForMethods!
fromSton:!public! !
stonOn:!public!ston! !
!

!ControlView class methodsFor!

stonAllInstVarNames
	"Override to exclude the number of instance variables we never want to serialize."

	^super stonAllInstVarNames copyWithout: 'oldWndProc'! !

!ControlView class categoriesForMethods!
stonAllInstVarNames!public!ston! !
!

!Date methodsFor!

stonOn: stonWriter
	"Use an ISO style YYYY-MM-DD representation.
	Since my current implementation is time zone offset sensitive, the offset has to be included."

	"Custom implementation for Dolphin replacing the Pharo implementation in STON-Core"

	| stream offset |
	stream := String writeStream: 15.
	Locale invariant
		printDate: self
		on: stream
		format: 'yyyy-MM-dd'.
	offset := Locale timeZoneInformation offsetSeconds.
	offset = 0
		ifTrue: [stream nextPut: $Z]
		ifFalse: 
			[Locale invariant
				printDuration: offset seconds
				on: stream
				format: '+hh:mm'].
	stonWriter writeObject: self listSingleton: stream grabContents! !

!Date categoriesForMethods!
stonOn:!public!ston! !
!

!Date class methodsFor!

fromSton: stonReader
	"Read a ISO YYYY-MM-DD format.
	Since my current implementation is time zone offset sensitive, the offset has to be taken into account.
	A missing offset results in the local timezone offset to be used"

	"Customised for Dolphin from the STON-Core original"

	| readStream date |
	readStream := stonReader parseListSingleton readStream.
	date := self readFrom: readStream format: 'ymd'.
	readStream atEnd
		ifFalse: 
			[| offset dt |
			offset := DateAndTime readOffsetFrom: readStream sign: readStream next.
			dt := DateAndTime year: date year day: date dayOfYear offset: offset.
			date := dt asLocal asDate].
	^date! !

!Date class categoriesForMethods!
fromSton:!instance creation!public! !
!

!FramingCalculation methodsFor!

stonOn: stonWriter
	stonWriter writeObject: self listSingleton: name! !

!FramingCalculation categoriesForMethods!
stonOn:!public!ston! !
!

!FramingCalculation class methodsFor!

fromSton: stonReader
	#todo. "Perform is a security risk - provide a lookup table."
	^self perform: stonReader parseListSingleton! !

!FramingCalculation class categoriesForMethods!
fromSton:!public!ston! !
!

!GraphicsTool class methodsFor!

stonAllInstVarNames
	"Override to exclude the handle and ownsHandle instance variable as we never want to serialize these."

	^self == ##(self) ifTrue: [#()] ifFalse: [superclass stonAllInstVarNames , self instVarNames]! !

!GraphicsTool class categoriesForMethods!
stonAllInstVarNames!public!ston! !
!

!GUID methodsFor!

stonOn: stonWriter
	"Use a hex representation"
	
	stonWriter writeObject: self listSingleton: self idlString! !

!GUID categoriesForMethods!
stonOn:!public!ston! !
!

!GUID class methodsFor!

fromSton: stonReader
	^ self fromIdlString: stonReader parseListSingleton! !

!GUID class categoriesForMethods!
fromSton:!public!ston! !
!

!IconImageManager methodsFor!

stonOn: stonWriter
	stonWriter 
		writeObject: self 
		do: []! !

!IconImageManager categoriesForMethods!
stonOn:!public!ston! !
!

!IconImageManager class methodsFor!

fromSton: stonReader
	^self current! !

!IconImageManager class categoriesForMethods!
fromSton:!public! !
!

!MessageSequence methodsFor!

stonOn: stonWriter
	stonWriter writeObject: self do: [stonWriter encodeList: list]! !

!MessageSequence categoriesForMethods!
stonOn:!public!ston! !
!

!MessageSequence class methodsFor!

fromSton: stonReader
	"MessageSequences are like OrderedCollections of Messages."

	| collection |
	collection := self new.
	stonReader parseListDo: [:each | collection add: each].
	^collection! !

!MessageSequence class categoriesForMethods!
fromSton:!public!ston! !
!

!Model class methodsFor!

stonAllInstVarNames
	"Override to exclude the events instance variable as we never want to serialize these."

	^superclass == Object ifTrue: [#()] ifFalse: [superclass stonAllInstVarNames , self instVarNames]! !

!Model class categoriesForMethods!
stonAllInstVarNames!public!ston! !
!

!Object class methodsFor!

fromSton: stonReader
	"Create a new instance and delegate decoding to instance side.
	Override only when new instance should be created directly (see implementors). "

	| instance |
	"Divergence from standard STON in using #basicNew. As we are trying to restore an Object, we don't want it to run its normal initialization."
	instance := self basicNew.
	"We also call back to the reader to inform it of the new instance, as it can then use this to resolve any internal references. This is only really necessary if the receiving class creates external references as it is being materialized."
	stonReader registerNewObject: instance.
	^instance
		fromSton: stonReader;
		yourself! !

!Object class categoriesForMethods!
fromSton:!public!ston! !
!

!ProcessorScheduler methodsFor!

stonOn: aSTONWriter 
	aSTONWriter 
		writeObject: self 
		do: []! !

!ProcessorScheduler categoriesForMethods!
stonOn:!public!ston! !
!

!ProcessorScheduler class methodsFor!

fromSton: stonReader
	^Processor! !

!ProcessorScheduler class categoriesForMethods!
fromSton:!public!ston! !
!

!ReferenceView methodsFor!

stonExtras
	^{#state -> self state}! !

!ReferenceView categoriesForMethods!
stonExtras!public!ston! !
!

!ReferenceView class methodsFor!

stonAllInstVarNames
	"Override to exclude the number of instance variables we never want to serialize."

	^super stonAllInstVarNames copyWithout: 'referee'! !

!ReferenceView class categoriesForMethods!
stonAllInstVarNames!public! !
!

!ScintillaView class methodsFor!

stonAllInstVarNames
	"Override to exclude the number of instance variables we never want to serialize."

	^super stonAllInstVarNames copyWithout: 'styleIdMap'! !

!ScintillaView class categoriesForMethods!
stonAllInstVarNames!public! !
!

!SearchPolicy class methodsFor!

fromSton: stonReader
	^stonReader isList
		ifTrue: [self perform: stonReader parseListSingleton]
		ifFalse: [super fromSton: stonReader]! !

!SearchPolicy class categoriesForMethods!
fromSton:!public!ston! !
!

!SingletonSearchPolicy methodsFor!

stonOn: stonWriter
	self name
		ifNil: [stonWriter writeObject: self]
		ifNotNil: [:name | stonWriter writeObject: self listSingleton: name]! !

!SingletonSearchPolicy categoriesForMethods!
stonOn:!public!ston! !
!

!SingletonSearchPolicy class methodsFor!

stonName
	^#SearchPolicy! !

!SingletonSearchPolicy class categoriesForMethods!
stonName!public!ston! !
!

!SmalltalkSystem methodsFor!

stonOn: stonWriter
	stonWriter writeObject: self listSingleton: #current! !

!SmalltalkSystem categoriesForMethods!
stonOn:!public!ston! !
!

!STON class methodsFor!

icon
	"Answers an Icon that can be used to represent this class"

	^self defaultIcon! !

!STON class categoriesForMethods!
icon!constants!public! !
!

!STONCStyleCommentsSkipStream methodsFor!

skipSeparators
	^stream skipSeparators! !

!STONCStyleCommentsSkipStream categoriesForMethods!
skipSeparators!public! !
!

!STONCStyleCommentsSkipStream class methodsFor!

icon
	"Answers an Icon that can be used to represent this class"

	^STON icon!

new
	^DolphinSTONCStyleCommentsSkipStream basicNew! !

!STONCStyleCommentsSkipStream class categoriesForMethods!
icon!constants!public! !
new!instance creation!public! !
!

!STONJSON class methodsFor!

icon
	"Answers an Icon that can be used to represent this class"

	^STON icon! !

!STONJSON class categoriesForMethods!
icon!constants!public! !
!

!STONReader class methodsFor!

icon
	"Answers an Icon that can be used to represent this class"

	^STON icon!

new
	^DolphinSTONReader basicNew initialize! !

!STONReader class categoriesForMethods!
icon!constants!public! !
new!instance creation!private! !
!

!STONReference class methodsFor!

icon
	"Answers an Icon that can be used to represent this class"

	^STON icon! !

!STONReference class categoriesForMethods!
icon!constants!public! !
!

!STONStreamWriter class methodsFor!

icon
	"Answers an Icon that can be used to represent this class"

	^STON icon! !

!STONStreamWriter class categoriesForMethods!
icon!constants!public! !
!

!STONWriter class methodsFor!

icon
	"Answers an Icon that can be used to represent this class"

	^STON icon!

new
	^DolphinSTONWriter basicNew initialize! !

!STONWriter class categoriesForMethods!
icon!constants!public! !
new!public! !
!

!SystemDictionary methodsFor!

stonOn: aSTONWriter 
	aSTONWriter 
		writeObject: self 
		listSingleton: #Smalltalk! !

!SystemDictionary categoriesForMethods!
stonOn:!public! !
!

!SystemDictionary class methodsFor!

fromSton: stonReader
	| globalName theClass |
	globalName := stonReader parseListSingleton.
	theClass := self environment at: globalName.
	^theClass! !

!SystemDictionary class categoriesForMethods!
fromSton:!public! !
!

!Toolbar class methodsFor!

stonAllInstVarNames
	"Override to exclude the number of instance variables we never want to serialize."

	^super stonAllInstVarNames copyWithoutAll: #('idMap' 'bitmapsStart')! !

!Toolbar class categoriesForMethods!
stonAllInstVarNames!public! !
!

!View methodsFor!

stonExtras
	^{#state -> self state. #subViews -> self subViews}!

stonOn: stonWriter
	"At present this is more experiment than anything useful. To show the potential though try Display-It on the following in a workspace:

			shell := View desktop.
			resourceView := Smalltalk developmentSystem loadViewResource: (ResourceIdentifier class: ClassBrowserShell selector: #resource_Default_view) resource inContext: shell. 
			ston := STON toStringPretty: resourceView.
			resourceView destroy.
			ston.
	"

	stonWriter writeObject: self extra: self stonExtras! !

!View categoriesForMethods!
stonExtras!public!ston! !
stonOn:!public!ston! !
!

!View class methodsFor!

stonAllInstVarNames
	"Override to exclude the number of instance variables we never want to serialize."

	^self == ##(self)
		ifTrue: 
			[#('creationStyle' 'model' 'backcolor' 'preferredExtent' 'flags' 'contextMenu' 'font' 'interactor')	"'presenter'"]
		ifFalse: [superclass stonAllInstVarNames , self instVarNames]! !

!View class categoriesForMethods!
stonAllInstVarNames!public!ston! !
!

"End of package definition"!

