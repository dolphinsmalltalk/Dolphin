"Filed out from Dolphin Smalltalk X6.1"!

PointEvent subclass: #MouseEvent
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
MouseEvent guid: (GUID fromString: '{87B4C555-026E-11D3-9FD7-00A0CC3E4A32}')!
MouseEvent comment: 'MouseEvent represents the class of events generated by mouse hardware input device, including movements. button press/release events, scrolling of the wheel (where present), and whatever other fiddly bit MS dreams up next.'!
!MouseEvent categoriesForClass!Events!MVP-Views-Events! !
!MouseEvent methodsFor!

button
	"Answer the symbolic name of the button to which this event relates."

	(message between: WM_LBUTTONDOWN and: WM_LBUTTONDBLCLK) ifTrue: [^#left].
	(message between: WM_RBUTTONDOWN and: WM_RBUTTONDBLCLK) ifTrue: [^#right].
	(message between: WM_MBUTTONDOWN and: WM_MBUTTONDBLCLK) ifTrue: [^#middle].
	(message between: WM_XBUTTONDOWN and: WM_XBUTTONDBLCLK) 
		ifTrue: [self wParam highWord == 1 ifTrue: [^#xButton1] ifFalse: [^#xButton2]].
	^nil!

isCtrlDown
	"Answer whether the control key is down."

	^self keys anyMask: MK_CONTROL!

isLButtonDown
	"Answer whether the left mouse button is down."

	^self keys anyMask: MK_LBUTTON!

isMButtonDown
	"Answer whether the middle mouse button is down."

	^self keys anyMask: MK_MBUTTON!

isRButtonDown
	"Answer whether the right mouse button is down."

	^self keys anyMask: MK_RBUTTON!

isShiftDown
	"Answer whether the shift key is down."

	^self keys anyMask: MK_SHIFT!

isXButtonDown: anInteger 
	"Answer whether the specified X button (numbered either 1 or 2) is down."

	(anInteger between: 1 and: 2) 
		ifFalse: [self error: 'Invalid X button number: ' , anInteger displayString].
	^self keys anyMask: (MK_XBUTTON1 bitShift: anInteger - 1)!

keys
	"Private - Answer the keys flags from the message."

	^self wParam!

wheelRotation
	"Answer the <number> of notches that the mouse wheel was rotated to generate this
	event (assuming that this is a WM_MOUSEWHEEL event)."

	self assert: [message == WM_MOUSEWHEEL].
	^self wParam highPartSigned / WHEEL_DELTA! !
!MouseEvent categoriesFor: #button!accessing!public! !
!MouseEvent categoriesFor: #isCtrlDown!public!testing! !
!MouseEvent categoriesFor: #isLButtonDown!public!testing! !
!MouseEvent categoriesFor: #isMButtonDown!public!testing! !
!MouseEvent categoriesFor: #isRButtonDown!public!testing! !
!MouseEvent categoriesFor: #isShiftDown!public!testing! !
!MouseEvent categoriesFor: #isXButtonDown:!public!testing! !
!MouseEvent categoriesFor: #keys!accessing!private! !
!MouseEvent categoriesFor: #wheelRotation!accessing!public! !

