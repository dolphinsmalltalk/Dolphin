| package |
package := Package name: 'DolphinSure UI'.
package paxVersion: 2.1;
	preDeclareClassesOnLoad: false;
	basicComment: 'Dolphin Smalltalk "DolphinSure" user interface.
Copyright (c) Object Arts Ltd, 1999-2001.

Extends the DolphinSure package with UI elements.

For documentation evaluate:
	SmalltalkSystem help: ''DolphinSure'''.

package basicPackageVersion: '6.0'.


package setClassNames: #(
	#{UI.DolphinSureTrustWarning}
).

package setMethodNames: #(
	#(#{Security.DolphinSureCertificate} #backgroundImageFile)
	#(#{Security.DolphinSureCertificate} #gap)
	#(#{Security.DolphinSureCertificate} #image)
	#(#{Security.DolphinSureCertificate} #imageAttributes)
	#(#{Security.DolphinSureCertificate} #imagePrint:in:label:in:on:brush:)
	#(#{Security.DolphinSureCertificate} #show)
	#(#{Security.DolphinSureCertificationAuthorityCertificate} #imageAttributes)
	#(#{Security.DolphinSurePersonalCertificate} #backgroundImageFile)
	#(#{Security.DolphinSurePersonalCertificate} #imageAttributes)
	#(#{Security.NotSignedCertificate} #backgroundImageFile)
	#(#{Security.NotSignedCertificate} #imageAttributes)
).

package setPrerequisites: #(
	'..\Base\Dolphin'
	'..\MVP\Dialogs\Dolphin Base Dialogs'
	'..\MVP\Base\Dolphin Basic Geometry'
	'..\MVP\Presenters\Boolean\Dolphin Boolean Presenter'
	'..\MVP\Views\Buttons\Dolphin Check Buttons'
	'..\MVP\Graphics\Dolphin GDI Graphics'
	'..\MVP\Presenters\Image\Dolphin Image Presenter'
	'..\System\Filer\Dolphin Literal Filer'
	'..\MVP\Base\Dolphin MVP Base'
	'..\MVP\Views\Buttons\Dolphin Push Buttons'
	'..\MVP\Views\Static\Text\Dolphin StaticText Control'
	'..\System\Filer\Dolphin STx Filer Core'
	'..\MVP\Presenters\Text\Dolphin Text Presenter'
	'..\MVP\Type Converters\Dolphin Type Converters'
	'..\MVP\Models\Value\Dolphin Value Models'
	'DolphinSure'
	'..\MVP\Gdiplus\Gdiplus'
).

package!

"Class Definitions"!

UI.Dialog
	subclass: #'UI.DolphinSureTrustWarning'
	instanceVariableNames: 'certificateImage description alwaysTrust'
	classVariableNames: ''
	imports: #()
	classInstanceVariableNames: ''
	classConstants: {}!

"Loose Methods"!

!Security.DolphinSureCertificate methodsFor!

backgroundImageFile
	"Private - Answers the resource name of the background image file
	for the receiver"

	^'Resources/DolphinSureCert.jpg'
!

gap	
	"Private - Answers a string to leave a gap"

	^' '!

image
	"Answers an image displaying the certificate details"

	<namespace: Graphics.Gdiplus>

	| background image extent labelBox textBox graphics blackBrush dpi |
	"First validate the certificate"
	self validData.
	background := Bitmap fromFile: self backgroundImageFile
				usingLocator: FileLocator installRelative.
	extent := background extent.
	labelBox := RectF origin: 35 @ 55 corner: 110 @ (extent y - 20).
	textBox := RectF origin: 120 @ 55 corner: extent - 20.
	dpi := SystemMetrics current dpi.
	extent := extent * dpi / 96.0.
	labelBox scaleToDpi: dpi from: 96.
	textBox scaleToDpi: dpi from: 96.
	image := Bitmap width: extent x height: extent y.
	graphics := image graphics.
	image graphics
		drawImage: background
		at: Point zero
		extent: extent.
	background free.
	blackBrush := SolidBrush black.
	"Draw attributes"
	self imageAttributes do: 
			[:each |
			| text attributeName |
			attributeName := each value.
			(text := self perform: attributeName) isEmpty
				ifFalse: 
					[self
						imagePrint: text
						in: textBox
						label: each key
						in: labelBox
						on: graphics
						brush: ((attributeName == #validityString and: [self info hasExpired])
								ifTrue: [SolidBrush red]
								ifFalse: [blackBrush])]].
	blackBrush free.
	graphics free.
	^image!

imageAttributes
	"Private - Answers a <sequencedReadableCollection> of <Association> detailing the attributes that should appear on a image displaying the certificate details"

	^{'Serial no:' -> #serial.
		'Publisher:' -> #owner.
		'Details:' -> #details.
		'' -> #gap.
		'Purpose:' -> #purpose.
		'' -> #gap.
		'Validity:' -> #validityString.
		'Issued by:' -> #issuingAuthority}!

imagePrint: textString in: textRect label: labelString in: labelRect on: aGdiplusGraphics brush: aGdiplusBrush
	"Private - Prints the attribute of the receiver identified by attributeAssociation on aCanvas
	in labelRect and textRect. The key of the association is a <readableString> that acts as a heading for
	the attribute data while the value is a selector to use to get the attribute data itself. Once
	the attribute has been printed on the canvas then labelRect and textRect are updated to the next location."

	<namespace: Graphics.Gdiplus>

	| value font height fontHeight |
	value := textString.
	fontHeight := 8 * SystemMetrics current dpi / 72.0.
	font := Font
				name: 'Arial'
				pixelSize: fontHeight
				style: FontStyleBold.
	height := (aGdiplusGraphics
				measureString: labelString
				font: font
				rect: labelRect
				format: nil) height.
	aGdiplusGraphics
		drawString: labelString
		font: font
		rect: labelRect
		format: nil
		brush: aGdiplusBrush.
	font free.
	font := Font name: 'Arial' pixelSize: fontHeight.
	aGdiplusGraphics
		drawString: value
		font: font
		rect: textRect
		format: nil
		brush: aGdiplusBrush.
	height := height
				max: (aGdiplusGraphics
						measureString: value
						font: font
						rect: textRect
						format: nil) height.
	font free.
	labelRect top: labelRect top + height.
	textRect top: textRect top + height!

show
	"Shows the receiver as a splash screen"

	(UI.Splash bitmap: self image) show	! !

!Security.DolphinSureCertificate categoriesForMethods!
backgroundImageFile!constants!private! !
gap!drawing!helpers!private! !
image!drawing!public! !
imageAttributes!drawing!helpers!private! !
imagePrint:in:label:in:on:brush:!drawing!helpers!private! !
show!operations!public! !
!

!Security.DolphinSureCertificationAuthorityCertificate methodsFor!

imageAttributes
	"Private - Answers a <sequencedReadableCollection> of <Association> detailing the attributes that should appear on a image displaying the certificate details"

	^{'Serial no:' -> #serial.
		'Certifying Authority:' -> #owner.
		'Details:' -> #details.
		'' -> #gap.
		'Purpose:' -> #purpose.
		'' -> #gap.
		'Validity:' -> #validityString.
		'Issued by:' -> #issuingAuthority}! !

!Security.DolphinSureCertificationAuthorityCertificate categoriesForMethods!
imageAttributes!accessing!private! !
!

!Security.DolphinSurePersonalCertificate methodsFor!

backgroundImageFile
	"Private - Answers the resource name of the background image file
	for the receiver"

	^'Resources/DolphinSurePersonalCert.jpg'
!

imageAttributes
	"Private - Answers a <sequencedReadableCollection> of <Association> detailing the attributes that should appear on a image displaying the certificate details"

	^{'Serial no:' -> #serial.
		'Issued to:' -> #owner.
		'Details:' -> #details.
		'' -> #gap.
		'Purpose:' -> #purpose.
		'' -> #gap.
		'Validity:' -> #validityString.
		'Issued by:' -> #issuingAuthority}! !

!Security.DolphinSurePersonalCertificate categoriesForMethods!
backgroundImageFile!constants!private! !
imageAttributes!drawing!helpers!private! !
!

!Security.NotSignedCertificate methodsFor!

backgroundImageFile
	"Private - Answers the resource name of the background image file
	for the receiver"

	^'Resources/DolphinSureNotSignedCert.jpg'
!

imageAttributes
	"Private - Answers a <sequencedReadableCollection> of <Association> detailing the attributes that should appear on a image displaying the certificate details"

	^{'' -> #gap. 'IMPORTANT:' -> #purpose}! !

!Security.NotSignedCertificate categoriesForMethods!
backgroundImageFile!constants!private! !
imageAttributes!accessing!private! !
!

"End of package definition"!

