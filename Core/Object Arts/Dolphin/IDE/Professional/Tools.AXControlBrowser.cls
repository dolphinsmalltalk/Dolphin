"Filed out from Dolphin Smalltalk"!

Tools.SmalltalkToolShell
	subclass: #'Tools.AXControlBrowser'
	instanceVariableNames: 'site windowStatus inspector eventsPresenter flags traceWindow progId'
	classVariableNames: ''
	imports: #()
	classInstanceVariableNames: ''
	classConstants: { 'TraceMask' -> 16r1 }!

Tools.AXControlBrowser guid: (Core.GUID fromString: '{e2e33600-aa60-11d3-b376-080046056106}')!

Tools.AXControlBrowser comment: 'AXControlBrowser is a simple Active-X control (OCX) browser and test environment.

AXControlBrowser show

Instance Variables:
	site			<ActiveXHost> which is hosting the embedded Active-X Control.
	windowStatus	<ValueHolder>
	inspector		<AXInspector>. Inspector on the control''s default dispinterface.
	eventsPresenter	<ListPresenter>. List of events in the control''s default source interface.

'!

!Tools.AXControlBrowser categoriesForClass!MVP-Presenters! !

!Tools.AXControlBrowser methodsFor!

about
	"Pop up the controls about box"

	<commandQuery: #hasProgId>
	^self controlSite aboutControl!

browseIDL
	"Open a new workspace populated with the IDL for the control."

	<commandQuery: #hasProgId>
	| ti typeLib |
	ti := self controlSite controlDispatch typeInfoIfNone: [^Sound warningBeep].
	typeLib := ti typeLib.
	self browseIDLOfTypeLib: typeLib!

browseIDLOfTypeLib: anAXTypeLibraryAnalyzer
	| stream |
	stream := ReadWriteStream on: String new.
	Cursor wait showWhile: [anAXTypeLibraryAnalyzer printIDLOn: stream].
	stream reset.
	SmalltalkSystem current workspaceShellClass show
		language: #null;
		streamIn: stream!

chooseAmbientBackcolor
	"Choose and assign to the host an ambient background colour.
	N.B. This may have no effect if the control ignores the ambient background colour."

	| bc |
	bc := (ColorDialog on: self controlSite backcolor asValue)
				caption: 'Background Color';
				showModal.
	bc notNil ifTrue: [self controlSite backcolor: bc]!

chooseAmbientFont
	"Choose and assign to the host an ambient font.
	N.B. This may have no effect if the control ignores the 
	ambient font."

	FontDialog showModalOn: (self controlSite aspectValue: #font)!

chooseAmbientForecolor
	"Choose and assign to the host an ambient foreground colour.
	N.B. This may have no effect if the control ignores the ambient foreground colour."

	| colour |
	colour := (ColorDialog on: self controlSite forecolor asValue)
				caption: 'Foreground Color';
				showModal.
	colour notNil ifTrue: [self controlSite forecolor: colour]!

controlHelp
	"Open the control's help file, if any."

	<commandQuery: #hasProgId>
	| unk tiControl typeLib filename |
	unk := self controlSite controlUnknown.
	tiControl := unk coclassTypeInfoIfNone: 
					[MessageBox
						notify: 'Sorry, there appears to be no help or type information available for the control.'.
					^self].
	typeLib := tiControl typeLib.
	filename := typeLib documentation helpfile.
	filename isEmpty
		ifTrue: 
			[(MessageBox
				confirm: ('Sorry, there appears to be no help available for the <1s> control.<n><n>Would you like to browse the IDL instead?'
						expandMacrosWith: tiControl name))
					ifTrue: [self browseIDLOfTypeLib: typeLib]]
		ifFalse: 
			[[OS.Shell32 shellOpen: filename] on: OS.Win32Error
				do: 
					[:e |
					MessageBox new
						headline: 'Unable to open help file: ' , filename;
						errorMsg: e messageText]]!

controlProperties
	"Open the control's property dialog (if possible)."

	<commandQuery: #hasProgId>
	self controlSite editControlProperties!

controlSite
	"Answer the <AXControlSite> hosting the control being tested."

	^site!

copyEventName
	"Copy the name of the currently selected event to the clipboard (useful for pasting into 
	a #createSchematicWiring/#onViewOpened method in order to wire up to events
	generated by the control)."

	<commandQuery: #hasEventSelected>
	eventsPresenter selection key printString copyToClipboard!

createComponents
	"Create the presenters contained by the receiver"

	super createComponents.
	inspector := self add: PublishedAspectInspector new name: 'inspector'.
	eventsPresenter := self add: ListPresenter new name: 'eventNames'!

createSchematicWiring
	"Create the trigger wiring for the receiver"
	
	super createSchematicWiring.

	eventsPresenter
		when: #drag: send: #onDragEvent: to: self!

editProperties
	"Open the control's property dialog (if possible)."

	<commandQuery: #hasProgId>
	self controlSite editControlProperties!

fileNewControl
	"Insert a new control"

	| host |
	host := self controlSite.
	progId := AXControlSite chooseControl: host progId.
	progId notNil
		ifTrue: 
			[self progId: progId.
			host setFocus]!

hasEventSelected
	^eventsPresenter hasSelection!

hasProgId
	^progId notNil!

initialize
	"Private - Initialize the receiver"

	super initialize.
	flags := TraceMask.!

inspectControl
	"Open an <AXInspector> on the control."

	<commandQuery: #hasProgId>
	self controlSite controlDispatch inspect!

isTracingEvents
	^flags allMask: TraceMask!

isTracingEvents: aBoolean 
	flags := flags mask: TraceMask set: aBoolean!

onDragEvent: aDragDropSession 
	"A drag has been initiated from the events list. Populate the <DragDropSession>
	accordingly (just extend the suggested drag objects by adding the string format)."

	aDragDropSession dragObjects do: [:dragee | | eventName |
		eventName := (dragee format: #Object) key.
		dragee format: #Symbol data: eventName;
			format: #String data: eventName printString]!

onEvent: aSymbol withArguments: argumentArray
	"Private - Handle the generic event from the host which details an event from the control."

	self isTracingEvents
		ifTrue: 
			[self trace: ((String smalltalkWriteStream: 80)
						nextPutAll: 'Event: ';
						print: aSymbol;
						space;
						print: argumentArray;
						cr;
						contents)]!

onPropertyChanged: propertyName
	"Private - Handle a property change event from the host."

	self isTracingEvents
		ifTrue: 
			[self trace: ((String smalltalkWriteStream: 80)
						nextPutAll: 'Property Changed: ';
						print: propertyName;
						cr;
						contents)]!

onPropertyChanging: propertyName accept: booleanValue
	"Private - Handle a property changing event from the host."

	self isTracingEvents
		ifTrue: 
			[self trace: ((String smalltalkWriteStream: 80)
						nextPutAll: 'Property Changing: ';
						print: propertyName;
						cr;
						contents)]!

onViewOpened
	"Received when the receiver's view has been connected. "

	| statusItem |
	super onViewOpened.
	site := self view viewNamed: 'site'.
	site
		when: #axEvent:withArguments:
			send: #onEvent:withArguments:
			to: self;
		when: #controlPropertyChanged:
			send: #onPropertyChanged:
			to: self;
		when: #controlPropertyChanging:accept:
			send: #onPropertyChanging:accept:
			to: self.
	statusItem := self view viewNamed: 'windowStatus' ifNone: nil.
	statusItem notNil
		ifTrue: 
			[windowStatus := ValueHolder new.
			statusItem model: windowStatus].
	traceWindow := self view viewNamed: 'trace'!

progId: aString
	"Set the progId of the component loaded in the container."

	traceWindow clear.
	site progId: aString.
	self updateControlInfo
!

queryToggleAllowShowUI: aCommandQuery
	aCommandQuery
		beEnabled;
		isChecked: self controlSite allowShowUI!

queryToggleAllowWindowless: aCommandQuery
	aCommandQuery
		beEnabled;
		isChecked: self controlSite allowWindowless!

queryToggleDesignMode: aCommandQuery
	aCommandQuery
		beEnabled;
		isChecked: self controlSite isDesignMode!

queryToggleDisplayAsDefault: aCommandQuery
	aCommandQuery
		beEnabled;
		isChecked: self controlSite isDefault!

queryToggleEventTracing: aCommandQuery
	aCommandQuery
		beEnabled;
		isChecked: self isTracingEvents!

queryToggleFireControlEvents: aCommandQuery
	aCommandQuery
		beEnabled;
		isChecked: self controlSite firesControlEvents!

queryToggleFirePropertyNotifications: aCommandQuery
	aCommandQuery
		beEnabled;
		isChecked: self controlSite firesPropertyNotifications!

toggleAllowShowUI
	<commandQuery: #queryToggleAllowShowUI:>
	self controlSite allowShowUI: self controlSite allowShowUI not!

toggleAllowWindowless
	<commandQuery: #queryToggleAllowWindowless:>
	self controlSite allowWindowless: self controlSite allowWindowless not!

toggleDesignMode
	<commandQuery: #queryToggleDesignMode:>
	self controlSite isDesignMode: self controlSite isDesignMode not!

toggleDisplayAsDefault
	<commandQuery: #queryToggleDisplayAsDefault:>
	self controlSite isDefault: self controlSite isDefault not!

toggleEventTracing
	"Toggle on/off event tracing."

	<commandQuery: #queryToggleEventTracing:>
	self isTracingEvents: self isTracingEvents not!

toggleFireControlEvents
	"Toggle events from the control."

	<commandQuery: #queryToggleFireControlEvents:>
	self controlSite firesControlEvents: self controlSite firesControlEvents not!

toggleFirePropertyNotifications
	"Toggle property notifications from the control. Note that this causes the control to be 
	recreated because we can only specify this at control creation time."

	<commandQuery: #queryToggleFirePropertyNotifications:>
	self controlSite firesPropertyNotifications: self controlSite firesPropertyNotifications not.
	inspector object: self controlSite controlDispatch!

trace: aString
	"Private - Append some output to the receiver's trace window."

	traceWindow caretPosition: 0; replaceSelection: aString!

updateControlInfo
	"Private - Refresh any details we're displaying about the control."

	| host |
	host := self controlSite.
	self caption: host progId.
	windowStatus value: (host isWindowless ifTrue: ['Windowless'] ifFalse: ['Windowed']).
	"Note that the IDispatch instance used by an AXControlSite never changes (as of D5), but is re-used,
	therefore we must replace the entire model so that the inspector refreshes."
	inspector model: host controlDispatch asValue.
	host isSinkingEvents
		ifTrue: [ | sink funcs |
			sink := host sink.
			sink triggerIndividualEvents: false; triggerGenericEvent: true.
			funcs := sink sourceTypeInfo functions.
			eventsPresenter 
				list: (funcs collect: [:f | f selector -> f printIDL]);
				view enable]
		ifFalse: [eventsPresenter clear; view disable]! !

!Tools.AXControlBrowser categoriesForMethods!
about!commands-actions!public! !
browseIDL!commands-actions!public! !
browseIDLOfTypeLib:!operations!public! !
chooseAmbientBackcolor!commands-actions!public! !
chooseAmbientFont!commands-actions!public! !
chooseAmbientForecolor!commands-actions!public! !
controlHelp!commands-actions!public! !
controlProperties!commands-actions!public! !
controlSite!accessing!public! !
copyEventName!commands-actions!public! !
createComponents!initializing!public! !
createSchematicWiring!initializing!public! !
editProperties!commands-actions!public! !
fileNewControl!commands-actions!public! !
hasEventSelected!commands-queries!private! !
hasProgId!commands-queries!private! !
initialize!initializing!private! !
inspectControl!commands-actions!public! !
isTracingEvents!public!testing! !
isTracingEvents:!accessing!public! !
onDragEvent:!event handling!public! !
onEvent:withArguments:!event handling!private! !
onPropertyChanged:!event handling!private! !
onPropertyChanging:accept:!event handling!private! !
onViewOpened!event handling!public! !
progId:!operations!public! !
queryToggleAllowShowUI:!commands-queries!private! !
queryToggleAllowWindowless:!commands-queries!private! !
queryToggleDesignMode:!commands-queries!private! !
queryToggleDisplayAsDefault:!commands-queries!private! !
queryToggleEventTracing:!commands-queries!private! !
queryToggleFireControlEvents:!commands-queries!private! !
queryToggleFirePropertyNotifications:!commands-queries!private! !
toggleAllowShowUI!commands-actions!public! !
toggleAllowWindowless!commands-actions!public! !
toggleDesignMode!commands-actions!public! !
toggleDisplayAsDefault!commands-actions!public! !
toggleEventTracing!commands-actions!public! !
toggleFireControlEvents!commands-actions!public! !
toggleFirePropertyNotifications!commands-actions!public! !
trace:!operations!private! !
updateControlInfo!operations!private! !
!

!Tools.AXControlBrowser class methodsFor!

displayOn: aPuttableStream
	"Append to the <puttableStream> first argument a String whose characters are a representation of the receiver that an end-user might want to see."

	aPuttableStream nextPutAll: 'Active-X Control Browser'!

icon
	"Answers an Icon that can be used to represent this class."

	^AXControlSite icon!

initialize
	"Private - Initialize the receiver. Register the tools folder icon with SmalltalkSystem.

		self initialize.
	"

	SmalltalkSystem current
		addAdditionalToolsFolderIcon: (SmalltalkSystemIcon
					show: self
					description: self displayString
					helpId: self toolsFolderHelpId);
		registerTool: self!

resource_Default_view
	"Answer the literal data from which the 'Default view' resource can be reconstituted.
	DO NOT EDIT OR RECATEGORIZE THIS METHOD.

	If you wish to modify this resource evaluate:
	Tools.ViewComposer openOn: (UI.ResourceIdentifier class: self selector: #resource_Default_view)
	"

	^#(#'!!STL' 6 2118 11 #{UI.STBViewProxy} #{UI.ShellView} 38 #{Core.Array} 27 nil nil 8 #(13565952 65536) 32 nil 518 #{Graphics.ThemeColor} #toolBackground nil 517 nil nil nil 32 1798 #{UI.BorderLayout} 1 1 nil 18 #{UI.StatusBar} 50 21 nil 32 50 2 8 1140850956 1 160 nil nil nil 5 nil 1350 1 #{Graphics.Font} nil true 262 #{OS.LOGFONTW} 8 #[243 255 255 255 0 0 0 0 0 0 0 0 0 0 0 0 144 1 0 0 0 0 0 0 3 2 1 0 65 0 114 0 105 0 97 0 108 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0] 193 nil nil 160 nil nil 518 #{Kernel.STBIdentityDictionaryProxy} #{Core.IdentityDictionary} 50 2 1862 1 #{UI.StatusBarItem} 8193 201 160 nil 518 #{Core.Message} #displayString 8 #() 386 #iconImageIndex 416 nil 8 'windowStatus' nil nil nil 50 1 368 1094 1 #{UI.StatusBarNullItem} 8705 1 160 nil nil 262 #{Core.MessageSequence} 50 1 774 #{Core.MessageSend} #createWindow: 50 1 1030 #{UI.CreateWindow} 262 #{OS.RECTL} 8 #[0 0 0 0 63 1 0 0 72 2 0 0 85 1 0 0] 193 192 8 '' 160 3 8 #() 518 #{Graphics.Point} 193 193 nil 29 nil nil 18 #{UI.ContainerView} 50 15 nil 32 50 2 8 1174405120 131073 752 nil nil nil 5 nil nil nil 752 518 #{UI.ProportionalLayout} 518 #{Kernel.STBCollectionProxy} #{Core.LookupTable} 50 3 518 #{Core.Association} 18 #{UI.AXControlSite} 50 25 nil 752 50 2 8 1073741824 262145 928 nil 6 #{Graphics.Color} #default nil 5 nil nil nil 928 nil nil 8 'mshtml:<h2>Active-X Control Browser</h2>This browser allows you to load various ActiveX controls and to inspects their methods, properties and events. Choose <I>File/New (Ctrl+N)</I> to bring up a list of controls available on your computer.<h4>Copyright © Object Arts Ltd, 2000</h4>' 1030 #{OS.COM.IDispatch} nil nil 2374 1 #{OS.COM.TKindDispatchAnalyzer} 1094 1 #{OS.COM.ITypeInfo2} nil nil 774 #{OS.COM.TYPEATTR} nil 1088 1120 nil nil nil 2630 3 #{OS.COM.TypeLibraryAnalyzer} 518 #{OS.COM.ITypeLib2} nil nil 2053 nil 262 #{OS.COM.TLIBATTR} 8 #[197 241 80 48 181 152 207 17 187 130 0 170 0 189 206 11 0 0 0 0 1 0 0 0 4 0 0 0 8 0 110 0] 8 #{Root.MSHTML} nil nil nil 550 #{Core.LookupTable} 0 nil nil 1329 1030 #{OS.COM.TypeLibDocumentation} 8 'DispHTMLDocument' 8 '' 1 8 '' nil nil nil 518 #{UI.IDolphinAxHost} nil nil nil 3787 1298 1 -1049 #ReadyState 1030 #{UI.IAxWinAmbientDispatchEx} nil nil nil nil 518 #{OS.COM.IUnknown} nil nil nil nil nil 514 50 3 562 #createWindow: 50 1 610 642 8 #[0 0 0 0 0 0 0 0 72 2 0 0 169 0 0 0] 193 960 8 '' 928 562 #restoreAmbientProperties 8 #() 928 562 #controlBinaryStoreBytes: 50 1 8 #[119 0 115 0 101 0 114 0 60 0 47 0 104 0 50 0 62 0 84 0 104 0 105 0 115 0 32 0 98 0 114 0 111 0 119 0 115 0 101 0 114 0 32 0 97 0 108 0 108 0 111 0 119 0 115 0 32 0 121 0 111 0 117 0 32 0 116 0 111 0 32 0 108 0 111 0 97 0 100 0 32 0 118 0 97 0 114 0 105 0 111 0 117 0 115 0 32 0 65 0 99 0 116 0 105 0 118 0 101 0 88 0 32 0 99 0 111 0 110 0 116 0 114 0 111 0 108 0 115 0 32 0 97 0 110 0 100 0 32 0 116 0 111 0 32 0 105 0 110 0 115 0 112 0 101 0 99 0 116 0 115 0 32 0 116 0 104 0 101 0 105 0 114 0 32 0 109 0 101 0 116 0 104 0 111 0 100 0 115 0 44 0 32 0 112 0 114 0 111 0 112 0 101 0 114 0 116 0 105 0 101 0 115 0 32 0 97 0 110 0 100 0 32 0 101 0 118 0 101 0 110 0 116 0 115 0 46 0 32 0 67 0 104 0 111 0 111 0 115 0 101 0 32 0 60 0 73 0 62 0 70 0 105 0 108 0 101 0 47 0 78 0 101 0 119 0 32 0 40 0 67 0 116 0 114 0 108 0 43 0 78 0 41 0 60 0 47 0 73 0 62 0 32 0 116 0 111 0 32 0 98 0 114 0 105 0 110 0 103 0 32 0 117 0 112 0 32 0 97 0 32 0 108 0 105 0 115 0 116 0 32 0 111 0 102 0 32 0 99 0 111 0 110 0 116 0 114 0 111 0 108 0 115 0 32 0 97 0 118 0 97 0 105 0 108 0 97 0 98 0 108 0 101 0 32 0 111 0 110 0 32 0 121 0 111 0 117 0 114 0 32 0 99 0 111 0 109 0 112 0 117 0 116 0 101 0 114 0 46 0 60 0 104 0 52 0 62 0 67 0 111 0 112 0 121 0 114 0 105 0 103 0 104 0 116 0 32 0 169 0 32 0 79 0 98 0 106 0 101 0 99 0 116 0 32 0 65 0 114 0 116 0 115 0 32 0 76 0 116 0 100 0 44 0 32 0 50 0 48 0 48 0 48 0 60 0 47 0 104 0 52 0 62 0] 928 3 8 #() 722 193 193 nil 27 111 898 18 #{UI.ContainerView} 50 15 nil 752 50 2 8 1140850688 131073 1776 nil nil nil 5 nil nil nil 1776 818 850 1296 50 2 898 18 #{UI.ReferenceView} 50 14 nil 1776 50 2 8 1140850688 131073 1904 nil nil nil 5 nil nil nil 1904 582 1 #{UI.ResourceIdentifier} #{Tools.PublishedAspectInspector} #resource_Default_view nil 514 50 1 562 #createWindow: 50 1 610 642 8 #[0 0 0 0 0 0 0 0 149 1 0 0 92 0 0 0] 193 1936 8 '' 1904 3 1664 722 193 193 nil 27 141 898 18 #{UI.ListView} 50 45 nil 1776 50 2 8 1140936773 262145 2160 838 2 #{UI.ListModel} 550 #{Core.OrderedCollection} 0 nil 6 #{Kernel.IdentitySearchPolicy} 1008 nil 13 2886 4 #{UI.Menu} nil true 50 1 1094 2 #{UI.CommandMenuItem} 2097153 1350 4 #{UI.CommandDescription} #copyEventName 8 '&Copy' 1 1 nil nil nil 8 '' nil 134217729 nil nil nil nil nil nil nil 2160 nil nil 386 #selector 8 #() nil 1350 1 #{Graphics.IconImageManager} nil 386 #printIDL 8 #() nil nil nil nil 2258 1 3142 5 #{UI.ListViewColumn} 8 '' 341 #left 386 #key 8 #() 386 #<= 8 #() nil nil 2160 nil 3 386 #value 2640 nil #report 8 #() nil 133121 nil 1 nil nil nil nil 1 262 #{UI.ListViewVirtualUpdateMode} 2160 nil nil nil nil nil nil nil nil nil 514 50 2 562 #createWindow: 50 1 610 642 8 #[154 1 0 0 0 0 0 0 72 2 0 0 92 0 0 0] 193 2192 8 '' 2160 562 #contextMenu: 50 1 2336 2160 3 8 #() 722 193 193 nil 35 61 false 306 #{Core.IdentityDictionary} 50 4 2160 8 'eventNames' 1904 8 'inspector' nil 514 50 1 562 #createWindow: 50 1 610 642 8 #[0 0 0 0 174 0 0 0 72 2 0 0 10 1 0 0] 193 1808 8 '' 1776 3 50 3 1904 18 #{UI.Splitter} 50 12 nil 1776 50 2 8 1140850688 1 3152 nil nil nil 517 nil nil nil 3142 1 #{UI.DraggableViewInteractor} 3152 nil 1 #left nil nil nil 722 1 1 722 9 9 nil 3248 nil 514 50 1 562 #createWindow: 50 1 610 642 8 #[149 1 0 0 0 0 0 0 154 1 0 0 92 0 0 0] 193 3184 8 '' 3152 3 8 #() 722 193 193 nil 27 2160 722 193 193 nil 27 61 898 18 #{UI.MultilineTextEdit} 50 20 nil 752 50 2 8 1142952260 262145 3472 nil 994 #white nil 5 2322 nil true 50 4 2370 1 2402 #copySelection 8 '&Copy' 1 1 nil nil nil 326 1 #{UI.DividerMenuItem} 4097 2370 1 2402 #selectAll 8 '&Select All' 1 1 nil nil nil 2370 1 2402 #clear 8 'Clea&r' 1 1 nil nil nil 8 '' nil 134217729 nil nil nil nil nil nil nil 3472 nil nil 518 #{UI.NullConverter} nil nil 11 #focusLost nil nil nil 514 50 2 562 #createWindow: 50 1 610 642 8 #[0 0 0 0 15 1 0 0 72 2 0 0 63 1 0 0] 193 3504 nil 3472 562 #contextMenu: 50 1 3552 3472 3 8 #() 722 193 193 nil 45 31 true 306 #{Core.IdentityDictionary} 50 4 928 8 'site' 3472 8 'trace' nil 514 50 1 562 #createWindow: 50 1 610 642 8 #[0 0 0 0 0 0 0 0 72 2 0 0 63 1 0 0] 193 784 8 '' 752 3 50 5 928 18 #{UI.Splitter} 50 12 nil 752 50 2 8 1140850688 1 4192 nil nil nil 517 nil nil nil 3218 4192 nil 1 #left nil nil nil 3248 3264 nil 3248 nil 514 50 1 562 #createWindow: 50 1 610 642 8 #[0 0 0 0 169 0 0 0 72 2 0 0 174 0 0 0] 193 4224 8 '' 4192 3 8 #() 722 193 193 nil 27 1776 18 #{UI.Splitter} 50 12 nil 752 50 2 8 1140850688 1 4432 nil nil nil 517 nil nil nil 3218 4432 nil 1 #left nil nil nil 3248 3264 nil 3248 nil 514 50 1 562 #createWindow: 50 1 610 642 8 #[0 0 0 0 10 1 0 0 72 2 0 0 15 1 0 0] 193 4464 8 '' 4432 3 8 #() 722 193 193 nil 27 3472 722 193 193 nil 27 550 #{Core.IdentityDictionary} 0 nil 2886 4 #{UI.MenuBar} nil true 50 7 2322 nil true 50 3 2370 1 2402 #fileNewControl 8 '&New…' 9373 1 nil nil nil 3634 4097 2370 1 2402 #exit 8 '&Close' 17639 1 2374 2 #{Graphics.TextTileIcon} 8 $✖ nil nil nil nil nil 1 nil nil nil nil 8 '&File' nil 134217729 nil nil nil nil nil 2322 nil true 50 6 2370 1 2402 #cutSelection 8 'Cu&t' 1 1 838 4 #{Graphics.Icon} nil true 1030 #{Graphics.ImageFromStringResourceInitializer} 8 'EditCut.ico' 838 1 #{External.ResourceLibrary} 8 'dolphindr8.dll' 65541 nil nil nil 2370 1 2402 #copySelection 8 '&Copy' 1 1 5058 nil true 5090 8 'EditCopy.ico' 5152 65541 nil nil nil 2370 1 2402 #pasteClipboard 8 '&Paste' 1 1 5058 nil true 5090 8 'EditPaste.ico' 5152 65541 nil nil nil 2370 1 2402 #clearSelection 8 'De&lete' 1 1 5058 nil true 5090 8 'EditClear.ico' 5152 65541 nil nil nil 3634 4097 2370 1 2402 #editProperties 8 'P&roperties' 1267 1 nil nil nil 8 '&Edit' nil 134217729 nil nil nil nil nil 2322 nil true 50 6 2370 1 2402 #browseIt 8 '&Browse It' 9349 1 5058 nil true 5090 8 'ClassBrowserShell.ico' 5152 65541 nil nil nil 2370 1 2402 #displayIt 8 '&Display It' 9353 1 5058 nil true 5090 8 'DisplayIt.ico' 5152 65541 nil nil nil 2370 1 2402 #printIt 8 '&Print It' 9377 1 5058 nil true 5090 8 'PrintIt.ico' 5152 65541 nil nil nil 2370 1 2402 #evaluateIt 8 '&Evaluate It' 9355 1 5058 nil true 5090 8 'EvaluateIt.ico' 5152 65541 nil nil nil 2370 1 2402 #inspectIt 8 '&Inspect It' 9363 1 5058 nil true 5090 8 'BasicInspector.ico' 5152 65541 nil nil nil 2370 1 2402 #debugIt 8 'Deb&ug It' 1269 1 5058 nil true 5090 8 'Debugger.ico' 5152 65541 nil nil nil 8 '&Workspace' nil 134217729 nil nil nil nil nil 2322 nil true 50 7 2370 1 2402 #toggleAllowWindowless 8 'Allow &Windowless' 1 1 nil nil nil 2370 1 2402 #toggleAllowShowUI 8 'Allow Show &UI' 1 1 nil nil nil 2370 1 2402 #toggleDisplayAsDefault 8 'Display as &Default' 1 1 nil nil nil 2370 1 2402 #toggleDesignMode 8 'Design &Mode' 1 1 nil nil nil 3634 4097 2322 nil true 50 3 2370 1 2402 #toggleFireControlEvents 8 '&Enable' 1 1 nil nil nil 2370 1 2402 #toggleFirePropertyNotifications 8 '&Property Notifications' 1 1 nil nil nil 2370 1 2402 #toggleEventTracing 8 '&Trace' 1 1 nil nil nil 8 '&Events' nil 134217729 nil nil nil nil nil 2322 nil true 50 3 2370 1 2402 #chooseAmbientForecolor 8 'Foreground &Color' 1 1 nil nil nil 2370 1 2402 #chooseAmbientBackcolor 8 '&Background Color' 1 1 nil nil nil 2370 1 2402 #chooseAmbientFont 8 '&Font' 1 1 nil nil nil 8 '&Ambient' nil 134217729 nil nil nil nil nil 8 '&Options' nil 134217729 nil nil nil nil nil 2322 nil true 8 #() 8 '&Tools' #toolsMenu 134217729 nil nil nil nil nil 2322 nil true 8 #() 8 'Wi&ndow' #windowMenu 134217729 nil nil nil nil nil 2322 nil true 50 15 2370 1 2402 #helpContents 8 '&Contents' 1025 1 5058 nil true 5090 8 'Help.ico' 5152 65541 nil nil nil 2370 1 2402 #help 8 'On this &Tool' 1249 1 nil nil nil 2370 1 2402 #helpWhatsThis 8 'What''s This?' 5345 1 nil nil nil 3634 4097 2370 1 2402 #learnSmalltalk 8 'Learn Smalltalk' 1 1 4914 8 $🎓 nil 994 #orchid 3536 nil nil 1 nil nil nil nil 3634 4097 2370 1 2402 #helpWhatsNew 8 'What''s &New' 1 1 nil nil nil 2370 1 2402 #helpGuidedTour 8 '&Guided Tour' 1 1 nil nil nil 2370 1 2402 #helpTutorials 8 'Tutorials' 1 1 nil nil nil 3634 4097 2370 1 2402 #dolphinHomePage 8 'Object Arts Homepage' 1 1 5058 nil true 5090 8 '!!APPLICATION' 5152 65541 nil nil nil 2370 1 2402 #dolphinNewsgroup 8 'Dolphin Newsgroup/Forum' 1 1 nil nil nil 2370 1 2402 #dolphinWikiWeb 8 'Dolphin WikiWeb' 1 1 nil nil nil 3634 4097 2370 1 2402 #aboutDolphin 8 '&About Dolphin Smalltalk' 1 1 4914 8 $🐬 nil nil 994 #darkSlateBlue nil nil 1 nil nil nil nil 8 '&Help' #help 134217729 nil nil nil nil nil 8 '' nil 134217729 nil nil nil nil nil nil nil nil 1 nil nil nil nil 1 nil 193 514 50 1 562 #createWindow: 50 1 610 518 #{Graphics.Rectangle} 722 7679 21 722 8879 821 193 80 8 'Active-X Control Browser' 32 1 50 2 752 160 722 193 193 nil 27)!

toolsFolderHelpId
	^10603! !

!Tools.AXControlBrowser class categoriesForMethods!
displayOn:!displaying!public! !
icon!constants!public! !
initialize!initializing!private! !
resource_Default_view!public!resources-views! !
toolsFolderHelpId!constants!public! !
!

