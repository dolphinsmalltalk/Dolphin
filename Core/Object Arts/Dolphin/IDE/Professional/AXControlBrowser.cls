"Filed out from Dolphin Smalltalk 7"!

SmalltalkToolShell subclass: #AXControlBrowser
	instanceVariableNames: 'site windowStatus inspector eventsPresenter flags traceWindow progId'
	classVariableNames: 'TraceMask'
	poolDictionaries: ''
	classInstanceVariableNames: ''!
AXControlBrowser guid: (GUID fromString: '{e2e33600-aa60-11d3-b376-080046056106}')!
AXControlBrowser comment: 'AXControlBrowser is a simple Active-X control (OCX) browser and test environment.

AXControlBrowser show

Instance Variables:
	site			<ActiveXHost> which is hosting the embedded Active-X Control.
	windowStatus	<ValueHolder>
	inspector		<AXInspector>. Inspector on the control''s default dispinterface.
	eventsPresenter	<ListPresenter>. List of events in the control''s default source interface.

'!
!AXControlBrowser categoriesForClass!Development!MVP-Presenters! !
!AXControlBrowser methodsFor!

about
	"Pop up the controls about box"

	^self controlSite aboutControl!

browseIDL
	"Open a new workspace populated with the IDL for the control."

	| ti typeLib |
	ti := self controlSite controlDispatch 
				typeInfoIfNone: [^Sound warningBeep].
	typeLib := ti typeLib.
	self browseIDLOfTypeLib: typeLib!

browseIDLOfTypeLib: anAXTypeLibraryAnalyzer 
	| stream |
	stream := ReadWriteStream on: String new.
	Cursor wait showWhile: [anAXTypeLibraryAnalyzer printIDLOn: stream].
	stream reset.
	(Smalltalk developmentSystem workspaceShellClass show)
		language: #text;
		streamIn: stream!

chooseAmbientBackcolor
	"Choose and assign to the host an ambient background colour.
	N.B. This may have no effect if the control ignores the ambient background colour."

	| bc |
	bc := (ColorDialog on: self controlSite backcolor asValue)
				caption: 'Background Color';
				showModal.
	bc notNil ifTrue: [self controlSite backcolor: bc]!

chooseAmbientFont
	"Choose and assign to the host an ambient font.
	N.B. This may have no effect if the control ignores the 
	ambient font."

	FontDialog showModalOn: (self controlSite aspectValue: #font)!

chooseAmbientForecolor
	"Choose and assign to the host an ambient foreground colour.
	N.B. This may have no effect if the control ignores the ambient foreground colour."

	| colour |
	colour := (ColorDialog on: self controlSite forecolor asValue)
				caption: 'Foreground Color';
				showModal.
	colour notNil ifTrue: [self controlSite forecolor: colour]!

controlHelp
	"Open the control's help file, if any."

	| unk tiControl typeLib filename |
	unk := self controlSite controlUnknown.
	tiControl := unk coclassTypeInfoIfNone: 
					[MessageBox 
						notify: 'Sorry, there appears to be no help or type information available for the control.'.
					^self].
	typeLib := tiControl typeLib.
	filename := typeLib documentation helpfile.
	filename isEmpty 
		ifTrue: 
			[(MessageBox 
				confirm: ('Sorry, there appears to be no help available for the <1s> control.<n><n>Would you like to browse the IDL instead?' 
						expandMacrosWith: tiControl name)) 
					ifTrue: [self browseIDLOfTypeLib: typeLib]]
		ifFalse: 
			[[ShellLibrary default shellOpen: filename] on: Win32Error
				do: [:e | MessageBox errorMsg: e messageText caption: 'Unabled to open help file: ' , filename]]!

controlProperties
	"Open the control's property dialog (if possible)."

	self controlSite editControlProperties!

controlSite
	"Answer the <AXControlSite> hosting the control being tested."

	^site!

copyEventName
	"Copy the name of the currently selected event to the clipboard (useful for pasting into 
	a #createSchematicWiring/#onViewOpened method in order to wire up to events
	generated by the control)."

	(eventsPresenter selection key) printString copyToClipboard!

createComponents
	"Create the presenters contained by the receiver"

	super createComponents.
	inspector  := self add: PublishedAspectInspector new name: 'inspector'.
	eventsPresenter := self add: ListPresenter new name: 'eventNames'	!

createSchematicWiring
	"Create the trigger wiring for the receiver"
	
	super createSchematicWiring.

	eventsPresenter
		when: #drag: send: #onDragEvent: to: self!

editProperties
	"Open the control's property dialog (if possible)."

	self controlSite editControlProperties!

fileNewControl
	"Insert a new control"

	| host |
	host := self controlSite.
	progId := AXControlSite chooseControl: host progId.
	progId notNil ifTrue: [
		self progId: progId.
		host setFocus]!

initialize
	"Private - Initialize the receiver"

	super initialize.
	flags := TraceMask.!

inspectControl
	"Open an <AXInspector> on the control."

	self controlSite controlDispatch inspect!

isTracingEvents
	^flags allMask: TraceMask!

isTracingEvents: aBoolean 
	flags := flags mask: TraceMask set: aBoolean!

onDragEvent: aDragDropSession 
	"A drag has been initiated from the events list. Populate the <DragDropSession>
	accordingly (just extend the suggested drag objects by adding the string format)."

	aDragDropSession dragObjects do: [:dragee | | eventName |
		eventName := (dragee format: #Object) key.
		dragee format: #Symbol data: eventName;
			format: #String data: eventName printString]!

onEvent: aSymbol withArguments: argumentArray
	"Private - Handle the generic event from the host which details an event from the control."

	self isTracingEvents ifTrue: [
		self trace: ((String writeStream: 80)
							nextPutAll: 'Event: ';
							print: aSymbol;
							space;
							print: argumentArray;
							cr;
							contents)]!

onPropertyChanged: propertyName
	"Private - Handle a property change event from the host."

	self isTracingEvents ifTrue: [
		self trace: ((String writeStream: 80)
							nextPutAll: 'Property Changed: ';
							print: propertyName;
							cr;
							contents)]!

onPropertyChanging: propertyName accept: booleanValue
	"Private - Handle a property changing event from the host."

	self isTracingEvents ifTrue: [
		self trace: ((String writeStream: 80)
							nextPutAll: 'Property Changing: ';
							print: propertyName;
							cr;
							contents)]!

onViewOpened
	"Received when the receiver's view has been connected. "

	| statusItem |
	super onViewOpened.

	site := self view viewNamed: 'site'.
	site
		when: #axEvent:withArguments: send: #onEvent:withArguments: to: self;
		when: #controlPropertyChanged: send: #onPropertyChanged: to: self;
		when: #controlPropertyChanging:accept: send: #onPropertyChanging:accept: to: self.

	statusItem:= self view viewNamed: 'windowStatus' ifNone: [].
	statusItem notNil ifTrue: [
		windowStatus := ValueHolder new.
		statusItem model: windowStatus].
	traceWindow := self view viewNamed: 'trace'.!

progId: aString
	"Set the progId of the component loaded in the container."

	traceWindow clear.
	site progId: aString.
	self updateControlInfo
!

queryCommand: query
	"Private - Enters details about a potential command for the receiver into the 
	<CommandQuery>,  query."

	| command host |
	command := query commandSymbol.

	#copyEventName == command ifTrue: [
		query isEnabled: eventsPresenter hasSelection.
		^true].

	host := self controlSite.

	#toggleDesignMode == command ifTrue: [
		query isEnabled: true; 
			isChecked: host isDesignMode.
		^true].

	#toggleEventTracing == command ifTrue: [
		query isEnabled: true; 
			isChecked: self isTracingEvents.
		^true].

	#toggleAllowShowUI == command ifTrue: [
		query isEnabled: true;
			isChecked: host allowShowUI.
		^true].

	#toggleDisplayAsDefault == command ifTrue: [
		query isEnabled: true;
			isChecked: host isDefault.
		^true].

	#toggleAllowWindowless == command ifTrue: [
		query isEnabled: true;
			isChecked: host allowWindowless.
		^true].

	#toggleFireControlEvents == command ifTrue: [
		query isEnabled: true;
			isChecked: host firesControlEvents.
		^true].

	#toggleFirePropertyNotifications == command ifTrue: [
		query isEnabled: true;
			isChecked: host firesPropertyNotifications.
		^true].

	(#(about browseIDL controlHelp inspectControlDispatch editProperties) identityIncludes: command) ifTrue: [
		query isEnabled: progId notNil. 
		^true].

	^super queryCommand: query.
!

toggleAllowShowUI
	self controlSite allowShowUI: (self controlSite allowShowUI not)!

toggleAllowWindowless
	self controlSite allowWindowless: (self controlSite allowWindowless not)!

toggleDesignMode
	self controlSite isDesignMode: (self controlSite isDesignMode not)!

toggleDisplayAsDefault
	self controlSite isDefault: (self controlSite isDefault not)!

toggleEventTracing
	"Toggle on/off event tracing."

	self isTracingEvents: self isTracingEvents not!

toggleFireControlEvents
	"Toggle events from the control."

	self controlSite firesControlEvents: (self controlSite firesControlEvents not)!

toggleFirePropertyNotifications
	"Toggle property notifications from the control. Note that this causes the control to be 
	recreated because we can only specify this at control creation time."

	self controlSite firesPropertyNotifications: (self controlSite firesPropertyNotifications not).
	inspector object: self controlSite controlDispatch!

trace: aString
	"Private - Append some output to the receiver's trace window."

	traceWindow caretPosition: 0; replaceSelection: aString!

updateControlInfo
	"Private - Refresh any details we're displaying about the control."

	| host |
	host := self controlSite.
	self caption: host progId.
	windowStatus value: (host isWindowless ifTrue: ['Windowless'] ifFalse: ['Windowed']).
	"Note that the IDispatch instance used by an AXControlSite never changes (as of D5), but is re-used,
	therefore we must replace the entire model so that the inspector refreshes."
	inspector model: host controlDispatch asValue.
	host isSinkingEvents
		ifTrue: [ | sink funcs |
			sink := host sink.
			sink triggerIndividualEvents: false; triggerGenericEvent: true.
			funcs := sink sourceTypeInfo functions.
			eventsPresenter 
				list: (funcs collect: [:f | f selector -> f printIDL]);
				view enable]
		ifFalse: [eventsPresenter clear; view disable]! !
!AXControlBrowser categoriesFor: #about!commands!public! !
!AXControlBrowser categoriesFor: #browseIDL!commands!public! !
!AXControlBrowser categoriesFor: #browseIDLOfTypeLib:!commands!public! !
!AXControlBrowser categoriesFor: #chooseAmbientBackcolor!commands!public! !
!AXControlBrowser categoriesFor: #chooseAmbientFont!commands!public! !
!AXControlBrowser categoriesFor: #chooseAmbientForecolor!commands!public! !
!AXControlBrowser categoriesFor: #controlHelp!commands!public! !
!AXControlBrowser categoriesFor: #controlProperties!commands!public! !
!AXControlBrowser categoriesFor: #controlSite!accessing!public! !
!AXControlBrowser categoriesFor: #copyEventName!commands!public! !
!AXControlBrowser categoriesFor: #createComponents!initializing!public! !
!AXControlBrowser categoriesFor: #createSchematicWiring!initializing!public! !
!AXControlBrowser categoriesFor: #editProperties!commands!public! !
!AXControlBrowser categoriesFor: #fileNewControl!commands!public! !
!AXControlBrowser categoriesFor: #initialize!initializing!private! !
!AXControlBrowser categoriesFor: #inspectControl!commands!public! !
!AXControlBrowser categoriesFor: #isTracingEvents!commands!public! !
!AXControlBrowser categoriesFor: #isTracingEvents:!accessing!public! !
!AXControlBrowser categoriesFor: #onDragEvent:!event handling!public! !
!AXControlBrowser categoriesFor: #onEvent:withArguments:!event handling!private! !
!AXControlBrowser categoriesFor: #onPropertyChanged:!event handling!private! !
!AXControlBrowser categoriesFor: #onPropertyChanging:accept:!event handling!private! !
!AXControlBrowser categoriesFor: #onViewOpened!event handling!public! !
!AXControlBrowser categoriesFor: #progId:!operations!public! !
!AXControlBrowser categoriesFor: #queryCommand:!commands!private! !
!AXControlBrowser categoriesFor: #toggleAllowShowUI!commands!public! !
!AXControlBrowser categoriesFor: #toggleAllowWindowless!commands!public! !
!AXControlBrowser categoriesFor: #toggleDesignMode!commands!public! !
!AXControlBrowser categoriesFor: #toggleDisplayAsDefault!commands!public! !
!AXControlBrowser categoriesFor: #toggleEventTracing!commands!public! !
!AXControlBrowser categoriesFor: #toggleFireControlEvents!commands!public! !
!AXControlBrowser categoriesFor: #toggleFirePropertyNotifications!commands!public! !
!AXControlBrowser categoriesFor: #trace:!operations!private! !
!AXControlBrowser categoriesFor: #updateControlInfo!operations!private! !

!AXControlBrowser class methodsFor!

displayOn: aStream
	"Append, to aStream, a String whose characters are a representation of the receiver as a user
	would want to see it."

	aStream nextPutAll: 'Active-X Control Browser'!

icon
	"Answers an Icon that can be used to represent this class."

	^AXTypeLibraryAnalyzer icon!

initialize
	"Private - Initialize the receiver. Register the tools folder icon with SmalltalkSystem.

		self initialize.
	"

	TraceMask := 16r1.
	
	Smalltalk developmentSystem
		addAdditionalToolsFolderIcon: (SmalltalkSystemIcon show: self description: self displayString helpId: self toolsFolderHelpId );
		registerTool: self!

publishedAspects
	"Answer a <LookupTable> of the <Aspect>s published by the receiver."

	^(super publishedAspects)
		add: (Aspect boolean: #reuseIfOpen);
		add: (Aspect choice: #defaultView from: self resourceNames);
		yourself!

resource_Default_view
	"Answer the literal data from which the 'Default view' resource can be reconstituted.
	DO NOT EDIT OR RECATEGORIZE THIS METHOD.

	If you wish to modify this resource evaluate:
	ViewComposer openOn: (ResourceIdentifier class: self selector: #resource_Default_view)
	"

	^#(#'!!STL' 4 788558 10 ##(Smalltalk.STBViewProxy) ##(Smalltalk.ShellView) 34 27 nil nil 8 #(13565952 65536) 416 nil 655878 ##(Smalltalk.ThemeColor) #toolBackground nil 549 nil nil nil 416 788230 ##(Smalltalk.BorderLayout) 1 1 nil 410 ##(Smalltalk.StatusBar) 34 18 nil 416 34 2 8 1140850956 1 528 nil nil nil 7 nil 263174 ##(Smalltalk.Font) nil true 459014 ##(Smalltalk.LOGFONT) 8 #[243 255 255 255 0 0 0 0 0 0 0 0 0 0 0 0 144 1 0 0 0 0 0 0 3 2 1 34 65 114 105 97 108 0 183 4 128 213 87 1 0 0 204 53 87 1 3 0 34 59 0 0 0 0 15 1 31 86 135 1] 328198 ##(Smalltalk.Point) 193 193 nil 528 nil 8 1895487424 170 192 34 2 853766 ##(Smalltalk.StatusBarItem) 1 201 528 nil ##(Smalltalk.BasicListAbstract) ##(Smalltalk.IconicListAbstract) 1049926 1 ##(Smalltalk.IconImageManager) 8 'windowStatus' 34 1 768 1115142 ##(Smalltalk.StatusBarNullItem) 513 1 528 nil nil 983302 ##(Smalltalk.MessageSequence) 138 144 34 1 721670 ##(Smalltalk.MessageSend) #createAt:extent: 34 2 674 1 639 674 1169 45 528 983302 ##(Smalltalk.WINDOWPLACEMENT) 8 #[44 0 0 0 0 0 0 0 1 0 0 0 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 0 0 0 0 63 1 0 0 72 2 0 0 85 1 0 0] 8 #() 674 193 193 nil 27 nil nil 410 ##(Smalltalk.ContainerView) 34 15 nil 416 34 2 8 1174405120 131073 1104 nil nil nil 7 nil nil nil 1104 1180166 ##(Smalltalk.ProportionalLayout) 170 176 34 6 410 ##(Smalltalk.AXControlSite) 34 25 nil 1104 34 2 8 1073741824 262145 1232 nil 786694 ##(Smalltalk.ColorDefault) 8 4278190080 nil 7 nil nil nil 1232 nil 537177485 8 'mshtml:<h2>Active-X Control Browser</h2>This browser allows you to load various ActiveX controls and to inspects their methods, properties and events. Choose <I>File/New (Ctrl+N)</I> to bring up a list of controls available on your computer.<h4>Copyright © Object Arts Ltd, 2000</h4>' 590854 ##(Smalltalk.IDispatch) nil nil 1378630 1 ##(Smalltalk.TKindDispatchAnalyzer) 656134 ##(Smalltalk.ITypeInfo2) nil nil 525062 ##(Smalltalk.TYPEATTR) nil 1408 1440 nil 1378630 2 ##(Smalltalk.AXTypeLibraryAnalyzer) 590342 ##(Smalltalk.ITypeLib2) nil nil 2049 524550 ##(Smalltalk.TLIBATTR) 8 #[197 241 80 48 181 152 207 17 187 130 0 170 0 189 206 11 0 0 0 0 1 0 0 0 4 0 0 0 8 0 0 0] nil nil nil nil 170 176 34 6 8 'PROPVARIANT' #VARIANT 8 'GUID' #GUID 8 'RemotableHandle' #ExternalHandle nil nil nil 1329 nil nil nil nil 918022 ##(Smalltalk.IDolphinAxHost) nil nil 722438 ##(Smalltalk.AXEventSink) 170 176 34 76 2091 #ondeactivate 2095 #onbeforeactivate 2069 #onbeforedeactivate 2047 #oncontextmenu 2053 #onstop 2055 #onbeforeeditfocus 8 -2147418108 #onbeforeupdate 8 -2147418107 #onafterupdate 8 -2147418103 #onmouseout 8 -2147418102 #onhelp 8 -2147418104 #onmouseover 8 -2147418106 #onrowexit 8 -2147418105 #onrowenter 8 -2147418101 #ondragstart 8 -2147418100 #onselectstart 8 -2147418099 #onerrorupdate 8 -2147418093 #onpropertychange 8 -2147418098 #ondatasetchanged 8 -2147418097 #ondataavailable -1217 #onreadystatechange 8 -2147418080 #onrowsdelete -1213 #onmouseup -1211 #onmousemove -1209 #onmousedown -1207 #onkeyup -1205 #onkeypress -1203 #onkeydown -1201 #ondblclick -1199 #onclick 8 -2147418079 #onrowsinserted 8 -2147418078 #oncellchange 8 -2147418096 #ondatasetcomplete 2075 #onselectionchange 2073 #oncontrolselect 2067 #onmousewheel 2097 #onfocusin 2099 #onfocusout 2089 #onactivate 1232 1049094 ##(Smalltalk.IConnectionPoint) nil nil 46459201 1394 1426 nil nil 1458 nil 2080 2096 nil 1504 nil 262198 ##(Smalltalk.GUID) 16 96 242 80 48 181 152 207 17 187 130 0 170 0 189 206 11 1279 nil nil nil 1 3787 170 176 8 #(-525 #ReadyState 1005 #activeElement) 1508358 ##(Smalltalk.IAxWinAmbientDispatchEx) nil nil nil nil 524806 ##(Smalltalk.IUnknown) nil nil nil nil nil 882 138 144 34 3 946 #createAt:extent: 34 2 674 1 1 674 1169 339 1232 946 #restoreAmbientProperties 8 #() 1232 946 #controlBinaryStoreBytes: 34 1 8 #[60 0 104 0 50 0 62 0 65 0 99 0 116 0 105 0 118 0 101 0 45 0 88 0 32 0 67 0 111 0 110 0 116 0 114 0 111 0 108 0 32 0 66 0 114 0 111 0 119 0 115 0 101 0 114 0 60 0 47 0 104 0 50 0 62 0 84 0 104 0 105 0 115 0 32 0 98 0 114 0 111 0 119 0 115 0 101 0 114 0 32 0 97 0 108 0 108 0 111 0 119 0 115 0 32 0 121 0 111 0 117 0 32 0 116 0 111 0 32 0 108 0 111 0 97 0 100 0 32 0 118 0 97 0 114 0 105 0 111 0 117 0 115 0 32 0 65 0 99 0 116 0 105 0 118 0 101 0 88 0 32 0 99 0 111 0 110 0 116 0 114 0 111 0 108 0 115 0 32 0 97 0 110 0 100 0 32 0 116 0 111 0 32 0 105 0 110 0 115 0 112 0 101 0 99 0 116 0 115 0 32 0 116 0 104 0 101 0 105 0 114 0 32 0 109 0 101 0 116 0 104 0 111 0 100 0 115 0 44 0 32 0 112 0 114 0 111 0 112 0 101 0 114 0 116 0 105 0 101 0 115 0 32 0 97 0 110 0 100 0 32 0 101 0 118 0 101 0 110 0 116 0 115 0 46 0 32 0 67 0 104 0 111 0 111 0 115 0 101 0 32 0 60 0 73 0 62 0 70 0 105 0 108 0 101 0 47 0 78 0 101 0 119 0 32 0 40 0 67 0 116 0 114 0 108 0 43 0 78 0 41 0 60 0 47 0 73 0 62 0 32 0 116 0 111 0 32 0 98 0 114 0 105 0 110 0 103 0 32 0 117 0 112 0 32 0 97 0 32 0 108 0 105 0 115 0 116 0 32 0 111 0 102 0 32 0 99 0 111 0 110 0 116 0 114 0 111 0 108 0 115 0 32 0 97 0 118 0 97 0 105 0 108 0 97 0 98 0 108 0 101 0 32 0 111 0 110 0 32 0 121 0 111 0 117 0 114 0 32 0 99 0 111 0 109 0 112 0 117 0 116 0 101 0 114 0 46 0 60 0 104 0 52 0 62 0 67 0 111 0 112 0 121 0 114 0 105 0 103 0 104 0 116 0 32 0 169 0 32 0 79 0 98 0 106 0 101 0 99 0 116 0 32 0 65 0 114 0 116 0 115 0 32 0 76 0 116 0 100 0 44 0 32 0 50 0 48 0 48 0 48 0 60 0 47 0 104 0 52 0 62 0] 1232 1026 8 #[44 0 0 0 0 0 0 0 1 0 0 0 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 0 0 0 0 0 0 0 0 72 2 0 0 169 0 0 0] 8 #() 1088 nil 27 111 410 ##(Smalltalk.MultilineTextEdit) 34 16 nil 1104 34 2 8 1142952260 262145 2496 nil 196934 1 ##(Smalltalk.RGB) 33554431 nil 7 265030 4 ##(Smalltalk.Menu) nil true 34 4 984134 2 ##(Smalltalk.CommandMenuItem) 1 1180998 4 ##(Smalltalk.CommandDescription) #copySelection 8 '&Copy' 1 1 nil nil nil 983366 1 ##(Smalltalk.DividerMenuItem) 4097 2642 1 2674 #selectAll 8 '&Select All' 1 1 nil nil nil 2642 1 2674 #clear 8 'Clea&r' 1 1 nil nil nil 8 '' nil 134217729 nil nil nil nil nil nil nil 2496 nil 8 1895678768 852486 ##(Smalltalk.NullConverter) nil nil 11 882 138 144 34 2 946 #createAt:extent: 34 2 674 1 543 674 1169 97 2496 946 #contextMenu: 34 1 2608 2496 1026 8 #[44 0 0 0 0 0 0 0 1 0 0 0 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 0 0 0 0 15 1 0 0 72 2 0 0 63 1 0 0] 8 #() 1088 nil 27 31 410 ##(Smalltalk.ContainerView) 34 15 nil 1104 34 2 8 1140850688 131073 3104 nil nil nil 7 nil nil nil 3104 1170 170 176 34 4 410 ##(Smalltalk.ListView) 34 30 nil 3104 34 2 8 1140936773 262145 3216 590662 2 ##(Smalltalk.ListModel) 138 144 2384 nil 1310726 ##(Smalltalk.IdentitySearchPolicy) 1312 nil 15 2594 nil true 34 1 2642 2097153 2674 #copyEventName 8 '&Copy' 1 1 nil nil nil 8 '' nil 134217729 nil nil nil nil nil nil nil 3216 nil 8 1895760912 459270 ##(Smalltalk.Message) #selector 8 #() nil 800 nil 3474 #printIDL 8 #() nil nil nil nil 138 144 34 1 920646 5 ##(Smalltalk.ListViewColumn) 8 '' 341 #left 3474 #key 8 #() ##(Smalltalk.SortedCollection) nil nil 3216 nil 3 3474 #value 3648 nil #report 2384 nil 133121 nil 34 4 nil nil 674 1 1 nil 882 138 144 34 2 946 #createAt:extent: 34 2 674 821 1 674 349 185 3216 946 #contextMenu: 34 1 3360 3216 1026 8 #[44 0 0 0 0 0 0 0 1 0 0 0 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 154 1 0 0 0 0 0 0 72 2 0 0 92 0 0 0] 8 #() 1088 nil 27 61 410 ##(Smalltalk.ReferenceView) 34 14 nil 3104 34 2 8 1140850688 131073 3904 nil nil nil 7 nil nil nil 3904 1180166 ##(Smalltalk.ResourceIdentifier) ##(Smalltalk.PublishedAspectInspector) #resource_Default_view nil 882 138 144 34 1 946 #createAt:extent: 34 2 674 1 1 674 811 185 3904 1026 8 #[44 0 0 0 0 0 0 0 1 0 0 0 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 0 0 0 0 0 0 0 0 149 1 0 0 92 0 0 0] 2384 1088 nil 27 141 false 170 192 34 4 3216 8 'eventNames' 3904 8 'inspector' nil 882 138 144 34 1 946 #createAt:extent: 34 2 674 1 349 674 1169 185 3104 1026 8 #[44 0 0 0 0 0 0 0 1 0 0 0 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 0 0 0 0 174 0 0 0 72 2 0 0 10 1 0 0] 34 3 3904 410 ##(Smalltalk.Splitter) 34 12 nil 3104 34 2 8 1140850688 1 4368 nil nil nil 519 nil nil nil 1510470 1 ##(Smalltalk.DraggableViewInteractor) 4368 nil 1 #left nil nil nil 674 1 1 674 9 9 nil 4464 nil 882 138 144 34 1 946 #createAt:extent: 34 2 674 811 1 674 11 185 4368 1026 8 #[44 0 0 0 0 0 0 0 1 0 0 0 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 149 1 0 0 0 0 0 0 154 1 0 0 92 0 0 0] 8 #() 1088 nil 27 3216 1088 nil 27 61 true 170 192 34 4 1232 8 'site' 2496 8 'trace' nil 882 138 144 34 1 946 #createAt:extent: 34 2 674 1 1 674 1169 639 1104 1026 8 #[44 0 0 0 0 0 0 0 1 0 0 0 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 0 0 0 0 0 0 0 0 72 2 0 0 63 1 0 0] 34 5 1232 410 ##(Smalltalk.Splitter) 34 12 nil 1104 34 2 8 1140850688 1 4880 nil nil nil 519 nil nil nil 4434 4880 nil 1 #left nil nil nil 4464 4480 nil 4464 nil 882 138 144 34 1 946 #createAt:extent: 34 2 674 1 339 674 1169 11 4880 1026 8 #[44 0 0 0 0 0 0 0 1 0 0 0 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 0 0 0 0 169 0 0 0 72 2 0 0 174 0 0 0] 8 #() 1088 nil 27 3104 410 ##(Smalltalk.Splitter) 34 12 nil 1104 34 2 8 1140850688 1 5120 nil nil nil 519 nil nil nil 4434 5120 nil 1 #left nil nil nil 4464 4480 nil 4464 nil 882 138 144 34 1 946 #createAt:extent: 34 2 674 1 533 674 1169 11 5120 1026 8 #[44 0 0 0 0 0 0 0 1 0 0 0 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 0 0 0 0 10 1 0 0 72 2 0 0 15 1 0 0] 8 #() 1088 nil 27 2496 1088 nil 27 170 192 2384 nil 461638 4 ##(Smalltalk.MenuBar) nil true 34 7 2594 nil true 34 3 2642 1 2674 #fileNewControl 8 '&New...' 9373 1 nil nil nil 2722 4097 2642 1 2674 #exit 8 '&Close' 17639 1 788806 1 ##(Smalltalk.TextTileIcon) $\x2716 nil nil nil nil nil 1 nil nil nil nil 8 '&File' nil 134217729 nil nil 13263 nil nil 2594 nil true 34 6 2642 1 2674 #cutSelection 8 'Cu&t' 1 1 263494 3 ##(Smalltalk.Icon) nil true 1572870 ##(Smalltalk.ImageRelativeFileLocator) 8 'EditCut.ico' 2032142 ##(Smalltalk.STBExternalResourceLibraryProxy) 8 'dolphindr7.dll' nil nil nil 2642 1 2674 #copySelection 8 '&Copy' 1 1 5698 nil true 5744 8 'EditCopy.ico' 5792 nil nil 2642 1 2674 #pasteClipboard 8 '&Paste' 1 1 5698 nil true 5744 8 'EditPaste.ico' 5792 nil nil 2642 1 2674 #clearSelection 8 'De&lete' 1 1 5698 nil true 5744 8 'EditClear.ico' 5792 nil nil 2722 4097 2642 1 2674 #editProperties 8 'P&roperties' 1267 1 nil nil nil 8 '&Edit' nil 134217729 nil nil 13275 nil nil 2594 nil true 34 5 2642 1 2674 #browseIt 8 '&Browse It' 9349 1 5698 nil true 5744 8 'ClassBrowserShell.ico' 5792 nil nil 2642 1 2674 #displayIt 8 '&Display It' 9353 1 5698 nil true 5744 8 'DisplayIt.ico' 5792 nil nil 2642 1 2674 #evaluateIt 8 '&Evaluate It' 9355 1 5698 nil true 5744 8 'EvaluateIt.ico' 5792 nil nil 2642 1 2674 #inspectIt 8 '&Inspect It' 9363 1 5698 nil true 5744 8 'BasicInspector.ico' 5792 nil nil 2642 1 2674 #debugIt 8 'Deb&ug It' 1269 1 5698 nil true 5744 8 'Debugger.ico' 5792 nil nil 8 '&Workspace' nil 134217729 nil nil 13287 nil nil 2594 nil true 34 7 2642 1 2674 #toggleAllowWindowless 8 'Allow &Windowless' 1 1 nil nil nil 2642 1 2674 #toggleAllowShowUI 8 'Allow Show &UI' 1 1 nil nil nil 2642 1 2674 #toggleDisplayAsDefault 8 'Display as &Default' 1 1 nil nil nil 2642 1 2674 #toggleDesignMode 8 'Design &Mode' 1 1 nil nil nil 2722 4097 2594 nil true 34 3 2642 1 2674 #toggleFireControlEvents 8 '&Enable' 1 1 nil nil nil 2642 1 2674 #toggleFirePropertyNotifications 8 '&Property Notifications' 1 1 nil nil nil 2642 1 2674 #toggleEventTracing 8 '&Trace' 1 1 nil nil nil 8 '&Events' nil 134217729 nil nil 13303 nil nil 2594 nil true 34 3 2642 1 2674 #chooseAmbientForecolor 8 'Foreground &Color' 1 1 nil nil nil 2642 1 2674 #chooseAmbientBackcolor 8 '&Background Color' 1 1 nil nil nil 2642 1 2674 #chooseAmbientFont 8 '&Font' 1 1 nil nil nil 8 '&Ambient' nil 134217729 nil nil 13311 nil nil 8 '&Options' nil 134217729 nil nil 13313 nil nil 2594 nil true 8 #() 8 '&Tools' #toolsMenu 134217729 nil nil 13315 nil nil 2594 nil true 8 #() 8 'Wi&ndow' #windowMenu 134217729 nil nil 13317 nil nil 2594 nil true 34 15 2642 1 2674 #helpContents 8 '&Contents' 1025 1 5698 nil true 5744 8 'Help.ico' 5792 nil nil 2642 1 2674 #help 8 'On this &Tool' 1249 1 nil nil nil 2642 1 2674 #helpWhatsThis 8 'What''s This?' 5345 1 nil nil nil 2722 4097 2642 1 2674 #learnSmalltalk 8 'Learn Smalltalk' 1 1 5570 $\x1F393 nil 262406 ##(Smalltalk.ARGB) 8 4292695180 786694 ##(Smalltalk.IndexedColor) 33554471 nil nil 1 nil nil nil nil 2722 4097 2642 1 2674 #helpWhatsNew 8 'What''s &New' 1 1 nil nil nil 2642 1 2674 #helpGuidedTour 8 '&Guided Tour' 1 1 nil nil nil 2642 1 2674 #helpTutorials 8 'Tutorials' 1 1 nil nil nil 2722 4097 2642 1 2674 #objectArtsHomePage 8 'Object Arts Homepage' 1 1 5698 nil true 5744 8 '!!APPLICATION' 5792 nil nil 2642 1 2674 #dolphinNewsgroup 8 'Dolphin Newsgroup/Forum' 1 1 nil nil nil 2642 1 2674 #dolphinWikiWeb 8 'Dolphin WikiWeb' 1 1 nil nil nil 2722 4097 2642 1 2674 #aboutDolphin 8 '&About Dolphin Smalltalk' 1 1 5570 $\x1F42C nil nil 2562 17741887 nil nil 1 nil nil nil nil 8 '&Help' nil 134217729 nil nil 13341 nil nil 8 '' nil 134217729 nil nil nil nil nil nil nil nil 1 nil nil nil nil 1 nil nil 882 138 144 34 3 946 #createAt:extent: 34 2 674 3839 21 674 1201 801 416 946 #text: 34 1 8 'Active-X Control Browser' 416 946 #updateMenuBar 2384 416 1026 8 #[44 0 0 0 0 0 0 0 0 0 0 0 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 255 127 7 0 0 10 0 0 0 215 9 0 0 154 1 0 0] 34 2 1104 528 1088 nil 27 )!

toolsFolderHelpId
	^10603!

uninitialize
	"Private - Un-register the system tools folder icon for the receiver to allow clean
	removal of this class from the system.
		self uninitialize
	"

	Smalltalk developmentSystem
		removeSystemFolderIconNamed: self displayString;
		unregisterTool: self! !
!AXControlBrowser class categoriesFor: #displayOn:!displaying!public! !
!AXControlBrowser class categoriesFor: #icon!constants!public! !
!AXControlBrowser class categoriesFor: #initialize!initializing!must strip!private! !
!AXControlBrowser class categoriesFor: #publishedAspects!development!public! !
!AXControlBrowser class categoriesFor: #resource_Default_view!public!resources-views! !
!AXControlBrowser class categoriesFor: #toolsFolderHelpId!constants!public! !
!AXControlBrowser class categoriesFor: #uninitialize!initializing!private! !

