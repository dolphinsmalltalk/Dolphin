"Filed out from Dolphin Smalltalk"!

SmalltalkToolShell subclass: #AXControlBrowser
	instanceVariableNames: 'site windowStatus inspector eventsPresenter flags traceWindow progId'
	classVariableNames: 'TraceMask'
	poolDictionaries: ''
	classInstanceVariableNames: ''!

AXControlBrowser guid: (GUID fromString: '{e2e33600-aa60-11d3-b376-080046056106}')!

AXControlBrowser addClassConstant: 'TraceMask' value: 16r1!

AXControlBrowser comment: 'AXControlBrowser is a simple Active-X control (OCX) browser and test environment.

AXControlBrowser show

Instance Variables:
	site			<ActiveXHost> which is hosting the embedded Active-X Control.
	windowStatus	<ValueHolder>
	inspector		<AXInspector>. Inspector on the control''s default dispinterface.
	eventsPresenter	<ListPresenter>. List of events in the control''s default source interface.

'!

!AXControlBrowser categoriesForClass!MVP-Presenters! !

!AXControlBrowser methodsFor!

about
	"Pop up the controls about box"

	^self controlSite aboutControl!

browseIDL
	"Open a new workspace populated with the IDL for the control."

	| ti typeLib |
	ti := self controlSite controlDispatch 
				typeInfoIfNone: [^Sound warningBeep].
	typeLib := ti typeLib.
	self browseIDLOfTypeLib: typeLib!

browseIDLOfTypeLib: anAXTypeLibraryAnalyzer 
	| stream |
	stream := ReadWriteStream on: String new.
	Cursor wait showWhile: [anAXTypeLibraryAnalyzer printIDLOn: stream].
	stream reset.
	(Smalltalk developmentSystem workspaceShellClass show)
		language: #text;
		streamIn: stream!

chooseAmbientBackcolor
	"Choose and assign to the host an ambient background colour.
	N.B. This may have no effect if the control ignores the ambient background colour."

	| bc |
	bc := (ColorDialog on: self controlSite backcolor asValue)
				caption: 'Background Color';
				showModal.
	bc notNil ifTrue: [self controlSite backcolor: bc]!

chooseAmbientFont
	"Choose and assign to the host an ambient font.
	N.B. This may have no effect if the control ignores the 
	ambient font."

	FontDialog showModalOn: (self controlSite aspectValue: #font)!

chooseAmbientForecolor
	"Choose and assign to the host an ambient foreground colour.
	N.B. This may have no effect if the control ignores the ambient foreground colour."

	| colour |
	colour := (ColorDialog on: self controlSite forecolor asValue)
				caption: 'Foreground Color';
				showModal.
	colour notNil ifTrue: [self controlSite forecolor: colour]!

controlHelp
	"Open the control's help file, if any."

	| unk tiControl typeLib filename |
	unk := self controlSite controlUnknown.
	tiControl := unk coclassTypeInfoIfNone: 
					[MessageBox 
						notify: 'Sorry, there appears to be no help or type information available for the control.'.
					^self].
	typeLib := tiControl typeLib.
	filename := typeLib documentation helpfile.
	filename isEmpty 
		ifTrue: 
			[(MessageBox 
				confirm: ('Sorry, there appears to be no help available for the <1s> control.<n><n>Would you like to browse the IDL instead?' 
						expandMacrosWith: tiControl name)) 
					ifTrue: [self browseIDLOfTypeLib: typeLib]]
		ifFalse: 
			[[ShellLibrary default shellOpen: filename] on: Win32Error
				do: [:e | MessageBox errorMsg: e messageText caption: 'Unabled to open help file: ' , filename]]!

controlProperties
	"Open the control's property dialog (if possible)."

	self controlSite editControlProperties!

controlSite
	"Answer the <AXControlSite> hosting the control being tested."

	^site!

copyEventName
	"Copy the name of the currently selected event to the clipboard (useful for pasting into 
	a #createSchematicWiring/#onViewOpened method in order to wire up to events
	generated by the control)."

	(eventsPresenter selection key) printString copyToClipboard!

createComponents
	"Create the presenters contained by the receiver"

	super createComponents.
	inspector  := self add: PublishedAspectInspector new name: 'inspector'.
	eventsPresenter := self add: ListPresenter new name: 'eventNames'	!

createSchematicWiring
	"Create the trigger wiring for the receiver"
	
	super createSchematicWiring.

	eventsPresenter
		when: #drag: send: #onDragEvent: to: self!

editProperties
	"Open the control's property dialog (if possible)."

	self controlSite editControlProperties!

fileNewControl
	"Insert a new control"

	| host |
	host := self controlSite.
	progId := AXControlSite chooseControl: host progId.
	progId notNil ifTrue: [
		self progId: progId.
		host setFocus]!

initialize
	"Private - Initialize the receiver"

	super initialize.
	flags := TraceMask.!

inspectControl
	"Open an <AXInspector> on the control."

	self controlSite controlDispatch inspect!

isTracingEvents
	^flags allMask: TraceMask!

isTracingEvents: aBoolean 
	flags := flags mask: TraceMask set: aBoolean!

onDragEvent: aDragDropSession 
	"A drag has been initiated from the events list. Populate the <DragDropSession>
	accordingly (just extend the suggested drag objects by adding the string format)."

	aDragDropSession dragObjects do: [:dragee | | eventName |
		eventName := (dragee format: #Object) key.
		dragee format: #Symbol data: eventName;
			format: #String data: eventName printString]!

onEvent: aSymbol withArguments: argumentArray
	"Private - Handle the generic event from the host which details an event from the control."

	self isTracingEvents ifTrue: [
		self trace: ((String writeStream: 80)
							nextPutAll: 'Event: ';
							print: aSymbol;
							space;
							print: argumentArray;
							cr;
							contents)]!

onPropertyChanged: propertyName
	"Private - Handle a property change event from the host."

	self isTracingEvents ifTrue: [
		self trace: ((String writeStream: 80)
							nextPutAll: 'Property Changed: ';
							print: propertyName;
							cr;
							contents)]!

onPropertyChanging: propertyName accept: booleanValue
	"Private - Handle a property changing event from the host."

	self isTracingEvents ifTrue: [
		self trace: ((String writeStream: 80)
							nextPutAll: 'Property Changing: ';
							print: propertyName;
							cr;
							contents)]!

onViewOpened
	"Received when the receiver's view has been connected. "

	| statusItem |
	super onViewOpened.

	site := self view viewNamed: 'site'.
	site
		when: #axEvent:withArguments: send: #onEvent:withArguments: to: self;
		when: #controlPropertyChanged: send: #onPropertyChanged: to: self;
		when: #controlPropertyChanging:accept: send: #onPropertyChanging:accept: to: self.

	statusItem:= self view viewNamed: 'windowStatus' ifNone: [].
	statusItem notNil ifTrue: [
		windowStatus := ValueHolder new.
		statusItem model: windowStatus].
	traceWindow := self view viewNamed: 'trace'.!

progId: aString
	"Set the progId of the component loaded in the container."

	traceWindow clear.
	site progId: aString.
	self updateControlInfo
!

queryCommand: query
	"Private - Enters details about a potential command for the receiver into the 
	<CommandQuery>,  query."

	| command host |
	command := query commandSymbol.

	#copyEventName == command ifTrue: [
		query isEnabled: eventsPresenter hasSelection.
		^true].

	host := self controlSite.

	#toggleDesignMode == command ifTrue: [
		query isEnabled: true; 
			isChecked: host isDesignMode.
		^true].

	#toggleEventTracing == command ifTrue: [
		query isEnabled: true; 
			isChecked: self isTracingEvents.
		^true].

	#toggleAllowShowUI == command ifTrue: [
		query isEnabled: true;
			isChecked: host allowShowUI.
		^true].

	#toggleDisplayAsDefault == command ifTrue: [
		query isEnabled: true;
			isChecked: host isDefault.
		^true].

	#toggleAllowWindowless == command ifTrue: [
		query isEnabled: true;
			isChecked: host allowWindowless.
		^true].

	#toggleFireControlEvents == command ifTrue: [
		query isEnabled: true;
			isChecked: host firesControlEvents.
		^true].

	#toggleFirePropertyNotifications == command ifTrue: [
		query isEnabled: true;
			isChecked: host firesPropertyNotifications.
		^true].

	(#(about browseIDL controlHelp inspectControlDispatch editProperties) identityIncludes: command) ifTrue: [
		query isEnabled: progId notNil. 
		^true].

	^super queryCommand: query.
!

toggleAllowShowUI
	self controlSite allowShowUI: (self controlSite allowShowUI not)!

toggleAllowWindowless
	self controlSite allowWindowless: (self controlSite allowWindowless not)!

toggleDesignMode
	self controlSite isDesignMode: (self controlSite isDesignMode not)!

toggleDisplayAsDefault
	self controlSite isDefault: (self controlSite isDefault not)!

toggleEventTracing
	"Toggle on/off event tracing."

	self isTracingEvents: self isTracingEvents not!

toggleFireControlEvents
	"Toggle events from the control."

	self controlSite firesControlEvents: (self controlSite firesControlEvents not)!

toggleFirePropertyNotifications
	"Toggle property notifications from the control. Note that this causes the control to be 
	recreated because we can only specify this at control creation time."

	self controlSite firesPropertyNotifications: (self controlSite firesPropertyNotifications not).
	inspector object: self controlSite controlDispatch!

trace: aString
	"Private - Append some output to the receiver's trace window."

	traceWindow caretPosition: 0; replaceSelection: aString!

updateControlInfo
	"Private - Refresh any details we're displaying about the control."

	| host |
	host := self controlSite.
	self caption: host progId.
	windowStatus value: (host isWindowless ifTrue: ['Windowless'] ifFalse: ['Windowed']).
	"Note that the IDispatch instance used by an AXControlSite never changes (as of D5), but is re-used,
	therefore we must replace the entire model so that the inspector refreshes."
	inspector model: host controlDispatch asValue.
	host isSinkingEvents
		ifTrue: [ | sink funcs |
			sink := host sink.
			sink triggerIndividualEvents: false; triggerGenericEvent: true.
			funcs := sink sourceTypeInfo functions.
			eventsPresenter 
				list: (funcs collect: [:f | f selector -> f printIDL]);
				view enable]
		ifFalse: [eventsPresenter clear; view disable]! !

!AXControlBrowser categoriesForMethods!
about!commands!public! !
browseIDL!commands!public! !
browseIDLOfTypeLib:!commands!public! !
chooseAmbientBackcolor!commands!public! !
chooseAmbientFont!commands!public! !
chooseAmbientForecolor!commands!public! !
controlHelp!commands!public! !
controlProperties!commands!public! !
controlSite!accessing!public! !
copyEventName!commands!public! !
createComponents!initializing!public! !
createSchematicWiring!initializing!public! !
editProperties!commands!public! !
fileNewControl!commands!public! !
initialize!initializing!private! !
inspectControl!commands!public! !
isTracingEvents!commands!public! !
isTracingEvents:!accessing!public! !
onDragEvent:!event handling!public! !
onEvent:withArguments:!event handling!private! !
onPropertyChanged:!event handling!private! !
onPropertyChanging:accept:!event handling!private! !
onViewOpened!event handling!public! !
progId:!operations!public! !
queryCommand:!commands!private! !
toggleAllowShowUI!commands!public! !
toggleAllowWindowless!commands!public! !
toggleDesignMode!commands!public! !
toggleDisplayAsDefault!commands!public! !
toggleEventTracing!commands!public! !
toggleFireControlEvents!commands!public! !
toggleFirePropertyNotifications!commands!public! !
trace:!operations!private! !
updateControlInfo!operations!private! !
!

!AXControlBrowser class methodsFor!

displayOn: aStream
	"Append, to aStream, a String whose characters are a representation of the receiver as a user
	would want to see it."

	aStream nextPutAll: 'Active-X Control Browser'!

icon
	"Answers an Icon that can be used to represent this class."

	^AXControlSite icon!

initialize
	"Private - Initialize the receiver. Register the tools folder icon with SmalltalkSystem.

		self initialize.
	"

	self addClassConstant: 'TraceMask' value: 16r1.
	
	Smalltalk developmentSystem
		addAdditionalToolsFolderIcon: (SmalltalkSystemIcon show: self description: self displayString helpId: self toolsFolderHelpId );
		registerTool: self!

publishedAspects
	"Answer a <LookupTable> of the <Aspect>s published by the receiver."

	^(super publishedAspects)
		add: (Aspect choice: #defaultView from: self resourceNames);
		yourself!

resource_Default_view
	"Answer the literal data from which the 'Default view' resource can be reconstituted.
	DO NOT EDIT OR RECATEGORIZE THIS METHOD.

	If you wish to modify this resource evaluate:
	ViewComposer openOn: (ResourceIdentifier class: self selector: #resource_Default_view)
	"

	^#(#'!!STL' 4 788558 11 ##(Smalltalk.STBViewProxy) ##(Smalltalk.ShellView) 34 27 nil nil 8 #(13565952 65536) 416 nil 655878 ##(Smalltalk.ThemeColor) #toolBackground nil 517 nil nil nil 416 788230 ##(Smalltalk.BorderLayout) 1 1 nil 410 ##(Smalltalk.StatusBar) 34 21 nil 416 34 2 8 1140850956 1 528 nil nil nil 5 nil 263494 1 ##(Smalltalk.Font) nil true 524550 ##(Smalltalk.LOGFONTW) 8 #[243 255 255 255 0 0 0 0 0 0 0 0 0 0 0 0 144 1 0 0 0 0 0 0 3 2 1 0 65 0 114 0 105 0 97 0 108 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0] 193 nil nil 528 nil 8 1758805184 170 192 34 2 853830 1 ##(Smalltalk.StatusBarItem) 8193 201 528 nil ##(Smalltalk.BasicListAbstract) ##(Smalltalk.IconicListAbstract) nil 8 'windowStatus' nil nil nil 34 1 736 1115206 1 ##(Smalltalk.StatusBarNullItem) 8705 1 528 nil nil 983302 ##(Smalltalk.MessageSequence) 138 144 34 1 721670 ##(Smalltalk.MessageSend) #createWindow: 34 1 787462 ##(Smalltalk.CreateWindow) 262406 ##(Smalltalk.RECT) 8 #[0 0 0 0 63 1 0 0 72 2 0 0 85 1 0 0] 193 560 8 '' 528 3 8 #() 328198 ##(Smalltalk.Point) 193 193 nil 29 nil nil 410 ##(Smalltalk.ContainerView) 34 15 nil 416 34 2 8 1174405120 131073 1072 nil nil nil 5 nil nil nil 1072 1180166 ##(Smalltalk.ProportionalLayout) 170 176 34 6 410 ##(Smalltalk.AXControlSite) 34 25 nil 1072 34 2 8 1073741824 262145 1200 nil 327686 ##(Smalltalk.Color) #default nil 5 nil nil nil 1200 nil 537202249 8 'mshtml:<h2>Active-X Control Browser</h2>This browser allows you to load various ActiveX controls and to inspects their methods, properties and events. Choose <I>File/New (Ctrl+N)</I> to bring up a list of controls available on your computer.<h4>Copyright © Object Arts Ltd, 2000</h4>' 590854 ##(Smalltalk.IDispatch) nil nil 1378630 1 ##(Smalltalk.TKindDispatchAnalyzer) 656134 ##(Smalltalk.ITypeInfo2) nil nil 525062 ##(Smalltalk.TYPEATTR) nil 1360 1392 nil 1378630 2 ##(Smalltalk.AXTypeLibraryAnalyzer) 590342 ##(Smalltalk.ITypeLib2) nil nil 2049 524550 ##(Smalltalk.TLIBATTR) 8 #[197 241 80 48 181 152 207 17 187 130 0 170 0 189 206 11 0 0 0 0 1 0 0 0 4 0 0 0 8 0 0 128] nil nil nil nil 170 176 34 6 8 'PROPVARIANT' #VARIANT 8 'GUID' #GUID 8 'RemotableHandle' #ExternalHandle nil nil nil 1329 nil nil nil nil 918022 ##(Smalltalk.IDolphinAxHost) nil nil 722438 ##(Smalltalk.AXEventSink) 170 176 34 76 8 -2147418100 #onselectstart 8 -2147418099 #onerrorupdate 2073 #oncontrolselect 8 -2147418101 #ondragstart -1217 #onreadystatechange 2047 #oncontextmenu 2099 #onfocusout -1203 #onkeydown 2075 #onselectionchange 8 -2147418098 #ondatasetchanged 2089 #onactivate 8 -2147418097 #ondataavailable -1201 #ondblclick 8 -2147418093 #onpropertychange 8 -2147418105 #onrowenter -1213 #onmouseup 2091 #ondeactivate -1199 #onclick 8 -2147418078 #oncellchange 2053 #onstop -1211 #onmousemove 2067 #onmousewheel 8 -2147418079 #onrowsinserted 8 -2147418102 #onhelp 8 -2147418106 #onrowexit 2055 #onbeforeeditfocus -1209 #onmousedown 2095 #onbeforeactivate 8 -2147418104 #onmouseover 8 -2147418108 #onbeforeupdate 8 -2147418096 #ondatasetcomplete 8 -2147418107 #onafterupdate 8 -2147418103 #onmouseout 2069 #onbeforedeactivate -1207 #onkeyup 2097 #onfocusin 8 -2147418080 #onrowsdelete -1205 #onkeypress 1200 1049094 ##(Smalltalk.IConnectionPoint) nil nil 455189121 1346 1378 nil nil 1410 nil 2032 2048 nil 1442 1474 nil nil 2049 1506 8 #[197 241 80 48 181 152 207 17 187 130 0 170 0 189 206 11 0 0 0 0 1 0 0 0 4 0 0 0 8 0 0 0] nil nil nil nil 170 176 34 6 8 'PROPVARIANT' #VARIANT 8 'GUID' #GUID 8 'RemotableHandle' #ExternalHandle nil nil 262198 ##(Smalltalk.GUID) 16 96 242 80 48 181 152 207 17 187 130 0 170 0 189 206 11 1279 nil nil nil 1 3787 170 176 8 #(-525 #ReadyState) 1508358 ##(Smalltalk.IAxWinAmbientDispatchEx) nil nil nil nil 524806 ##(Smalltalk.IUnknown) nil nil nil nil nil 818 138 144 34 3 882 #createWindow: 34 1 930 962 8 #[0 0 0 0 0 0 0 0 72 2 0 0 169 0 0 0] 193 1232 8 '' 1200 882 #restoreAmbientProperties 8 #() 1200 882 #controlBinaryStoreBytes: 34 1 8 #[116 0 114 0 111 0 108 0 32 0 66 0 114 0 111 0 119 0 115 0 101 0 114 0 60 0 47 0 104 0 50 0 62 0 84 0 104 0 105 0 115 0 32 0 98 0 114 0 111 0 119 0 115 0 101 0 114 0 32 0 97 0 108 0 108 0 111 0 119 0 115 0 32 0 121 0 111 0 117 0 32 0 116 0 111 0 32 0 108 0 111 0 97 0 100 0 32 0 118 0 97 0 114 0 105 0 111 0 117 0 115 0 32 0 65 0 99 0 116 0 105 0 118 0 101 0 88 0 32 0 99 0 111 0 110 0 116 0 114 0 111 0 108 0 115 0 32 0 97 0 110 0 100 0 32 0 116 0 111 0 32 0 105 0 110 0 115 0 112 0 101 0 99 0 116 0 115 0 32 0 116 0 104 0 101 0 105 0 114 0 32 0 109 0 101 0 116 0 104 0 111 0 100 0 115 0 44 0 32 0 112 0 114 0 111 0 112 0 101 0 114 0 116 0 105 0 101 0 115 0 32 0 97 0 110 0 100 0 32 0 101 0 118 0 101 0 110 0 116 0 115 0 46 0 32 0 67 0 104 0 111 0 111 0 115 0 101 0 32 0 60 0 73 0 62 0 70 0 105 0 108 0 101 0 47 0 78 0 101 0 119 0 32 0 40 0 67 0 116 0 114 0 108 0 43 0 78 0 41 0 60 0 47 0 73 0 62 0 32 0 116 0 111 0 32 0 98 0 114 0 105 0 110 0 103 0 32 0 117 0 112 0 32 0 97 0 32 0 108 0 105 0 115 0 116 0 32 0 111 0 102 0 32 0 99 0 111 0 110 0 116 0 114 0 111 0 108 0 115 0 32 0 97 0 118 0 97 0 105 0 108 0 97 0 98 0 108 0 101 0 32 0 111 0 110 0 32 0 121 0 111 0 117 0 114 0 32 0 99 0 111 0 109 0 112 0 117 0 116 0 101 0 114 0 46 0 60 0 104 0 52 0 62 0 67 0 111 0 112 0 121 0 114 0 105 0 103 0 104 0 116 0 32 0 169 0 32 0 79 0 98 0 106 0 101 0 99 0 116 0 32 0 65 0 114 0 116 0 115 0 32 0 76 0 116 0 100 0 44 0 32 0 50 0 48 0 48 0 48 0 60 0 47 0 104 0 52 0 62 0] 1200 3 8 #() 1042 193 193 nil 27 111 410 ##(Smalltalk.ContainerView) 34 15 nil 1072 34 2 8 1140850688 131073 2608 nil nil nil 5 nil nil nil 2608 1138 170 176 34 4 410 ##(Smalltalk.ListView) 34 45 nil 2608 34 2 8 1140936773 262145 2720 590662 2 ##(Smalltalk.ListModel) 138 144 2512 nil 1310726 ##(Smalltalk.IdentitySearchPolicy) 1280 nil 13 265030 4 ##(Smalltalk.Menu) nil true 34 1 984134 2 ##(Smalltalk.CommandMenuItem) 2097153 1180998 4 ##(Smalltalk.CommandDescription) #copyEventName 8 '&Copy' 1 1 nil nil nil 8 '' nil 134217729 nil nil nil nil nil nil nil 2720 nil 8 1759068368 459270 ##(Smalltalk.Message) #selector 8 #() nil 1049926 1 ##(Smalltalk.IconImageManager) nil 3026 #printIDL 8 #() nil nil nil nil 138 144 34 1 920646 5 ##(Smalltalk.ListViewColumn) 8 '' 341 #left 3026 #key 8 #() 3026 #<= 8 #() nil nil 2720 nil 3 3026 #value 3232 nil #report 8 #() nil 133121 nil 1 nil nil nil nil nil nil nil nil nil nil nil nil nil nil nil 818 138 144 34 2 882 #createWindow: 34 1 930 962 8 #[154 1 0 0 0 0 0 0 72 2 0 0 92 0 0 0] 193 2752 8 '' 2720 882 #contextMenu: 34 1 2880 2720 3 8 #() 1042 193 193 nil 35 61 410 ##(Smalltalk.ReferenceView) 34 14 nil 2608 34 2 8 1140850688 131073 3520 nil nil nil 5 nil nil nil 3520 1180230 1 ##(Smalltalk.ResourceIdentifier) ##(Smalltalk.PublishedAspectInspector) #resource_Default_view nil 818 138 144 34 1 882 #createWindow: 34 1 930 962 8 #[0 0 0 0 0 0 0 0 149 1 0 0 92 0 0 0] 193 3552 8 '' 3520 3 2512 1042 193 193 nil 27 141 false 170 192 34 4 2720 8 'eventNames' 3520 8 'inspector' nil 818 138 144 34 1 882 #createWindow: 34 1 930 962 8 #[0 0 0 0 174 0 0 0 72 2 0 0 10 1 0 0] 193 2640 8 '' 2608 3 34 3 3520 410 ##(Smalltalk.Splitter) 34 12 nil 2608 34 2 8 1140850688 1 4000 nil nil nil 517 nil nil nil 1510470 1 ##(Smalltalk.DraggableViewInteractor) 4000 nil 1 #left nil nil nil 1042 1 1 1042 9 9 nil 4096 nil 818 138 144 34 1 882 #createWindow: 34 1 930 962 8 #[149 1 0 0 0 0 0 0 154 1 0 0 92 0 0 0] 193 4032 8 '' 4000 3 8 #() 1042 193 193 nil 27 2720 1042 193 193 nil 27 61 410 ##(Smalltalk.MultilineTextEdit) 34 16 nil 1072 34 2 8 1142952260 262145 4320 nil 1266 #white nil 5 2866 nil true 34 4 2914 1 2946 #copySelection 8 '&Copy' 1 1 nil nil nil 983366 1 ##(Smalltalk.DividerMenuItem) 4097 2914 1 2946 #selectAll 8 '&Select All' 1 1 nil nil nil 2914 1 2946 #clear 8 'Clea&r' 1 1 nil nil nil 8 '' nil 134217729 nil nil nil nil nil nil nil 4320 nil 8 1759000288 852486 ##(Smalltalk.NullConverter) nil nil 11 818 138 144 34 2 882 #createWindow: 34 1 930 962 8 #[0 0 0 0 15 1 0 0 72 2 0 0 63 1 0 0] 193 4352 nil 4320 882 #contextMenu: 34 1 4400 4320 3 8 #() 1042 193 193 nil 27 31 true 170 192 34 4 1200 8 'site' 4320 8 'trace' nil 818 138 144 34 1 882 #createWindow: 34 1 930 962 8 #[0 0 0 0 0 0 0 0 72 2 0 0 63 1 0 0] 193 1104 8 '' 1072 3 34 5 1200 410 ##(Smalltalk.Splitter) 34 12 nil 1072 34 2 8 1140850688 1 5088 nil nil nil 517 nil nil nil 4066 5088 nil 1 #left nil nil nil 4096 4112 nil 4096 nil 818 138 144 34 1 882 #createWindow: 34 1 930 962 8 #[0 0 0 0 169 0 0 0 72 2 0 0 174 0 0 0] 193 5120 8 '' 5088 3 8 #() 1042 193 193 nil 27 2608 410 ##(Smalltalk.Splitter) 34 12 nil 1072 34 2 8 1140850688 1 5344 nil nil nil 517 nil nil nil 4066 5344 nil 1 #left nil nil nil 4096 4112 nil 4096 nil 818 138 144 34 1 882 #createWindow: 34 1 930 962 8 #[0 0 0 0 10 1 0 0 72 2 0 0 15 1 0 0] 193 5376 8 '' 5344 3 8 #() 1042 193 193 nil 27 4320 1042 193 193 nil 27 170 192 2512 nil 461638 4 ##(Smalltalk.MenuBar) nil true 34 7 2866 nil true 34 3 2914 1 2946 #fileNewControl 8 '&New...' 9373 1 nil nil nil 4482 4097 2914 1 2946 #exit 8 '&Close' 17639 1 788806 1 ##(Smalltalk.TextTileIcon) $✖ nil nil nil nil nil 1 nil nil nil nil 8 '&File' nil 134217729 nil nil 11197 nil nil 2866 nil true 34 6 2914 1 2946 #cutSelection 8 'Cu&t' 1 1 263494 3 ##(Smalltalk.Icon) nil true 1572870 ##(Smalltalk.ImageRelativeFileLocator) 8 'EditCut.ico' 2032142 ##(Smalltalk.STBExternalResourceLibraryProxy) 8 'dolphindr7.dll' nil nil nil 2914 1 2946 #copySelection 8 '&Copy' 1 1 5954 nil true 6000 8 'EditCopy.ico' 6048 nil nil 2914 1 2946 #pasteClipboard 8 '&Paste' 1 1 5954 nil true 6000 8 'EditPaste.ico' 6048 nil nil 2914 1 2946 #clearSelection 8 'De&lete' 1 1 5954 nil true 6000 8 'EditClear.ico' 6048 nil nil 4482 4097 2914 1 2946 #editProperties 8 'P&roperties' 1267 1 nil nil nil 8 '&Edit' nil 134217729 nil nil 11209 nil nil 2866 nil true 34 6 2914 1 2946 #browseIt 8 '&Browse It' 9349 1 5954 nil true 6000 8 'ClassBrowserShell.ico' 6048 nil nil 2914 1 2946 #displayIt 8 '&Display It' 9353 1 5954 nil true 6000 8 'DisplayIt.ico' 6048 nil nil 2914 1 2946 #printIt 8 '&Print It' 9377 1 5954 nil true 6000 8 'PrintIt.ico' 6048 nil nil 2914 1 2946 #evaluateIt 8 '&Evaluate It' 9355 1 5954 nil true 6000 8 'EvaluateIt.ico' 6048 nil nil 2914 1 2946 #inspectIt 8 '&Inspect It' 9363 1 5954 nil true 6000 8 'BasicInspector.ico' 6048 nil nil 2914 1 2946 #debugIt 8 'Deb&ug It' 1269 1 5954 nil true 6000 8 'Debugger.ico' 6048 nil nil 8 '&Workspace' nil 134217729 nil nil 11223 nil nil 2866 nil true 34 7 2914 1 2946 #toggleAllowWindowless 8 'Allow &Windowless' 1 1 nil nil nil 2914 1 2946 #toggleAllowShowUI 8 'Allow Show &UI' 1 1 nil nil nil 2914 1 2946 #toggleDisplayAsDefault 8 'Display as &Default' 1 1 nil nil nil 2914 1 2946 #toggleDesignMode 8 'Design &Mode' 1 1 nil nil nil 4482 4097 2866 nil true 34 3 2914 1 2946 #toggleFireControlEvents 8 '&Enable' 1 1 nil nil nil 2914 1 2946 #toggleFirePropertyNotifications 8 '&Property Notifications' 1 1 nil nil nil 2914 1 2946 #toggleEventTracing 8 '&Trace' 1 1 nil nil nil 8 '&Events' nil 134217729 nil nil 11239 nil nil 2866 nil true 34 3 2914 1 2946 #chooseAmbientForecolor 8 'Foreground &Color' 1 1 nil nil nil 2914 1 2946 #chooseAmbientBackcolor 8 '&Background Color' 1 1 nil nil nil 2914 1 2946 #chooseAmbientFont 8 '&Font' 1 1 nil nil nil 8 '&Ambient' nil 134217729 nil nil 11247 nil nil 8 '&Options' nil 134217729 nil nil 11249 nil nil 2866 nil true 8 #() 8 '&Tools' #toolsMenu 134217729 nil nil 11251 nil nil 2866 nil true 8 #() 8 'Wi&ndow' #windowMenu 134217729 nil nil 11253 nil nil 2866 nil true 34 15 2914 1 2946 #helpContents 8 '&Contents' 1025 1 5954 nil true 6000 8 'Help.ico' 6048 nil nil 2914 1 2946 #help 8 'On this &Tool' 1249 1 nil nil nil 2914 1 2946 #helpWhatsThis 8 'What''s This?' 5345 1 nil nil nil 4482 4097 2914 1 2946 #learnSmalltalk 8 'Learn Smalltalk' 1 1 5826 $🎓 nil 1266 #orchid 4384 nil nil 1 nil nil nil nil 4482 4097 2914 1 2946 #helpWhatsNew 8 'What''s &New' 1 1 nil nil nil 2914 1 2946 #helpGuidedTour 8 '&Guided Tour' 1 1 nil nil nil 2914 1 2946 #helpTutorials 8 'Tutorials' 1 1 nil nil nil 4482 4097 2914 1 2946 #dolphinHomePage 8 'Dolphin Homepage' 1 1 5954 nil true 6000 8 '!!APPLICATION' 6048 nil nil 2914 1 2946 #dolphinNewsgroup 8 'Dolphin Newsgroup/Forum' 1 1 nil nil nil 2914 1 2946 #dolphinWikiWeb 8 'Dolphin WikiWeb' 1 1 nil nil nil 4482 4097 2914 1 2946 #aboutDolphin 8 '&About Dolphin Smalltalk' 1 1 5826 $🐬 nil nil 1266 #darkSlateBlue nil nil 1 nil nil nil nil 8 '&Help' #help 134217729 nil nil 11277 nil nil 8 '' nil 134217729 nil nil nil nil nil nil nil nil 1 nil nil nil nil 1 nil 193 818 138 144 34 1 882 #createWindow: 34 1 930 590342 ##(Smalltalk.Rectangle) 1042 1 1 1042 1201 801 193 448 8 'Active-X Control Browser' 416 1 34 2 1072 528 1042 193 193 nil 27 )!

toolsFolderHelpId
	^10603! !

!AXControlBrowser class categoriesForMethods!
displayOn:!displaying!public! !
icon!constants!public! !
initialize!initializing!must strip!private! !
publishedAspects!public! !
resource_Default_view!public!resources-views! !
toolsFolderHelpId!constants!public! !
!

