| package |
package := Package name: 'Application Deployment Kit'.
package paxVersion: 2.1;
	preDeclareClassesOnLoad: false;
	basicComment: 'Dolphin Smalltalk Application Deployment Kit. 
Copyright (c) Object Arts Ltd, 1998-2006.

This package enables applications to be deployed to executable files using the Lagoon deployment wizard.

For documentation evaluate:
	SmalltalkSystem help: ''deployingapplications''
'.

package basicPackageVersion: '6.2'.


package setClassNames: #(
	#{Tools.ApplicationDeploymentWizard}
).

package setMethodNames: #(
	#(#{Kernel.ConsoleSessionManager class} #icon)
	#(#{Kernel.Package} #hasImageStripper)
	#(#{Kernel.Package} #imageStripper)
	#(#{Kernel.Package} #rehydrateImageStripper:)
	#(#{OS.VersionResource class} #publishedAspectsOfInstances)
	#(#{OS.VS_VERSION_INFO_HEADER} #printOn:)
	#(#{Smalltalk.BasicImageStripper} #getEvents)
	#(#{Smalltalk.BasicImageStripper} #postCopy)
	#(#{Smalltalk.BasicImageStripper} #setEvents:)
	#(#{Smalltalk.ImageStripper} #canDeploy)
	#(#{Smalltalk.ImageStripper} #compileExternalStructures:)
	#(#{Smalltalk.ImageStripper} #defaultSessionManagerClass)
	#(#{Smalltalk.ImageStripper} #deploy)
	#(#{Smalltalk.ImageStripper} #editCustomOptions)
	#(#{Smalltalk.ImageStripper} #executableName:)
	#(#{Smalltalk.ImageStripper} #ignoreViewReferences:)
	#(#{Smalltalk.ImageStripper} #includeManifestResource:)
	#(#{Smalltalk.ImageStripper} #initialize)
	#(#{Smalltalk.ImageStripper} #isToGo:)
	#(#{Smalltalk.ImageStripper} #isUnattended:)
	#(#{Smalltalk.ImageStripper} #logReferencesToClasses:)
	#(#{Smalltalk.ImageStripper} #preserveAspectSetters:)
	#(#{Smalltalk.ImageStripper} #preStripScript:)
	#(#{Smalltalk.ImageStripper} #queryCommand:)
	#(#{Smalltalk.ImageStripper} #retainInstVarNames:)
	#(#{Smalltalk.ImageStripper} #rootPackage:)
	#(#{Smalltalk.ImageStripper} #runtimeSessionManagerClass:)
	#(#{Smalltalk.ImageStripper} #snapshotType:)
	#(#{Smalltalk.ImageStripper} #stripClassBuilder:)
	#(#{Smalltalk.ImageStripper} #stripClassGuids:)
	#(#{Smalltalk.ImageStripper} #stripClassInfo:)
	#(#{Smalltalk.ImageStripper} #stripDeprecatedMethods:)
	#(#{Smalltalk.ImageStripper} #stripDuplicateBytecodes:)
	#(#{Smalltalk.ImageStripper} #stripDuplicateStrings:)
	#(#{Smalltalk.ImageStripper} #stripRedundantClasses:)
	#(#{Smalltalk.ImageStripper} #stripRedundantMethods:)
	#(#{Smalltalk.ImageStripper} #stripRedundantPackages:)
	#(#{Smalltalk.ImageStripper} #stripRedundantResources:)
	#(#{Smalltalk.ImageStripper} #stripShouldNotImplement:)
	#(#{Smalltalk.ImageStripper} #stripSubclassResponsibility:)
	#(#{Smalltalk.ImageStripper} #stubFilePath:)
	#(#{Smalltalk.ImageStripper} #versionResource:)
	#(#{Smalltalk.ImageStripper} #writeLogFile:)
	#(#{Smalltalk.ImageStripper class} #for:)
	#(#{Smalltalk.ImageStripper class} #publishedAspectsOfInstances)
	#(#{Smalltalk.ImageStripper class} #stbConvertFrom:)
	#(#{Smalltalk.ImageStripper class} #stbVersion)
	#(#{Tools.PackageSelector} #deploy)
).

package setPrerequisites: #(
	'Console Application Kit'
	'..\IDE\Base\Development System'
	'..\Base\Dolphin'
	'..\MVP\Base\Dolphin Base Dialogs'
	'..\MVP\Base\Dolphin Basic Geometry'
	'..\MVP\Presenters\Boolean\Dolphin Boolean Presenter'
	'..\MVP\Views\Cards\Dolphin Card Containers'
	'..\MVP\Views\Buttons\Dolphin Check Buttons'
	'..\MVP\Presenters\Choice\Dolphin Choice Presenter'
	'..\MVP\Dialogs\Common\Dolphin Common Dialogs'
	'..\MVP\Views\Control Bars\Dolphin Control Bars'
	'..\MVP\Graphics\Dolphin GDI Graphics'
	'..\MVP\Views\GroupBox\Dolphin GroupBox'
	'..\MVP\Presenters\Image\Dolphin Image Presenter'
	'..\MVP\Models\List\Dolphin List Models'
	'..\MVP\Presenters\List\Dolphin List Presenter'
	'..\System\Win32\MessageBox\Dolphin Message Box'
	'..\MVP\Base\Dolphin MVP Base'
	'..\IDE\Professional\Dolphin Professional Tools'
	'..\MVP\Views\Buttons\Dolphin Push Buttons'
	'..\MVP\Presenters\Text\Dolphin Rich Text Presenter'
	'..\MVP\Presenters\Text\Dolphin Text Presenter'
	'..\MVP\Type Converters\Dolphin Type Converters'
	'..\MVP\Models\Value\Dolphin Value Models'
	'..\MVP\Gdiplus\Gdiplus'
	'Lagoon Image Stripper'
).

package!

"Class Definitions"!

UI.ValueDialog subclass: #'Tools.ApplicationDeploymentWizard'
	instanceVariableNames: 'imageStripperClass executableName executablePath1 executablePath2 rootPackage runtimeSessionManagerClass stripRedundantMethods stripDeprecatedMethods stripShouldNotImplement stripRedundantPackages stripRedundantClasses preserveAspectSetters stripDuplicateStrings stripDuplicateBytecodes stripClassInfo retainInstVarNames stripClassGuids stripClassBuilder unused stripRedundantResources compileExternalStructures writeLogFile preStripScriptWorkspace preStripScriptErrors logFileName logReferencesToClasses stepsView isToGo stripSubclassResponsibility'
	classVariableNames: 'SaveImageOnDeploy'
	imports: #()
	classInstanceVariableNames: ''
	classConstants: {}!

"Loose Methods"!

!Kernel.ConsoleSessionManager class methodsFor!

icon
	"Answers an Icon that can be used to represent this class"

	^##(self) defaultIcon! !
!Kernel.ConsoleSessionManager class categoriesForMethods!
icon!constants!development!public! !
!

!Kernel.Package methodsFor!

hasImageStripper
	^imageStripperBytes notNil and: [self imageStripper notNil]!

imageStripper
	"Answer an image stripper for the receiver, or nil ImageStripper class is not present.
	Since we have to be able to load the receiver into images that do no necessarily have the 
	ImageStripper class installed, we hold the stripper as a binary filed out ByteArray in imageStripperBytes.
	We reconstitute this here."

	^self rehydrateImageStripper: Smalltalk.ImageStripper!

rehydrateImageStripper: stripperClass 
	| stripper |
	stripper := imageStripperBytes notNil ifTrue: [Object fromBinaryStoreBytes: imageStripperBytes].
	stripper isNil 
		ifTrue: 
			[stripper := (stripperClass new)
						rootPackage: self;
						stripRedundantClasses: true;
						yourself]
		ifFalse: 
			["If the package has been renamed the STB'd image stripper might lose its root package"
			stripper rootPackage isNil ifTrue: [stripper rootPackage: self]].
	^stripper! !
!Kernel.Package categoriesForMethods!
hasImageStripper!private!testing! !
imageStripper!accessing!public! !
rehydrateImageStripper:!accessing!private! !
!

!OS.VersionResource class methodsFor!

publishedAspectsOfInstances
	"Answer a <LookupTable> of the <Aspect>s published by instances of the receiver."

	<namespace: Tools>
	^super publishedAspectsOfInstances
		add: (Aspect string: #fileVersion);
		add: (Aspect string: #productVersion);
		add: (Aspect name: #stringTables);
		add: (Aspect name: #translations);
		yourself! !
!OS.VersionResource class categoriesForMethods!
publishedAspectsOfInstances!constants!development!public! !
!

!OS.VS_VERSION_INFO_HEADER methodsFor!

printOn: target
	target
		basicPrint: self;
		nextPut: $(;
		nextPutAll: self key;
		nextPutAll: ', wLength = '; print: self wLength;
		nextPutAll: ', wValueLength='; print: self wValueLength;
		nextPutAll: ', wType='; print: self wType;
		nextPut: $)
	! !
!OS.VS_VERSION_INFO_HEADER categoriesForMethods!
printOn:!printing!public! !
!

!BasicImageStripper methodsFor!

getEvents
	"Private - Answer the EventsCollection belonging to the receiver, or nil if the receiver
	has no events registered for it"

	^events!

postCopy
	"Apply any final flourish to the copy that may be required in order to ensure that the copy
	does not share any state with the original. Answer the receiver."

	events := nil.
	^self!

setEvents: anEventsCollectionOrNil
	"Private - Set the EventsCollection of the receiver to be anEventsCollectionOrNil.
	Answer the receiver."

	events := anEventsCollectionOrNil! !
!BasicImageStripper categoriesForMethods!
getEvents!events!private! !
postCopy!copying!public! !
setEvents:!events!private! !
!

!ImageStripper methodsFor!

canDeploy
	"Answer true if the receiver is currently in a state where it can be deployed."

	^(self executableName ifNil: [false] ifNotNil: [:name | name notEmpty]) and: [true]!

compileExternalStructures: aBoolean
	"Set the compile ExternalStructures option of the stripping process to aBoolean"

	flags := flags mask: CompileStructsMask set: aBoolean!

defaultSessionManagerClass
	"Answer the default class of SessionManager to be installed into the 
	deployed image."

	^RuntimeSessionManager!

deploy
	self canDeploy ifFalse: [self error: 'Deployment configuration incomplete'].
	SessionManager inputState queueDeferredAction: (MessageSend receiver: self selector: #stripAndSaveWithProgress) !

editCustomOptions
	"Open a <PublishedAspectInspector> on the version resource."

	| inspector |
	inspector := #{PublishedAspectInspector} value shellOn: self.
	inspector topShell caption: self class name , ' Properties'!

executableName: aString
	"Set the executable name to which the resultant stripped image will
	be saved"

	executableName := FileLocator imageRelative relativePathTo: aString.
	executableName notEmpty ifTrue: [
		"Default the file extension and save the path relative to the installation directory"
		executableName := File default: executableName extension: self exeFileExtension ].
!

ignoreViewReferences: aBoolean
	"Set whether view resources should NOT be scanned for references to other
	view resources."

	flags := flags mask: IgnoreViewReferencesMask set: aBoolean!

includeManifestResource: aBoolean 
	"Set whether the resulting executable will include a Windows XP application manifest
	resource."

	flags := flags mask: NoXPManifestMask set: aBoolean not!

initialize
	"Private - Initialize the receiver"

	self 
		executableName: '';
		runtimeSessionManagerClass: self defaultSessionManagerClass.
	flags := self class defaultOptions.
	
	!

isToGo: aBoolean 
	"Set whether the resulting executable is to be deployed standalone (that is without
	a separate VM DLL)."

	"Ensure path nilled if using default"

	self stubFilePath: self stubFilePath.
	flags := flags mask: ToGoMask set: aBoolean!

isUnattended: aBoolean 
	"Controls whether or not any prompts for user input will be displayed during the deployment,
	e.g. the final success message box. By default this is disabled (i.e. attended deploiyment)."

	flags := flags mask: UnattendedMask set: aBoolean!

logReferencesToClasses: aBoolean
	"Set whether the remaining classes log should include references to those classes for	
	debugging purposes."

	flags := flags mask: LogReferencesMask set: aBoolean
!

preserveAspectSetters: aBoolean
	"Sets the preserve aspect setters option of the receiver to aBoolean.
	See comment in #preserveAspectSetters"

	flags := flags mask: PreserveAspectsMask set: aBoolean!

preStripScript: aString 
	"Set the pre-image stripping script which allows for custom image stripping actions."

	preStripScript := (aString notNil and: [aString notEmpty]) ifTrue: [aString]!

queryCommand: query 
	"Private - Enter details about a potential command for the receiver 
	into the <CommandQuery>, query."

	(query canPerformAgainst: self) ifTrue: [query isEnabled: true].
	^false	"There was no specific enablement of the command at this level"!

retainInstVarNames: aBoolean
	"Set the retain Retain Instance Variable Names option of the stripping process to aBoolean"

	flags := flags mask: RetainInstVarNamesMask set: aBoolean!

rootPackage: aPackageOrNil
	"Sets the root package associated with the receiver."

	| sessionManager |
	rootPackageName := aPackageOrNil notNil ifTrue: [aPackageOrNil name].
	aPackageOrNil notNil ifFalse: [^self].
	sessionManager := aPackageOrNil classes detect: [:each | each includesBehavior: SessionManager]
				ifNone: nil.
	sessionManager notNil ifTrue: [self runtimeSessionManagerClass: sessionManager]!

runtimeSessionManagerClass: aSessionManagerClass 
	"Set the class of runtime session manager that the receiver will install into a stripped image"

	aSessionManagerClass asSessionManagerClass isRuntime 
		ifFalse: [^self error: aSessionManagerClass name , ' is not a runtime session manager'].
	runtimeSessionManagerClass := aSessionManagerClass!

snapshotType: anInteger
	(anInteger between: -1 and: 9) 
		ifFalse: [self error: 'snapshotType out of range (-1..9)'].
	snapshotType := anInteger!

stripClassBuilder: aBoolean
	"Set the strip class builder option of the stripping process to aBoolean."

	flags := flags mask: StripClassBuilderMask set: aBoolean!

stripClassGuids: aBoolean
	"Set whether the class GUIDs should be nulled at the end of the stripping process.
	This is appropriate unless the application makes use of these to uniquely identify classes.
	Note that guids of COMInterface's subclasses will not be nulled, as these are in fact the
	IIDs of the corresponding interfaces."

	flags := flags mask: ClearGuidsMask set: aBoolean!

stripClassInfo: aBoolean
	"Set the strip class info option of the stripping process to aBoolean"

	flags := flags mask: StripClassInfoMask set: aBoolean!

stripDeprecatedMethods: aBoolean
	"Sets the option to remove deprecated methods during the 
	receiver's stripping process."

	flags := flags mask: StripDeprecatedMask set: aBoolean
!

stripDuplicateBytecodes: aBoolean
	"Set the bytecode folding option of the stripping process to aBoolean."

	flags := flags mask: FoldBytecodesMask set: aBoolean!

stripDuplicateStrings: aBoolean
	"Set the fold duplicate strings option of the stripping process to aBoolean"

	flags := flags mask: FoldStringsMask set: aBoolean!

stripRedundantClasses: aBoolean
	"Sets the option to remove redundant classes during the receiver's stripping process
	to aBoolean. Note that setting this to true will significantly increase the time take to 
	strip an image"

	flags := flags mask: StripClassesMask set: aBoolean!

stripRedundantMethods: aBoolean
	"Sets the option to remove redundant methods during the receiver's stripping process
	to aBoolean. Note that setting this to true significantly increases the time take to 
	strip an image"

	flags := flags mask: StripMethodsMask set: aBoolean!

stripRedundantPackages: aBoolean
	"Sets the option to remove redundant packages during the receiver's stripping process
	to aBoolean."

	flags := flags mask: StripPackagesMask set: aBoolean
!

stripRedundantResources: aBoolean
	"Sets the option to remove redundant resources during the receiver's stripping process
	to aBoolean."

	flags := flags mask: StripResourcesMask set: aBoolean
!

stripShouldNotImplement: aBoolean
	"Sets the option to remove 'self shouldNotImplement' methods during the 
	receiver's stripping process."

	flags := flags mask: StripShouldNotImplMask set: aBoolean
!

stripSubclassResponsibility: aBoolean 
	"Set/reset the option to remove those methods which consist of a 'self
	subclassResponsibility' error report, depending on the <Boolean> argument. It should be safe
	to remove such methods in a properly tested application, since they are intended as an
	indication of a logic error for the developer."

	flags := flags mask: RetainSubclassRespMask set: aBoolean not!

stubFilePath: aString
	"Set the path of a custom stub file."

	stubFilePath := (aString isNil or: [aString sameAs: self _defaultStubFilePath]) 
				ifFalse: [FileLocator imageRelative relativePathTo: aString]!

versionResource: aVersionResource
	"Set the <VersionResource> describing the version resource to be built into	
	the deployed application/dll."

	versionResource := aVersionResource!

writeLogFile: aBoolean
	"Sets the option to write a log file during the receiver's stripping process
	to aBoolean."

	flags := flags mask: WriteLogMask set: aBoolean! !
!ImageStripper categoriesForMethods!
canDeploy!public!testing! !
compileExternalStructures:!accessing!public! !
defaultSessionManagerClass!accessing!public! !
deploy!operations!public! !
editCustomOptions!accessing!public! !
executableName:!accessing!public! !
ignoreViewReferences:!accessing!public! !
includeManifestResource:!accessing!public! !
initialize!initializing!private! !
isToGo:!accessing!public! !
isUnattended:!accessing!public! !
logReferencesToClasses:!accessing!public! !
preserveAspectSetters:!accessing!public! !
preStripScript:!accessing!public! !
queryCommand:!commands!private! !
retainInstVarNames:!accessing!public! !
rootPackage:!accessing!public! !
runtimeSessionManagerClass:!accessing!public! !
snapshotType:!accessing!public! !
stripClassBuilder:!accessing!public! !
stripClassGuids:!accessing!public! !
stripClassInfo:!accessing!public! !
stripDeprecatedMethods:!accessing!public! !
stripDuplicateBytecodes:!accessing!public! !
stripDuplicateStrings:!accessing!public! !
stripRedundantClasses:!accessing!public! !
stripRedundantMethods:!accessing!public! !
stripRedundantPackages:!accessing!public! !
stripRedundantResources:!accessing!public! !
stripShouldNotImplement:!accessing!public! !
stripSubclassResponsibility:!accessing!public! !
stubFilePath:!accessing!public! !
versionResource:!accessing!public! !
writeLogFile:!accessing!public! !
!

!ImageStripper class methodsFor!

for: aPackage
	"Answers an instance of the receiver suitable for stripping aPackage"

	^self new rootPackage: aPackage!

publishedAspectsOfInstances
	"Answer a <LookupTable> of the <Aspect>s published by instances of the receiver."

	| aspects |
	aspects := super publishedAspectsOfInstances.
	#(#stripClassGuids #stripDuplicateStrings #preserveAspectSetters #retainInstVarNames #stripRedundantClasses #stripClassInfo #stripClassBuilder #stripRedundantMethods #stripDeprecatedMethods #stripShouldNotImplement #stripSubclassResponsibility #stripRedundantResources #stripRedundantPackages #compileExternalStructures #writeLogFile #stripDuplicateBytecodes #logReferencesToClasses #isToGo #includeManifestResource #isUnattended)
		do: [:each | aspects add: (Aspect boolean: each)].
	aspects
		add: ((Aspect
					choice: #runtimeSessionManagerClass
					from: [SessionManager allSubclasses select: [:e | e isRuntime]]
					type: SessionManager class)
					icon: SessionManager icon;
					yourself);
		add: ((Aspect fileSave: #executableName)
					beNonNullable;
					yourself);
		add: (Aspect multilineString: #preStripScript);
		add: (Aspect name: #versionResource);
		add: (Aspect fileOpen: #stubFilePath);
		yourself.
	^aspects!

stbConvertFrom: anSTBClassFormat
	"Convert from previous version. 
		1: Added a retainPachydermInfo instance var (removed in v3).
		2: Added a rootClasses instance var and the events instance var in model.
		3: Consolidates all flags into one integer flags variable.
		4:  isToGo flag added (the default)
		5: supersentMessages and spare inst vars added
		6: runFlags added and spare inst vars added"

	^
	[:data |
	| newInstance ver |
	newInstance := self basicNew.
	ver := anSTBClassFormat version.
	ver < 2
		ifTrue: [1 to: data size do: [:i | newInstance instVarAt: i + 1 put: (data at: i)]]
		ifFalse: [1 to: data size do: [:i | newInstance instVarAt: i put: (data at: i)]].
	ver < 3
		ifTrue: 
			[| stripClasses base var flags |
			"Former stripRedundantClasses boolean slot now used for flags variable to hold all booleans"
			var := ImageStripper indexOfInstVar: 'flags'.
			stripClasses := newInstance instVarAt: var.

			"Initialize flags"
			flags := self defaultOptions | StripPackagesMask.
			stripClasses ifTrue: [flags := flags | StripClassesMask].
			newInstance instVarAt: var put: flags.

			"Convert former boolean vars to individual bits in the flags variable."
			base := self superclass instSize.
			#(5 6 7 8 9 10 13)
				with: #(#stripRedundantMethods: #preserveAspectSetters: #stripDuplicateStrings: #compileExternalStructures: #writeLogFile: #stripClassInfo: #retainInstVarNames:)
				do: 
					[:i :setter |
					newInstance perform: setter with: (newInstance instVarAt: i + base).
					newInstance instVarAt: i + base put: nil]].
	ver < 4
		ifTrue: 
			["Default is now 'To Go' deployment"
			newInstance isToGo: true].
	newInstance]!

stbVersion
	"Answer the current binary filer version number for instances of the receiver."

	^6! !
!ImageStripper class categoriesForMethods!
for:!instance creation!public! !
publishedAspectsOfInstances!development!public! !
stbConvertFrom:!binary filing!development!public! !
stbVersion!binary filing!development!public! !
!

!Tools.PackageSelector methodsFor!

deploy
	"Open a new Application Deployment Wizard on the image stripper of the current package."

	ApplicationDeploymentWizard showModalOn: (self singlePackage aspectValue: #imageStripper)! !
!Tools.PackageSelector categoriesForMethods!
deploy!commands!public! !
!

"End of package definition"!

