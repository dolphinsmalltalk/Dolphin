"Filed out from Dolphin Smalltalk"!

Refactory.Browser.Tests.RefactoringTest subclass: #'Refactory.Browser.Tests.ExtractMethodTest'
	instanceVariableNames: ''
	classVariableNames: ''
	imports: #()
	classInstanceVariableNames: ''
	classConstants: {}!
Refactory.Browser.Tests.ExtractMethodTest guid: (Core.GUID fromString: '{e6d6d232-8068-4ed9-97f4-59b812529daf}')!
Refactory.Browser.Tests.ExtractMethodTest comment: ''!
!Refactory.Browser.Tests.ExtractMethodTest categoriesForClass!Refactory-Testing! !
!Refactory.Browser.Tests.ExtractMethodTest methodsFor!

testBadInterval
	self
		shouldFail: (ExtractMethodRefactoring
					extract: (self convertInterval: (24 to: 30) for: (RefactoryTestDataApp sourceCodeAt: #testMethod))
					from: #testMethod
					in: RefactoryTestDataApp)
			withMessage: 'Invalid interval';
		shouldFail: (ExtractMethodRefactoring
					extract: (self convertInterval: (80 to: 147)
							for: (BasicLintRuleTestData class sourceCodeAt: #subclassOf:overrides:))
					from: #subclassOf:overrides:
					in: BasicLintRuleTestData class)
			withMessage: 'Invalid source to extract'!

testExtractFailure
	self
		shouldFail: (ExtractMethodRefactoring
					extract: (self convertInterval: (80 to: 269)
							for: (BasicLintRuleTestData class sourceCodeAt: #subclassOf:overrides:))
					from: #subclassOf:overrides:
					in: BasicLintRuleTestData class)
			withMessage: 'Couldn''t extract code since it contains a return.';
		shouldFail: (ExtractMethodRefactoring
					extract: (self convertInterval: (53 to: 56)
							for: (BasicLintRuleTestData class sourceCodeAt: #subclassOf:overrides:))
					from: #subclassOf:overrides:
					in: BasicLintRuleTestData class)
			withMessage: 'Cannot extract left hand side of an assignment';
		shouldFail: (ExtractMethodRefactoring
					extract: (self convertInterval: (77 to: 222)
							for: (BasicLintRuleTestData class sourceCodeAt: #subclassResponsibilityNotDefined))
					from: #subclassResponsibilityNotDefined
					in: BasicLintRuleTestData class)
			withMessage: 'Cannot extract assignment without all references: #(detector subclassResponsibilitySymbol)'!

testExtractFromMethodInCustomNamespace
	| method source refactoring class start stop |
	method := Graphics.Rectangle class compiledMethodAt: #publishedAspectsOfInstances.
	self assert: method environment equals: Tools.
	source := method getSource.
	start := source indexOf: $(.
	stop := source
				nextIndexOf: $)
				from: start
				to: source size.
	refactoring := ExtractMethodRefactoring
				extract: (start to: stop)
				from: #publishedAspectsOfInstances
				in: Graphics.Rectangle class.
	self setupMethodNameFor: refactoring toReturn: #topAspect.
	self executeRefactoring: refactoring.
	class := refactoring model classFor: Graphics.Rectangle.
	self assert: (class metaclass parseTreeFor: #publishedAspectsOfInstances)
		equals: (self
				parseMethod: 'publishedAspectsOfInstances
	"Answer a <LookupTable> of the <Aspect>s published by instances of the receiver."

	^super publishedAspectsOfInstances
		add: self topAspect;
		add: (Aspect integer: #left);
		add: (Aspect integer: #bottom);
		add: (Aspect integer: #right);
		yourself').
	self assert: (class metaclass parseTreeFor: #topAspect)
		equals: (self parseMethod: 'topAspect
	^Aspect integer: #top').
	self assert: refactoring model changes changes first environment identicalTo: Tools!

testExtractMethodAtEndOfMethodThatNeedsReturn
	| refactoring class |
	refactoring := ExtractMethodRefactoring
				extract: (73 to: 216)
				from: #openEditor
				in: LintRuleTestData.
	self setupMethodNameFor: refactoring toReturn: #foo:.
	self executeRefactoring: refactoring.
	class := refactoring model classNamed: #'Refactory.Browser.Tests.LintRuleTestData'.
	self assert: (class parseTreeFor: #openEditor)
		equals: (self
				parseMethod: 'openEditor
	<namespace: Tools>
	| rules |
	rules := self failedRules.
	^self foo: rules').
	self assert: (class parseTreeFor: #foo:)
		equals: (self
				parseMethod: 'foo: rules
	<namespace: Tools>
	rules isEmpty ifTrue: [^self].
	rules size == 1 ifTrue: [^rules first viewResults].
	SmalltalkSystem current browseEnvironment: self result')!

testExtractMethodThatMovesTemporaryVariable
	| refactoring class |
	refactoring := ExtractMethodRefactoring
				extract: (self convertInterval: (22 to: 280)
						for: (TransformationRuleTestData sourceCodeAt: #superSends))
				from: #superSends
				in: TransformationRuleTestData.
	self setupMethodNameFor: refactoring toReturn: #foo.
	self executeRefactoring: refactoring.
	class := refactoring model classNamed: #'Refactory.Browser.Tests.TransformationRuleTestData'.
	self assert: (class parseTreeFor: #superSends)
		equals: (self parseMethod: 'superSends
	| rule |
	rule := self foo.
	self rewriteUsing: rule').
	self assert: (class parseTreeFor: #foo)
		equals: (self
				parseMethod: 'foo | rule | 	rule := ParseTreeRewriter new.
	rule addSearch: ''super `@message: ``@args''
				-> (
					[:aNode | 
					(class withAllSubclasses
						detect: [:each | each includesSelector: aNode selector]
						ifNone: [nil]) isNil]
							-> ''self `@message: ``@args'').
		^rule')!

testExtractMethodThatNeedsArgument
	| refactoring class |
	refactoring := ExtractMethodRefactoring
				extract: (self convertInterval: (145 to: 343)
						for: (TransformationRuleTestData sourceCodeAt: #checkMethod:))
				from: #checkMethod:
				in: TransformationRuleTestData.
	self setupMethodNameFor: refactoring toReturn: #foo:.
	self executeRefactoring: refactoring.
	class := refactoring model classNamed: #'Refactory.Browser.Tests.TransformationRuleTestData'.
	self assert: (class parseTreeFor: #checkMethod:)
		equals: (self
				parseMethod: 'checkMethod: aSmalllintContext 
	class := aSmalllintContext selectedClass.
	(rewriteRule executeTree: aSmalllintContext parseTree) ifTrue: 
			[self foo: aSmalllintContext]').
	self assert: (class parseTreeFor: #foo:)
		equals: (self
				parseMethod: 'foo: aSmalllintContext (RecursiveSelfRule executeTree: rewriteRule tree initialAnswer: false)
				ifFalse: 
					[builder compile: rewriteRule tree printString
						in: class
						categories: aSmalllintContext protocols]')!

testExtractMethodThatNeedsTemporaryVariable
	| refactoring class |
	refactoring := ExtractMethodRefactoring
				extract: (self convertInterval: (78 to: 197) for: (LintRuleTestData sourceCodeAt: #displayName))
				from: #displayName
				in: LintRuleTestData.
	self setupMethodNameFor: refactoring toReturn: #foo:.
	self executeRefactoring: refactoring.
	class := refactoring model classNamed: #'Refactory.Browser.Tests.LintRuleTestData'.
	self assert: (class parseTreeFor: #displayName)
		equals: (self
				parseMethod: 'displayName
	| nameStream |
	nameStream := WriteStream on: (String new: 64).
	self foo: nameStream.
	^nameStream contents').
	self assert: (class parseTreeFor: #foo:)
		equals: (self
				parseMethod: 'foo: nameStream 	nameStream nextPutAll: self name;
		nextPutAll: '' (''.
	self problemCount printOn: nameStream.
	nameStream nextPut: $).')!

testModelExtractMethodWithTemporariesSelected
	| class refactoring |
	model := RBNamespace new.
	class := model classNamed: self class fullName.
	class compile: 'foo [| temp | temp := 5. temp * temp] value' classified: #accessing.
	refactoring := ExtractMethodRefactoring
				model: model
				extract: (6 to: 36)
				from: #foo
				in: class.
	self setupMethodNameFor: refactoring toReturn: #foobar.
	self executeRefactoring: refactoring.
	self assert: (class parseTreeFor: #foo) equals: (self parseMethod: 'foo [self foobar] value').
	self assert: (class parseTreeFor: #foobar)
		equals: (self parseMethod: 'foobar |temp | temp := 5. ^temp * temp')!

testModelExtractMethodWithTemporaryAssigned
	| class refactoring |
	model := RBNamespace new.
	class := model classNamed: self class fullName.
	class
		compile: 'foo 				| temp bar | 				bar := 5. 				temp := bar * bar. 				Transcript show: temp printString; cr. 				^temp * temp'
		classified: #accessing.
	refactoring := ExtractMethodRefactoring
				model: model
				extract: (26 to: 102)
				from: #foo
				in: class.
	self setupMethodNameFor: refactoring toReturn: #foobar.
	self executeRefactoring: refactoring.
	self assert: (class parseTreeFor: #foo)
		equals: (self parseMethod: 'foo | temp | temp := self foobar. ^temp * temp').
	self
		assert: ((class parseTreeFor: #foobar) equalTo: (self
						parseMethod: 'foobar | bar temp | bar := 5. temp := bar * bar. Transcript show: temp printString; cr. ^temp.')
				exceptForVariables: #())!

testNonExistantSelector
	self shouldFail: (ExtractMethodRefactoring
				extract: (10 to: 20)
				from: #checkClass1:
				in: BasicLintRuleTestData)
		withMessage: 'BasicLintRuleTestData does not define #checkClass1:'! !
!Refactory.Browser.Tests.ExtractMethodTest categoriesForMethods!
testBadInterval!failure tests!public! !
testExtractFailure!failure tests!public! !
testExtractFromMethodInCustomNamespace!failure tests!public! !
testExtractMethodAtEndOfMethodThatNeedsReturn!public!tests! !
testExtractMethodThatMovesTemporaryVariable!public!tests! !
testExtractMethodThatNeedsArgument!public!tests! !
testExtractMethodThatNeedsTemporaryVariable!public!tests! !
testModelExtractMethodWithTemporariesSelected!public!tests! !
testModelExtractMethodWithTemporaryAssigned!public!tests! !
testNonExistantSelector!failure tests!public! !
!

