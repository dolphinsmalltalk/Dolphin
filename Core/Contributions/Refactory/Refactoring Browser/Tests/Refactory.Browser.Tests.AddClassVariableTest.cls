"Filed out from Dolphin Smalltalk"!

Refactory.Browser.Tests.RefactoringTest subclass: #'Refactory.Browser.Tests.AddClassVariableTest'
	instanceVariableNames: ''
	classVariableNames: ''
	imports: #()
	classInstanceVariableNames: ''
	classConstants: {}!
Refactory.Browser.Tests.AddClassVariableTest guid: (Core.GUID fromString: '{f9de1392-b16f-4543-891c-a1831dca7b74}')!
Refactory.Browser.Tests.AddClassVariableTest comment: ''!
!Refactory.Browser.Tests.AddClassVariableTest categoriesForClass!Refactory-Testing! !
!Refactory.Browser.Tests.AddClassVariableTest methodsFor!

setUp
	super setUp.
	model := self abstractVariableTestData!

testAddClassVariable
	| refactoring |
	refactoring := AddClassVariableRefactoring variable: 'Asdf' class: TransformationRuleTestData.
	self executeRefactoring: refactoring.
	self assert: ((refactoring model classNamed: #'Refactory.Browser.Tests.TransformationRuleTestData')
				directlyDefinesClassVariable: #Asdf)!

testAlreadyExistingName
	self
		shouldFail: (AddClassVariableRefactoring variable: #RecursiveSelfRule
					class: TransformationRuleTestData)
			withMessage: 'Refactory.Browser.Tests.TransformationRuleTestData or one of its subclasses defines variable ''RecursiveSelfRule''';
		shouldFail: (AddClassVariableRefactoring variable: self objectClassVariable
					class: TransformationRuleTestData)
			withMessage: 'Refactory.Browser.Tests.TransformationRuleTestData or one of its subclasses defines variable <1p>'
					<< self objectClassVariable!

testMetaClassFailure
	self shouldFail: (AddClassVariableRefactoring variable: #VariableName
				class: TransformationRuleTestData class)
		withMessage: 'Refactory.Browser.Tests.TransformationRuleTestData class is a metaclass'!

testModelAddClassVariable
	| refactoring |
	refactoring := AddClassVariableRefactoring
				model: model
				variable: #ClassVarName3
				class: (model classNamed: #Bar).
	self executeRefactoring: refactoring.
	self assert: ((model classNamed: #Bar) directlyDefinesClassVariable: #ClassVarName3)!

testModelAlreadyExistingName
	| refactoring |
	refactoring := AddClassVariableRefactoring
				model: model
				variable: #ClassVarName2
				class: (model classNamed: #Bar).
	self shouldFail: refactoring
		withMessage: 'Bar or one of its subclasses defines variable ''ClassVarName2'''!

testModelMetaclassFailure
	| refactoring |
	refactoring := AddClassVariableRefactoring
				model: model
				variable: #ClassVarName3
				class: (model classNamed: #Bar) metaclass.
	self shouldFail: refactoring withMessage: 'Bar class is a metaclass'!

testRBMetaclassFailure
	| refactoring |
	refactoring := AddClassVariableRefactoring variable: #ClassVarName3
				class: (model classNamed: #'Core.Object') metaclass.
	refactoring model: model.
	self shouldFail: refactoring withMessage: 'Core.Object class is a metaclass'! !
!Refactory.Browser.Tests.AddClassVariableTest categoriesForMethods!
setUp!public!set up! !
testAddClassVariable!public!tests! !
testAlreadyExistingName!failure tests!public! !
testMetaClassFailure!failure tests!public! !
testModelAddClassVariable!public!tests! !
testModelAlreadyExistingName!failure tests!public! !
testModelMetaclassFailure!failure tests!public! !
testRBMetaclassFailure!failure tests!public! !
!

