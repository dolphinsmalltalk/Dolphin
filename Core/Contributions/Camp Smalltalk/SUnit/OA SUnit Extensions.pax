| package |
package := Package name: 'OA SUnit Extensions'.
package paxVersion: 1;
	preDeclareClassesOnLoad: false;
	basicComment: 'Object Arts SUnit Extensions.
Copyright (c) Object Arts Ltd, 2002.

Extensions to the basic SUnit unit testing packages, including the ability to test that the correct set of SASE events are fired for particular actions.'.

package methodNames
	add: #TestCase -> #<=;
	add: #TestCase -> #=;
	add: #TestCase -> #assert:closeTo:;
	add: #TestCase -> #assert:closeTo:precision:;
	add: #TestCase -> #assert:equals:description:;
	add: #TestCase -> #assert:identicalTo:;
	add: #TestCase -> #assert:isKindOf:;
	add: #TestCase -> #assert:sameAs:;
	add: #TestCase -> #assertIsNil:;
	add: #TestCase -> #assertNotNil:;
	add: #TestCase -> #debugWithResult;
	add: #TestCase -> #debugWithResult:;
	add: #TestCase -> #deny:equals:;
	add: #TestCase -> #deny:identicalTo:;
	add: #TestCase -> #deny:sameAs:;
	add: #TestCase -> #denyIsNil:;
	add: #TestCase -> #hash;
	add: #TestCase -> #isCiBuild;
	add: #TestCase -> #should:raise:matching:;
	add: #TestCase -> #should:trigger:against:;
	add: #TestCase -> #should:trigger:butNot:against:;
	add: #TestCase -> #should:trigger:matching:against:;
	add: #TestCase -> #should:triggerAllOf:against:;
	add: #TestCase -> #should:triggerAllOf:matching:against:;
	add: #TestCase -> #should:triggerAllOf:matching:butNoneOf:against:;
	add: #TestCase -> #shouldnt:trigger:against:;
	add: #TestCase -> #shouldnt:triggerAnyOf:against:;
	add: #TestCase -> #skipIfCiBuild;
	add: #TestCase -> #skipIfWin32;
	add: #TestCase -> #skipUnless:;
	add: #TestCase -> #thisTestMethod;
	add: #TestResult -> #debugCase:;
	add: 'TestCase class' -> #buildSuiteFromConcreteMethods:;
	add: 'TestCase class' -> #debugWithResult:;
	yourself.

package binaryGlobalNames: (Set new
	yourself).

package globalAliases: (Set new
	yourself).

package setPrerequisites: #(
	'..\..\..\Object Arts\Dolphin\Base\Dolphin'
	'SUnit').

package!

"Loose Methods"!

!TestCase methodsFor!

<= aTestCase
	"Answer whether the receiver is 'less than' the <TestCase> argument, for collation purposes only."

	^self class == aTestCase class
		ifTrue: [self selector <= aTestCase selector]
		ifFalse: [self class <= aTestCase class]!

= aTestCase 
	"Answer whether the argument, aTestCase, is considered exactly equivalent to the receiver."

	^self class == aTestCase class and: [self selector == aTestCase selector]!

assert: actualNumber closeTo: expectedNumber
	(actualNumber closeTo: expectedNumber)
		ifFalse: [self fail: (self comparingStringBetween: expectedNumber and: actualNumber)]!

assert: actualNumber closeTo: expectedNumber precision: precisionNumber
	(actualNumber closeTo: expectedNumber precision: precisionNumber)
		ifFalse: [self fail: (self comparingStringBetween: expectedNumber and: actualNumber)]!

assert: actualObject equals: expectedObject description: aStringOrValue
	expectedObject = actualObject
		ifFalse: 
			[self fail: (self getDescription: aStringOrValue) , ': '
						, (self comparingStringBetween: expectedObject and: actualObject)]!

assert: actualObject identicalTo: expectedObject
	expectedObject == actualObject
		ifFalse: [self fail: (self comparingStringBetween: expectedObject and: actualObject)]!

assert: actualObject isKindOf: expectedType
	(actualObject isKindOf: expectedType)
		ifFalse: 
			[self fail: ((String writeStream: 100)
						nextPutAll: 'Expected a ';
						print: expectedType;
						nextPutAll: ' Actual: ';
						print: actualObject class;
						nextPutAll: '.';
						contents)]!

assert: actualObject sameAs: expectedObject
	Notification deprecated.
	self assert: actualObject identicalTo: expectedObject!

assertIsNil: anObject
	anObject
		ifNotNil: 
			[self fail: (String writeStream
						nextPutAll: 'Expected nil, not ';
						print: anObject;
						nextPutAll: '.';
						contents)]!

assertNotNil: anObject
	anObject ifNil: [self fail: 'Expected non-nil object']!

debugWithResult
	| result |
	result := TestResult new.
	self debugWithResult: result.
	^result!

debugWithResult: aResult
	aResult debugCase: self!

deny: actualObject equals: expectedObject
	expectedObject ~= actualObject
		ifFalse: 
			[self fail: (String writeStream
						nextPutAll: 'Unexpectedly equal to ';
						print: expectedObject;
						nextPutAll: ' Actual: ';
						print: actualObject;
						nextPutAll: '.';
						contents)]!

deny: actualObject identicalTo: expectedObject
	expectedObject ~~ actualObject
		ifFalse: 
			[self fail: (String writeStream
						nextPutAll: 'Did not expect ';
						print: actualObject;
						nextPutAll: '.';
						contents)]!

deny: actualObject sameAs: expectedObject
	Notification deprecated.
	self deny: actualObject identicalTo: expectedObject!

denyIsNil: anObject
	anObject ifNil: [self fail: 'Expected non-nil result']!

hash
	"Answer the <SmallInteger> hash value for the receiver. We use only the selector at the moment."

	^testSelector hash!

isCiBuild
	^(SessionManager current getenv: 'APPVEYOR') notNil!

should: aBlock raise: anException matching: discriminator
	"Example usage:
		self should: [array at: $1] raise: Error matching: [:ex | ex messageText = 'Index: $1 is not an integer'].
	"

	| raised |
	raised := false.
	aBlock on: anException
		do: 
			[:exception | 
			(discriminator value: exception) ifTrue: [raised := true].
			"If the exception is resumable, such as a warning, then allow execution of the block to continue"
			exception exit: nil].
	self assert: raised!

should: aZeroArgBlock trigger: aSymbol against: anObject 
	"Example Usage:
		self 
			should: [Object trigger: #testNoArgs]
			trigger: #testNoArgs
			against: Object.
	"

	self 
		should: aZeroArgBlock
		triggerAllOf: (Array with: aSymbol)
		against: anObject!

should: aBlock trigger: eventSymbol1 butNot: eventSymbol2 against: anObject 
	"Example usage:
		self 
			should: [presenter selection: each]
			trigger: #selectionChanged
			butNot: #selectionChanging:
			against: presenter.
	"

	self 
		should: aBlock
		triggerAllOf: (Array with: eventSymbol1)
		matching: [:message | true]
		butNoneOf: (Array with: eventSymbol2)
		against: anObject!

should: aZeroArgBlock trigger: aSymbol matching: discriminatorBlock against: anObject
	"Example usage:
		self 
			should: [Object trigger: #testTwoArgs:arg2:]
			trigger: #testTwoArgs:arg2:
			matching: [:arg1 :arg2 | arg1 isNil & arg2 isNil]
			against: Object.
	"

	self assert: aSymbol argumentCount equals: discriminatorBlock argumentCount.
	self
		should: aZeroArgBlock
		triggerAllOf: (Array with: aSymbol)
		matching: [:message | discriminatorBlock valueWithArguments: message arguments]
		against: anObject!

should: aZeroArgBlock triggerAllOf: aCollectionOfSymbols against: anObject 
	self 
		should: aZeroArgBlock
		triggerAllOf: aCollectionOfSymbols
		matching: [:message | true]
		against: anObject!

should: aZeroArgBlock triggerAllOf: aCollectionOfSymbols matching: aOneArgBlockOrNil against: anObject 
	self 
		should: aZeroArgBlock
		triggerAllOf: aCollectionOfSymbols
		matching: aOneArgBlockOrNil
		butNoneOf: #()
		against: anObject!

should: aZeroArgBlock triggerAllOf: aCollectionOfSymbols matching: aOneArgBlock butNoneOf: aCollectionOfSymbols2 against: anObject
	| fired allEvents |
	fired := Bag new.
	allEvents := aCollectionOfSymbols union: aCollectionOfSymbols2.
	"The expected and unexpected event sets should be disjoint"
	self assert: allEvents size equals: aCollectionOfSymbols size + aCollectionOfSymbols2 size.
	
	[allEvents do: 
			[:each |
			"If the event selector is part of nil's protocol, then no MNU will be raised, 
			and the test will be invalid"
			self deny: (nil respondsTo: each).
			anObject when: each sendTo: nil].
	aZeroArgBlock on: MessageNotUnderstood
		do: 
			[:mnu |
			(mnu receiver ~~ nil or: [(allEvents includes: mnu selector) not]) ifTrue: [mnu pass].
			((aCollectionOfSymbols2 includes: mnu selector) or: [aOneArgBlock value: mnu message])
				ifTrue: [fired add: mnu selector].
			mnu resume: mnu receiver]]
			ensure: [anObject removeEventsTriggeredFor: nil].
	"If this assertion fails, then the object did not trigger one or more of the expected events"
	self assert: (aCollectionOfSymbols difference: fired) isEmpty.
	"If this assertion fails, then the object triggered one or more events that it should not have"
	self assert: (aCollectionOfSymbols2 intersection: fired) isEmpty!

shouldnt: aBlock trigger: aSymbol against: anObject 
	"Example usage:
		self 
			shouldnt: [presenter selection: each]
			trigger: #selectionChanging:
			against: presenter.
	"

	self 
		shouldnt: aBlock
		triggerAnyOf: (Array with: aSymbol)
		against: anObject!

shouldnt: aZeroArgBlock triggerAnyOf: aCollectionOfSymbols against: anObject 
	"Example usage:
		self 
			shouldnt: [presenter model remove: objects last]
			triggerAnyOf: #(#selectionChanged #selectionChanging:)
			against: presenter.
	"

	self 
		should: aZeroArgBlock
		triggerAllOf: #()
		matching: [:message | true]
		butNoneOf: aCollectionOfSymbols
		against: anObject!

skipIfCiBuild
	self skipUnless: [self isCiBuild not]!

skipIfWin32
	"Skip if running on 32-bit Windows"

	self skipUnless: [KernelLibrary default isWow64]!

skipUnless: aNiladicValuable
	" If the assumption in the <niladicValuable> argument evaluates to false, abandon the running test and mark it as skipped."
	
	aNiladicValuable value ifFalse: [TestSkip signal: 'Assumption in #skipUnless: failed']!

thisTestMethod
	^self class lookupMethod: self selector! !

!TestCase categoriesForMethods!
<=!comparing!public! !
=!comparing!public! !
assert:closeTo:!asserting!public! !
assert:closeTo:precision:!asserting!public! !
assert:equals:description:!asserting!public! !
assert:identicalTo:!asserting!public! !
assert:isKindOf:!asserting!public! !
assert:sameAs:!asserting!public! !
assertIsNil:!asserting!public! !
assertNotNil:!asserting!public! !
debugWithResult!public!running! !
debugWithResult:!private!running! !
deny:equals:!asserting!public! !
deny:identicalTo:!public! !
deny:sameAs:!public! !
denyIsNil:!asserting!public! !
hash!comparing!public! !
isCiBuild!helpers!public! !
should:raise:matching:!asserting!public! !
should:trigger:against:!asserting!public! !
should:trigger:butNot:against:!asserting!public! !
should:trigger:matching:against:!asserting!public! !
should:triggerAllOf:against:!asserting!public! !
should:triggerAllOf:matching:against:!asserting!public! !
should:triggerAllOf:matching:butNoneOf:against:!asserting!public! !
shouldnt:trigger:against:!asserting!public! !
shouldnt:triggerAnyOf:against:!asserting!public! !
skipIfCiBuild!helpers!public! !
skipIfWin32!helpers!public! !
skipUnless:!asserting!public! !
thisTestMethod!asserting!public! !
!

!TestCase class methodsFor!

buildSuiteFromConcreteMethods: testMethodSelectors
	^self isAbstract
		ifTrue: 
			[| suite |
			suite := TestSuite new.
			suite name: self displayString.
			self allSubclassesDo: 
					[:each |
					each isAbstract
						ifFalse: 
							[suite
								addTest: (each buildSuiteFromMethods: (testMethodSelectors intersection: each allTestSelectors))]].
			suite]
		ifFalse: [self buildSuiteFromMethods: testMethodSelectors]!

debugWithResult: aSymbol
	^(self selector: aSymbol) debugWithResult! !

!TestCase class categoriesForMethods!
buildSuiteFromConcreteMethods:!Instance Creation!public! !
debugWithResult:!Instance Creation!public! !
!

!TestResult methodsFor!

debugCase: aTestCase
	"Same as runCase:, but must pass out an errors so caught by the debugger"

	
	[| start stop |
	start := Delay microsecondClockValue.
	aTestCase runCase.
	stop := Delay microsecondClockValue.
	self addPass: aTestCase duration: stop - start]
			on: self class failure
			do: 
				[:signal |
				self addFailure: aTestCase signal: signal.
				signal pass]
			on: self class error
			do: 
				[:signal |
				self addError: aTestCase signal: signal.
				signal pass]
			on: self class skip
			do: 
				[:signal |
				self addSkip: aTestCase signal: signal.
				signal return]! !

!TestResult categoriesForMethods!
debugCase:!public!running! !
!

"End of package definition"!

