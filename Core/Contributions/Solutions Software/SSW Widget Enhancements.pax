| package |
package := Package name: 'SSW Widget Enhancements'.
package paxVersion: 1;
	basicComment: '© Solutions Software/John Aspinall 2018
Provided under the same licence terms as Dolphin Smalltalk 7'.

package basicPackageVersion: '7.1.0'.

package classNames
	add: #EmulatedPushButton;
	add: #FormattedTextEdit;
	add: #IconWithExtent;
	add: #OwnerDrawnButton;
	add: #TextAndImageButton;
	yourself.

package methodNames
	add: #Canvas -> #clipTo:during:;
	add: #Canvas -> #erase:;
	add: #Canvas -> #erase:color:;
	add: #Canvas -> #stateText:at:flags:;
	add: #ComboBox -> #droppedWidth;
	add: #ComboBox -> #droppedWidth:;
	add: #ComboBox -> #hasCueBanner;
	add: #Date -> #shortString;
	add: #Icon -> #asSmallIcon;
	add: #Icon -> #withExtent:;
	add: #KeyEvent -> #resendTo:;
	add: #Menu -> #showIn:position:rightAlign:;
	add: #NullConverter -> #isNullConverter;
	add: #PointEvent -> #resendTo:;
	add: #PushButton -> #alignment;
	add: #PushButton -> #alignment:;
	add: #PushButton -> #isRightButton;
	add: #PushButton -> #isRightButton:;
	add: #PushButton -> #tipText;
	add: #PushButton -> #tipText:;
	add: #PushButton -> #ttnGetDispInfo:;
	add: #PushButton -> #wmNotify:wParam:lParam:;
	add: #SystemMetrics -> #hasListViewAlphaBlendedHighlights;
	add: #SystemMetrics -> #hasListViewHotTracking;
	add: #TextEdit -> #hasCueBanner;
	add: #ThemeLibrary -> #endBufferedAnimation:fUpdateTarget:;
	add: #ThemeLibrary -> #getThemeTransitionDuration:iPartId:iStateIdFrom:iStateIdTo:iPropId:pdwDuration:;
	add: #Toolbar -> #addItem:after:;
	add: #Toolbar -> #addItem:before:;
	add: #Toolbar -> #itemWithCommand:;
	add: #Toolbar -> #itemWithCommand:ifNone:;
	add: #ToolbarButton -> #isAutoSize;
	add: #ToolbarButton -> #isAutoSize:;
	add: #TypeConverter -> #isNullConverter;
	add: #View -> #addToolTipWithHandle:;
	add: #View -> #getUICuesState;
	add: #View -> #setUICuesState:;
	add: #View -> #shouldShowFocusRect;
	add: #View -> #shouldShowFocusRect:;
	add: #View -> #shouldShowKeyboardAccelerators;
	add: #View -> #tabPrevious;
	add: #WinImageList -> #draw:on:at:extent:backcolor:forecolor:flags:;
	add: 'Color class' -> #buttonShadow;
	yourself.

package binaryGlobalNames: (Set new
	yourself).

package globalAliases: (Set new
	yourself).

package setPrerequisites: #(
	'..\..\Object Arts\Dolphin\Base\Dolphin'
	'..\..\Object Arts\Dolphin\MVP\Views\Common Controls\Dolphin Common Controls'
	'..\..\Object Arts\Dolphin\MVP\Views\Control Bars\Dolphin Control Bars'
	'..\..\Object Arts\Dolphin\Base\Dolphin Legacy Date & Time'
	'..\..\Object Arts\Dolphin\MVP\Presenters\List\Dolphin List Presenter'
	'..\..\Object Arts\Dolphin\MVP\Base\Dolphin MVP Base'
	'..\..\Object Arts\Dolphin\MVP\Presenters\Text\Dolphin Text Presenter'
	'..\..\Object Arts\Dolphin\MVP\Views\Tooltips\Dolphin Tooltips'
	'..\..\Object Arts\Dolphin\MVP\Type Converters\Dolphin Type Converters').

package!

"Class Definitions"!

Icon subclass: #IconWithExtent
	instanceVariableNames: 'extent'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!

PushButton subclass: #OwnerDrawnButton
	instanceVariableNames: 'displayState transitionDurations shouldShowKeyboardCues animationEndTime setFocusTime'
	classVariableNames: ''
	poolDictionaries: 'ThemeConstants'
	classInstanceVariableNames: ''!

OwnerDrawnButton subclass: #TextAndImageButton
	instanceVariableNames: 'imageExtent imageManager imageIndex dropDownMenu isMenuOpen'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!

TextAndImageButton subclass: #EmulatedPushButton
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!

TextEdit subclass: #FormattedTextEdit
	instanceVariableNames: 'isFormatting messageBubble format separatorChars placeholderChar'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!

"Loose Methods"!

!Canvas methodsFor!

clipTo: aRectangle during: aBlock

	| oldDC |

	oldDC := self save.
	[self intersectClipRectangle: aRectangle.
	aBlock value] ensure: [self restore: oldDC]!

erase: aRectangle
	"Erase the receiver to the current background colour"

	self erase: aRectangle color: self backcolor!

erase: aRectangle color: aColor
	"Erase the receiver to the current background colour"

	self fillRectangle: aRectangle brush: (Brush color: aColor)!

stateText: aString at: aPoint flags: anInteger
	| text |
	text := aString asUtf16String.
	^UserLibrary default
		drawState: self asParameter
		hbr: nil
		lpOutputFunc: 0
		lData: text
		wData: text size
		x: aPoint x y: aPoint y
		cx: 0 cy: 0
		fuFlags: anInteger! !

!Canvas categoriesForMethods!
clipTo:during:!drawing!public! !
erase:!drawing!public! !
erase:color:!drawing!public! !
stateText:at:flags:!drawing!public! !
!

!Color class methodsFor!

buttonShadow
	#deprecated.
	^self shadow3d! !

!Color class categoriesForMethods!
buttonShadow!constants!public! !
!

!ComboBox methodsFor!

droppedWidth

	^self sendMessage: CB_GETDROPPEDWIDTH!

droppedWidth: anInteger

	self sendMessage: CB_SETDROPPEDWIDTH wParam: anInteger!

hasCueBanner
	"Answer whether the receiver has a textual cue, displayed when the text box is empty."

	| buf |
	buf := Utf16String newFixed: 1.
	^(self
		sendMessage: CB_GETCUEBANNER
		wParam: buf yourAddress
		lParam: buf size + 1) ~~ 0
		and: [buf first ~~ $\0]! !

!ComboBox categoriesForMethods!
droppedWidth!hierarchy!public!sub views! !
droppedWidth:!hierarchy!public!sub views! !
hasCueBanner!public!testing! !
!

!Date methodsFor!

shortString

	| stream |

	stream := WriteStream on: (Utf8String new: 10).

	self printOn: stream longPicture: false.

	^stream contents! !

!Date categoriesForMethods!
shortString!printing!public! !
!

!Icon methodsFor!

asSmallIcon

	^self withExtent: 16@16!

withExtent: aPoint

	^IconWithExtent new
		fileLocator: self fileLocator;
		identifier: self identifier;
		instanceHandle: self instanceHandle;
		extent: aPoint;
		yourself! !

!Icon categoriesForMethods!
asSmallIcon!converting!public! !
withExtent:!converting!public! !
!

!KeyEvent methodsFor!

resendTo: aView

	"Send this message onto aView"

	aView 
		sendMessage: message 
		wParam: wParam 
		lParam: lParam! !

!KeyEvent categoriesForMethods!
resendTo:!public!testing! !
!

!Menu methodsFor!

showIn: aView position: aPos rightAlign: aBoolean
	"Show top level of context menu on screen at position aPos."

	^UserLibrary default
		trackPopupMenu: self asParameter
		uFlags: ((aBoolean ifTrue: [TPM_RIGHTALIGN] ifFalse: [TPM_LEFTALIGN]) | TPM_RIGHTBUTTON)
		x: aPos x
		y: aPos y
		nReserved: 0
		hWnd: aView asParameter
		prcRect: nil! !

!Menu categoriesForMethods!
showIn:position:rightAlign:!drawing!public! !
!

!NullConverter methodsFor!

isNullConverter

	^true! !

!NullConverter categoriesForMethods!
isNullConverter!public!testing! !
!

!PointEvent methodsFor!

resendTo: aView

	"Send this message onto aView, translating the position"

	| newPoint newLParam |

	newLParam := DWORD fromInteger: lParam.
	newPoint := self window mapPoint: self position to: aView.
	newLParam 
		lowSWord: newPoint x;
		highSWord: newPoint y.

	aView 
		sendMessage: message 
		wParam: wParam 
		lParam: newLParam asInteger! !

!PointEvent categoriesForMethods!
resendTo:!accessing!private! !
!

!PushButton methodsFor!

alignment

	| style |

	style := self baseStyle bitAnd: BS_CENTER.

	style = BS_LEFT ifTrue: [^#left].
	style = BS_CENTER ifTrue: [^#center].
	style = BS_RIGHT ifTrue: [^#right].

	^#center!

alignment: aSymbol

	| mask |

	self baseStyleMask: BS_CENTER set: false.

	mask := 
		##(Dictionary new 
			at: #left put: BS_LEFT;
			at: #right put: BS_RIGHT;
			at: #center put: BS_CENTER;
			yourself) at: aSymbol ifAbsent: [BS_CENTER].

	self baseStyleMask: mask set: true!

isRightButton

	^(self baseStyle bitAnd: BS_RIGHTBUTTON) ~= 0!

isRightButton: aBoolean

	self baseStyleMask: BS_RIGHTBUTTON set: aBoolean!

tipText

	^self propertyAt: #tipText ifAbsent: [nil]!

tipText: aString

	aString isNil
		ifTrue: [self removePropertyAt: #tipText ifAbsent: []; removePropertyAt: #tipTextBuffer ifAbsent: []]
		ifFalse: [self propertyAt: #tipText put: aString]!

ttnGetDispInfo: anNMTTDISPINFOW
	"Private - Handler for the TTN_GETDISPINFOW notification message."

	^(anNMTTDISPINFOW idIsHandle
		and: [anNMTTDISPINFOW idFrom = self handle asInteger and: [self tipText notNil]])
			ifTrue: 
				[anNMTTDISPINFOW text: self tipText.
				"Note that we must keep a reference to the text string to prevent its premature death"
				anNMTTDISPINFOW lpszText ifNotNil: [:text | self propertyAt: #tipTextBuffer put: text].
				0]
			ifFalse: [super ttnGetDispInfo: anNMTTDISPINFOW]!

wmNotify: message wParam: wParam lParam: lParam
	"Private - Redirects WM_NOTIFY messages to the appropriate control as a notify message.
	Overridden here to handle notifications from the receiver's tool tip control which 
	does not itself have an associated View so cannot receive the redirected message.
	In any case it is more convenient to direct the notification to the receiver's
	#ttnNeedText: handler."

	| pnmhdr code |
	pnmhdr := lParam asExternalAddress.
	code := pnmhdr sdwordAtOffset: 8.
	(code == TTN_GETDISPINFOW or: [code == TTN_GETDISPINFOA]) ifTrue: [^self nmNotify: pnmhdr].
	^super wmNotify: message wParam: wParam lParam: lParam! !

!PushButton categoriesForMethods!
alignment!accessing-styles!public! !
alignment:!accessing-styles!public! !
isRightButton!accessing-styles!public! !
isRightButton:!accessing-styles!public! !
tipText!accessing!public! !
tipText:!accessing!public! !
ttnGetDispInfo:!event handling-win32!private! !
wmNotify:wParam:lParam:!event handling-win32!private! !
!

!SystemMetrics methodsFor!

hasListViewAlphaBlendedHighlights
	"Does the host support alpha-blended list view selection highlights?"

	^VMLibrary default isWindowsVistaOrGreater!

hasListViewHotTracking
	"Does the host support hot tracking in list views?"

	^VMLibrary default isWindowsVistaOrGreater! !

!SystemMetrics categoriesForMethods!
hasListViewAlphaBlendedHighlights!capability enquiries!public! !
hasListViewHotTracking!capability enquiries!public! !
!

!TextEdit methodsFor!

hasCueBanner
	"Answer whether the receiver has a textual cue, displayed when the text box is empty."

	| buf |
	buf := Utf16String newFixed: 1.
	^(self
		sendMessage: EM_GETCUEBANNER
		wParam: buf yourAddress
		lParam: buf size + 1) ~~ 0
		and: [buf first ~~ $\0]! !

!TextEdit categoriesForMethods!
hasCueBanner!public!testing! !
!

!ThemeLibrary methodsFor!

endBufferedAnimation: hBufferedPaint fUpdateTarget: fUpdateTarget
	"Invoke the EndBufferedAnimation() function of the module wrapped by the receiver.
	Helpstring: Ends a buffered animation operation.

		HRESULT __stdcall EndBufferedAnimation(
			[in]HANDLE hBufferedPaint,
			[in]BOOL fUpdateTarget);"

	<stdcall: hresult EndBufferedAnimation handle bool>
	^self invalidCall: _failureCode!

getThemeTransitionDuration: htheme iPartId: iPartId iStateIdFrom: iStateIdFrom iStateIdTo: iStateIdTo iPropId: iPropId pdwDuration: pdwDuration
	"Gets the duration for the specified transition.
	HRESULT GetThemeTransitionDuration(
		HTHEME hTheme,
		int    iPartId,
		int    iStateIdFrom,
		int    iStateIdTo,
		int    iPropId,
	_Out_ DWORD  *pdwDuration
	);"

	<stdcall: hresult GetThemeTransitionDuration handle sdword sdword sdword sdword sdword*>
	^self invalidCall: _failureCode! !

!ThemeLibrary categoriesForMethods!
endBufferedAnimation:fUpdateTarget:!public! !
getThemeTransitionDuration:iPartId:iStateIdFrom:iStateIdTo:iPropId:pdwDuration:!public! !
!

!Toolbar methodsFor!

addItem: tool after: anotherTool
	"Add the <ToolbarItem>, tool, to the receiver placing it after anotherTool. Answer the new tool."

	(idMap includesKey: tool commandId) ifTrue: [^nil].

	items := 
		(OrderedCollection withAll: items) 
			add: tool after: anotherTool;
			yourself.
	tool toolbar: self.

	self basicAddItems: (Array with: tool).
	self updateAllItems.
	^tool!

addItem: tool before: anotherTool
	"Add the <ToolbarItem>, tool, to the receiver placing it before anotherTool. Answer the new tool."

	(idMap includesKey: tool commandId) ifTrue: [^nil].

	items := 
		(OrderedCollection withAll: items) 
			add: tool before: anotherTool;
			yourself.
	tool toolbar: self.

	self basicAddItems: (Array with: tool).
	self updateAllItems.
	^tool!

itemWithCommand: aSymbol

	^self itemWithCommand: aSymbol ifNone: [self error: aSymbol, ' not found']!

itemWithCommand: aSymbol ifNone: aBlock

	^items detect: [ :each | each command ifNil: [false] ifNotNil: [ :cmd | cmd asSymbol = aSymbol]] ifNone: aBlock! !

!Toolbar categoriesForMethods!
addItem:after:!adding!public! !
addItem:before:!adding!public! !
itemWithCommand:!accessing!public! !
itemWithCommand:ifNone:!accessing!public! !
!

!ToolbarButton methodsFor!

isAutoSize

	^self style allMask: BTNS_AUTOSIZE!

isAutoSize: aBoolean
	"Sets the receiver to automatically size itself to accomodate its contents according to aBoolean"

	self style: (self style mask: BTNS_AUTOSIZE set: aBoolean)! !

!ToolbarButton categoriesForMethods!
isAutoSize!public!style! !
isAutoSize:!public!style! !
!

!TypeConverter methodsFor!

isNullConverter

	^false! !

!TypeConverter categoriesForMethods!
isNullConverter!public!testing! !
!

!View methodsFor!

addToolTipWithHandle: toolTipHandle
	"Add the specified tool tip to this view (typically but not necessarily a Button).
	The receiver will need to override wmNotify:wParam:lParam: to intercept requests for tip text (see PushButton extension in this package).
	Borrowed from cdemers 2/26/2003, based on code from Steve Waring and mm_aa."

	| toolInfo |
	(toolInfo := TOOLINFOW new)
		uFlags: ##(TooltipConstants.TTF_IDISHWND | TooltipConstants.TTF_SUBCLASS);
		hwnd: self handle;
		uId: self handle;
		textCallback.
	UserLibrary default
		sendMessage: toolTipHandle
		msg: TooltipConstants.TTM_ADDTOOLW
		wParam: 0
		lParam: toolInfo yourAddress.
	"cdemers - 2/26/2003 Allow multiline."
	UserLibrary default
		sendMessage: toolTipHandle
		msg: TooltipConstants.TTM_SETMAXTIPWIDTH
		wParam: 0
		lParam: 500.
	UserLibrary default
		sendMessage: toolTipHandle
		msg: TooltipConstants.TTM_SETDELAYTIME
		wParam: TooltipConstants.TTDT_AUTOPOP
		lParam: 20000.	"Default seems to be 5000 (5 secs) - this is a bit more reasonable"
	^toolTipHandle!

getUICuesState

	^self sendMessage: WM_QUERYUISTATE wParam: 0 lParam: 0!

setUICuesState: anInteger

	^self sendMessage: WM_CHANGEUISTATE wParam: anInteger lParam: 0!

shouldShowFocusRect

	^self getUICuesState noMask: UISF_HIDEFOCUS!

shouldShowFocusRect: aBoolean

	self setUICuesState: (##(UISF_HIDEFOCUS bitShift: 16) bitOr: (aBoolean ifTrue: [UIS_CLEAR] ifFalse: [UIS_SET]))!

shouldShowKeyboardAccelerators

	^self getUICuesState noMask: UISF_HIDEACCEL!

tabPrevious
	"Tab to the control before the receiver in the tab order of the parent."

	| parent previous |
	parent := self parentView.
	[parent notNil and: [previous isNil]] whileTrue: [
		previous := parent tabNextSiblingFrom: self forward: false.
		parent := parent parentView].
	previous isNil ifFalse: [previous tabFocus]! !

!View categoriesForMethods!
addToolTipWithHandle:!operations!public! !
getUICuesState!accessing!private! !
setUICuesState:!accessing!private! !
shouldShowFocusRect!public!testing! !
shouldShowFocusRect:!public!testing! !
shouldShowKeyboardAccelerators!public!testing! !
tabPrevious!commands!public! !
!

!WinImageList methodsFor!

draw: imageIndexInteger on: aCanvas at: aPoint extent: exPoint backcolor: backColor forecolor: foreColor flags: flagsInteger 
	"Draw the image at one-based <integer> index, imageIndexInteger, in the receiver onto the
	<Canvas>, aCanvas, at the <Point> position, aPoint, using the foreground and background
	<Color>s foreColor and backColor respectively, with <integer> style flags, flagsInteger."

	^CommCtrlLibrary default 
		imageList_DrawEx: self asParameter
		i: imageIndexInteger - 1
		hdcDst: aCanvas asParameter
		x: aPoint x
		y: aPoint y
		dx: exPoint x
		dy: exPoint y
		rgbBk: (backColor isNil ifTrue: [CLR_NONE] ifFalse: [backColor asCOLORREF])
		rgbFg: (foreColor isNil ifTrue: [CLR_DEFAULT] ifFalse: [foreColor asCOLORREF])
		fStyle: flagsInteger! !

!WinImageList categoriesForMethods!
draw:on:at:extent:backcolor:forecolor:flags:!drawing-bitmaps!public! !
!

"End of package definition"!

